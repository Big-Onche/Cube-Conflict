///////////////////////////////////////////////////////////////////////////////
//  Options                                                                  //
///////////////////////////////////////////////////////////////////////////////
UI_opttabs = 0
UI_optc1wA = 0.025
UI_optc1wB = 0.270
UI_optc1w  = (+f $UI_optc1wA $UI_optc1wB)
UI_optc2w  = 0.363
UI_optrowh = 0.032
UI_optbar  = [
   uitablerow [
      uifill $UI_optc1w 0.020 [UIbar 1]
      uifill $UI_optc2w 0.020 [UIbar 1]
      uialign* -1
   ]
]
UI_ratio4:3 = [
   "1024 768"
   "1152 864"
   "1280 960"
   "1400 1050"
   "1600 1200"
   "1920 1440"
   "2560 1920"
]
UI_ratio5:4 = [
   "1280 1024"
   "1440 1152"
   "1600 1280"
   "1920 1536"
   "2560 2048"
]
UI_ratio16:9 = [
   "1024 600"
   "1280 720"
   "1366 768"
   "1600 900"
   "1920 1080"
   "2048 1152"
   "2560 1440"
   "2880 1620"
   "3840 2160"
]
UI_ratio16:10 = [
   "1024 640"
   "1280 800"
   "1440 900"
   "1680 1050"
   "1920 1200"
   "2560 1600"
]
opt_ratios = [4:3 5:4 16:9 16:10]
opt_ratios_assoc = ((looplistconcat ratio $opt_ratios [concat $ratio $ratio]))
UI_findratio = [
   looplist ratio $opt_ratios [
      looplist res $[UI_ratio@ratio] [
         if (=s $res $UI_resolution) [UI_aspectratio = $ratio]
      ]
   ]
]

setting_names_fr = ["Jouabilité" "Graphismes" "Affichage" "Audio" "Contrôles" "Console"]
setting_names_en = ["Gameplay" "Graphics" "Display" "Audio" "Controls" "Console"]
setting_ico = ["media/interface/hud/souris.png" "media/interface/hud/pixel.png" "media/interface/hud/comp.jpg" "media/interface/hud/hp.png" "media/interface/hud/souris.png" "media/interface/hud/comp.jpg"]

sel_setting = 0
infotxt = ""

UImenu "settings" [
	infotxt = ""
	uivgradient 0xEEEEEE 0xBBBBBB 0.9 0.7 [
		uihlist 0 [
			uispace 0.005
			setting_num = 0
			uivlist 0 [
				loop i 6 [
					uihlist 0 [
						uihover [
							setgradx = 0.24 ; setgrady = 0.11 ; imgsize = 0.09 ; txtsize = 0.65 ; tabcola = 0xCCCCAA ; tabcolb = 0xAAAA88][
							setgradx = 0.23 ; setgrady = 0.10 ; imgsize = 0.08 ; txtsize = 0.6 ; tabcola = 0x9F9F9F ; tabcolb = 0x7F7F7F
						]
						uitarget 0.24 0.11 [
							if (= $setting_num $sel_setting)[tabcola = 0xAACCAA ; tabcolb = 0x88AA88]
							uivgradient $tabcola $tabcolb $setgradx $setgrady [
								uihlist 0 [
									uispace 0.003
									if @GAME_LANG [setting_name = (at $setting_names_en $setting_num )][setting_name = (at $setting_names_fr $setting_num )]
									uiimage (at $setting_ico $setting_num) $imgsize $imgsize
									uispace 0.003
									uicolortext $setting_name 0xFFFFFF $txtsize
								]
								uialign* -1
							]
							uirelease [sel_setting = $setting_num]
						]
					]
					
					setting_num = (+ $setting_num 1)
				]
			]
			
			uispace 0.005
		
			UIbar 0 1
			
			uitarget 0.65 0.7 [
				uivlist 0 [
					uivgradient 0x96B9D0 0x76A0CA 0.65 0.1 [
						uispace 0.3 0.03
						uihlist 0 [
							uispace 0.005
							uiimage (at $setting_ico $sel_setting) 0.07 0.07
							uispace 0.01
							if @GAME_LANG [setting_name = (at $setting_names_en $sel_setting )][setting_name = (at $setting_names_fr $sel_setting )]
							uicolortext $setting_name 0x252525 0.8
						]
						uialign* -1
						uiclamp 1 1 1 1
					]
					
					uispace 0 0.0025
					
					uihlist 0 [
						uivlist 0 [
							case $sel_setting 0 [show_gameplay]
							case $sel_setting 1 [show_gfx]
							case $sel_setting 2 [show_display]
							case $sel_setting 3 [show_audio]
						]
						uispace 0.005
						uialign* -1 -1
					]
					uialign* -1 -1
					if $infotxt [showui "infobox"][hideui "infobox"]
				]
				uialign* -1 -1
			]
		]
	]
] [if @GAME_LANG [hideui "main_en"][hideui "main_fr"]] [if @GAME_LANG [showui "main_en"][showui "main_fr"] ; UI_menutabs = 0] [] 0 "SETTINGS" "OPTIONS" "media/interface/hud/options.jpg"

UImenu "infobox" [
	uihgradient 0xC5BFBB 0xA59F9B 0.5 0.01 [
		uihlist 0 [
			uiimage "media/interface/hud/info.jpg" 0.035 0.035
			uicolortext $infotxt 0x444444 0.5
		]
	]
][][][] 0.79

// UIsettingcheckbox 1:EN_TXT 2:FR_TXT 3:[on-change] 4:VAL 5:EN_INFO 6:FR_INFO
UIsettingcheckbox = [
	uispace 0 0.0015
	uitarget 0.6 0.05 [
		uihover [setgradsize = 0.038 ; setgradcol = 0xE5DFDB ; if @GAME_LANG [infotxt = @arg5][infotxt = @arg6]] [setgradsize = 0.035 ; setgradcol = 0xDAD4D1]
		uimodhgradient $setgradcol 0xFFFFFF 0.7 0.05 [
			uihlist 0 [
				uispace 0.005
				UIcheckbox @arg4 0.02
				uispace 0.005
				if @GAME_LANG [uicolortext @arg1 0x222222 0.6][uicolortext @arg2 0x222222 0.6]
			]
			uialign* -1
			uirelease [doargs $arg3]
		]
	]
	uialign* -1
	uispace 0 0.0015
]

// UIsettingslider 1:EN_TXT 2:FR_TXT 3:SETTING 4:VAL 5:MIN 6:MAX 7:INCR 8:[on-change] 9:GYR GRADIENT 10:EN_INFO 11:FR_INFO
UIsettingslider = [
	uispace 0 0.0015
	uitarget 0.6 0.05 [
		uihover [setgradsize = 0.038 ; setgradcol = 0xE5DFDB ; if @GAME_LANG [infotxt = @arg10][infotxt = @arg11]] [setgradsize = 0.035 ; setgradcol = 0xDAD4D1]
		uimodhgradient $setgradcol 0xFFFFFF 0.7 0.06 [
			uihlist 0 [
				uispace 0.005
				uitarget 0.25 0.03 [if @GAME_LANG [uicolortext @arg1 0x222222 0.6][uicolortext @arg2 0x222222 0.6] ; uialign* -1]
				UIhslider @arg3 @arg5 @arg6 @arg7 0.5 0.025 [uitext @arg4 0.6] [@arg8] [] @arg9
			]
		]
	]
	uispace 0 0.0015
]

show_gameplay = [
	uispace 0 0.0015
	uitarget 0.6 0.05 [
		uihover [setgradsize = 0.038 ; setgradcol = 0xE5DFDB] [setgradsize = 0.035 ; setgradcol = 0xDAD4D1]
		uimodhgradient $setgradcol 0xFFFFFF 0.7 0.06 [
			uihlist 0 [
				uihlist 0 [
					uispace 0.005
					redcol1 = 0xFF0000 ; redcol2 = 0xAA0000 ; greencol1 = 0x00FF00 ; greencol2 = 0x00AA00
					if @GAME_LANG [uitext "^fbLanguage:" 0.6 ; frflagcol1 = $redcol1 ; frflagcol2 = $redcol2 ; enflagcol1 = $greencol1 ; enflagcol2 = $greencol2][
						uitext "^fbLangue :" 0.6 ; frflagcol1 = $greencol1 ; frflagcol2 = $greencol2 ; enflagcol1 = $redcol1 ; enflagcol2 = $redcol2
					]
				]
				uispace 0.005
				uihlist 0 [
					uivgradient $frflagcol1 $frflagcol2 0.07 0.05 [uiimage "media/interface/hud/flag_fr.png" 0.06 0.042 ; uioutline 0x000000 ; uiclamp- 1 1 1 1 ; uirelease [GAME_LANG = 0]]
					uispace 0.005
					uivgradient $enflagcol1 $enflagcol2 0.07 0.05 [uiimage "media/interface/hud/flag_en.png" 0.06 0.042 ; uioutline 0x000000 ; uiclamp- 1 1 1 1; uirelease [GAME_LANG = 1]]
				]
			]
			uialign* -1
		]
	]
	uialign* -1
	
	uispace 0 0.0015
	
	UIsettingcheckbox ["Third person"] ["Caméra détachée"] [thirdperson (! $thirdperson)] [$thirdperson]
	
	UIsettingcheckbox ["Invert mouse"] ["Inverser la souris"] [invmouse (! $invmouse)] [$invmouse]
	
	UIsettingcheckbox ["Show blood"] ["Afficher le sang"] [blood (! $blood)] [$blood]
	
	UIsettingcheckbox ["Limit light flashes"] ["Limiter les flashs lumineux"] [epilepsyfriendly (! $epilepsyfriendly)] [$epilepsyfriendly] ["Reduces the intensity of light flashes. Useful if you have a headache or epilepsy."]["Réduit l'intensité des flashs lumineux, pratique si tu as un mal de crâne ou de l'épilepsie."]
	
	UIsettingcheckbox ["Show minimap"] ["Afficher la mini-carte"] [showminimap (! $showminimap)] [$showminimap]

	UIsettingcheckbox ["Show FPS"] ["Afficher le taux de rafraîchissement"] [showfps (! $showfps)] [$showfps]
	
	UIsettingcheckbox ["Show ping"] ["Afficher le ping"] [showmyping (! $showmyping)] [$showmyping]
	
	UIsettingslider ["Field of view"] ["Champ de vision"] [fov] [$fov] [10] [150] [1]
	
	UIsettingslider ["Mouse sensitivity"] ["Sensibilité de la souris"] [sensitivity] [$sensitivity] [0.1] [40.0] [0.1]
]

gfxsettings = ["^f0Basse" "^fdMoyenne" "^f2Élevée" "^f3Ultra" "^ffLow" "^f0Medium" "^f2High" "^f3Ultra"]

show_gfx = [
	UIsettingslider ["Shadows quality"] ["Qualité des ombres"] [shadowslod] [(at $gfxsettings (+ $shadowslod (* $shadowslod @GAME_LANG)))] [0] [3] [1] [setshadowslod] [1] ["Accuracy, render distance, and anti-aliasing of shadows."]["Précision, distance d'affichage et anti-crélénage des ombres."]

	UIsettingslider ["Lighting quality"] ["Qualité de l'éclairage"] [lightlod] [(at $gfxsettings (+ $lightlod (* $lightlod @GAME_LANG)))] [0] [3] [1] [setlightlod] [1] ["Volumetric lighting, global illumination, and ambient occlusion quality."]["Eclairage volumétrique, illumination globale et qualité d'occlusion ambiante."]
	
	UIsettingslider ["Water quality"] ["Qualité de l'eau"] [waterlod] [(at $gfxsettings (+ $waterlod (* $waterlod @GAME_LANG)))] [0] [3] [1] [setwaterlod] [1] ["Water geometry, animations, effects, and reflections."]["Géométrie de l'eau, animations, effets et reflets."]
	
	UIsettingslider ["Particles quality"] ["Qualité des particules"] [particleslod] [(at $gfxsettings (+ $particleslod (* $particleslod @GAME_LANG)))] [0] [3] [1] [setparticleslod] [1] ["Maximum amount of particles and rendering accuracy."] ["Quantité maximale de particules et précision du rendu."]
	
	UIsettingslider ["3D models rendering"] ["Qualité d'affichage 3D"] [trislod] [(at $gfxsettings (+ $trislod (* $trislod @GAME_LANG)))] [0] [3] [1] [set3Ddistlod] [1] ["3D models rendering distance and grass overall quality."]["Distance d'affichage des modèles 3D et qualité d'afficage de l'herbe."]
	
	UIsettingslider ["Decals quality"] ["Qualité des décorations"] [decalslod] [(at $gfxsettings (+ $decalslod (* $decalslod @GAME_LANG)))] [0] [3] [1] [setdecalslod] [1]
	
	UIsettingcheckbox ["Ambient occlusion"] ["Occlusion ambiante"] [ao (! $ao)] [$ao]
]


speaker_ico = ["media/interface/hud/speaker_off.png" "media/interface/hud/speaker_on.png"]

show_audio = [
	uispace 0 0.0015
	uitarget 0.6 0.05 [
		uihover [setgradsize = 0.038 ; setgradcol = 0xE5DFDB] [setgradsize = 0.035 ; setgradcol = 0xDAD4D1]
		uimodhgradient $setgradcol 0xFFFFFF 0.7 0.06 [
			uihlist 0 [
				uihlist 0 [
					uispace 0.005
					if @GAME_LANG [uitext "^fbSounds:" 0.6][uitext "^fbSons :" 0.6]
				]
				uispace 0.005
				uihlist 0 [
					if @sound [col1 = 0x00FF00 ; col2 = 0x00AA00][col1 = 0xFF0000 ; col2 = 0xAA0000]
					uivgradient $col1 $col2 0.07 0.05 [uiimage (at $speaker_ico @sound) 0.06 0.042 ; uioutline 0x000000 ; uiclamp- 1 1 1 1 ; uirelease [sound (! $sound) ; showui "changes"]]
				]
			]
			uialign* -1
		]
	]
	uialign* -1
	
	uispace 0 0.0015
	
	tmpset = (* (*f $soundvol 0.393) 1)
	UIsettingslider ["Sound volume"] ["Volume des sons"] [soundvol] [$tmpset] [0] [255] [1]
	
	tmpset = (* (*f $musicvol 0.393) 1)
	UIsettingslider ["Music volume"] ["Volume des musiques"] [musicvol] [$tmpset] [0] [255] [1]
	
	tmpvals = ["512" "1024" "2048" "4096"]
	UIsettingslider ["Sound buffer"] ["Mémoire tampon"] [soundbufferlen_ui] [$soundbufferlen] [0] [3] [1] [soundbufferlen = (at $tmpvals $soundbufferlen_ui) ; showui "changes"] ["Increase the buffer if you hear stuttering sounds like in Fallout 3."]["Augmenter la mise en mémoire tampon est utile si le son est saccadé (comme dans Fallout 3)."]
	
	UIsettingslider ["Max sounds at once"] ["Limite de sons"] [maxsoundsatonce] [$maxsoundsatonce] [32] [512] [1] [soundchans = @maxsoundsatonce ; showui "changes"] [1] ["Max amount of sounds that can be played simultaneously. A high amount can burn your CPU."]["Quantité de sons pouvant êtres lus simultanémant, impact important sur l'utilisation du processeur."]
	
	tmpvals = ["11025" "22050" "44100" "48000"]
	UIsettingslider ["Sampling frequency"] ["Taux d'échantillonnage"] [soundfreq_ui] [$soundfreq] [0] [3] [1] [soundfreq = (at $tmpvals $soundfreq_ui) ; showui "changes"] [1]
	
	UIsettingcheckbox ["Play sounds when the game is minimized"] ["Jouer les sons lorsque le jeu est minimisé"] [minimizedsounds (! $minimizedsounds)] [$minimizedsounds]
]


show_display = [
	uispace 0 0.0015
	uitarget 0.6 0.05 [
		uihover [setgradsize = 0.038 ; setgradcol = 0xE5DFDB] [setgradsize = 0.035 ; setgradcol = 0xDAD4D1]
		uimodhgradient $setgradcol 0xFFFFFF 0.7 0.06 [
			uihlist 0 [
				uispace 0.005
				uivlist 0 [
					uispace 0.005
					if @GAME_LANG [uitext "^fbAspect ratio:" 0.6][uitext "^fbRatio d'affichage :" 0.6]
					uispace 0 0.005
					if @GAME_LANG [uitext "^fbScreen resolution:" 0.6][uitext "^fbRésolution d'affichage :" 0.6]
					uialign* -1
				]
				uispace 0.01
				uivlist 0 [
					uihlist 0 [
						UIlistslider UI_aspectratio =s $opt_ratios_assoc 0.2 $UI_optrowh [
							if (=s $UI_aspectratio "custom") [] [
								UI_resolution = (at $[UI_ratio@UI_aspectratio] 1)
									screenw (at $UI_resolution 0)
									screenh (at $UI_resolution 1)
							]
						]
					]
					uispace 0 0.005
					UIlistslider UI_resolution = (
						looplistconcat i $[UI_ratio@UI_aspectratio] [
							result (concat (escape $i) (strreplace $i " " "x"))
						]
					) 0.2 $UI_optrowh [
						screenw (at $UI_resolution 0)
						screenh (at $UI_resolution 1)
					]
				]
			]
			uialign* -1
		]
	]
	uialign* -1
	
	uispace 0 0.0015
	
	UIsettingcheckbox ["Exclusive fullscreen"] ["Plein écran exclusif"] [fullscreen (! $fullscreen)] [$fullscreen]
	
	UIsettingslider ["Render scale"] ["Échelle de rendu"] [gscale] [$gscale] [25] [100] [1]
	
	UIsettingcheckbox ["Vertical synchronization"] ["Synchronisation verticale"] [vsync (! $vsync)] [$vsync] ["Enable vertical synchronization if you have screen tearing."]["Activez cette option si l'affichage présente des artéfacts."]
	
	UIsettingslider ["Gamma"] ["Luminosité"] [gamma] [$gamma] [20] [200] [1]
	
	tmpvals = ["Off" "2" "4" "8" "16"]
	UIsettingslider ["Anisotropic filtering"] ["Filtrage anisotrope"] [aniso_ui] [$aniso] [0] [4] [1] [aniso = (at $tmpvals $aniso_ui) ; showui "changes"] [1]
	
	tmpvals = ["Off" "2" "4" "8" "16"]
	UIsettingslider ["Multisample anti-aliasing (MSAA)"] ["Anti-crélénage (MSAA)"] [msaa_ui] [$msaa] [0] [4] [1] [msaa = (at $tmpvals $msaa_ui) ; showui "changes"] [1] ["Smoothing of jagged edges. For best rendering, it is recommended to use only one anti-aliasing method.^nThe MSAA is one of the cleanest anti-aliasing but has more impact on performance than the others."]["Améliore les rebords en les lissant. Pour meilleur rendu, mieux vaut n'utiliser qu'une seule méthode d'anti-crélénage.^nLe MSAA est l'un des anti-crélénage le plus propre, mais un peu plus gourmande que les autres."]
	
	tmpvals = ["Off" "Low" "Medium" "High"]
	UIsettingslider ["Fast approximate anti-aliasing (FXAA)"] ["Anti-crélénage (FXAA)"] [fxaa_ui] [$fxaa] [0] [3] [1] [fxaa = (at $tmpvals $fxaa_ui) ; showui "changes"] [1] ["Smoothing of jagged edges. For best rendering, it is recommended to use only one anti-aliasing method.^nFXAA is a nice anti-aliasing while having little impact on performance."]["Améliore les rebords en les lissant. Pour meilleur rendu, mieux vaut n'utiliser qu'une seule méthode d'anti-crélénage.^nLe FXAA est un anti-crélénage efficace tout en ayant peu d'impact sur les performances."]
	
	UIsettingcheckbox ["Temporal anti-aliasing"] ["Anti-crélénage (TXAA)"] [tqaa (! $tqaa)] [$tqaa] ["Smooths jagged edges by blurring them. (You can also remove your glasses to get the same effect.)"]["Lisse les bords irréguliers en les floutant. C'est un peu comme si tu retirais tes lunettes."]
	
]


opt_affichage = [

	
	

]

opt_clavier = [
	UItitle [
		uihlist 0 [
			uiimage "media/interface/hud/souris.png" 0.045 0.045
			uispace 0.003
			uitext "^fbCONTROLS :" 0.65
			uispace 0.038
		]
		uialign* -1 0
	]
	
	uispace 0.02	
	
    uitablerow [
        uihlist 0.01 [uitext "^fb Move forward" 0.55]
		temp1 = (searchbinds forward)
		uivgradient $c_field1 $c_field2 $arg2 $arg3 [
			uikeyfield [temp1] 18 [ 
				bind $temp1 forward
			] 0.55 [uioutline (uifocus? $c_focus $c_line); uiclamp- 1 1 1 1]
		]
		uispace 0.003
        uialign* -1 0
    ]
	
    uitablerow [
        uihlist 0.01 [uitext "^fb Move backward" 0.55]
		temp2 = (searchbinds backward)
		uivgradient $c_field1 $c_field2 $arg2 $arg3 [
			uikeyfield [temp2] 18 [ 
				bind $temp2 backward
			] 0.55 [uioutline (uifocus? $c_focus $c_line); uiclamp- 1 1 1 1]
		]
		uispace 0.003
        uialign* -1 0
    ]

    uitablerow [
        uihlist 0.01 [uitext "^fb Move left" 0.55]
		temp3 = (searchbinds left)
		uivgradient $c_field1 $c_field2 $arg2 $arg3 [
			uikeyfield [temp3] 18 [ 
				bind $temp3 left
			] 0.55 [uioutline (uifocus? $c_focus $c_line); uiclamp- 1 1 1 1]
		]
		uispace 0.003
        uialign* -1 0
    ]
	
    uitablerow [
        uihlist 0.01 [uitext "^fb Move right" 0.55]
		temp4 = (searchbinds right)
		uivgradient $c_field1 $c_field2 $arg2 $arg3 [
			uikeyfield [temp4] 18 [ 
				bind $temp4 right
			] 0.55 [uioutline (uifocus? $c_focus $c_line); uiclamp- 1 1 1 1]
		]
		uispace 0.003
        uialign* -1 0
    ]
	
    uitablerow [
        uihlist 0.01 [uitext "^fb Jump" 0.55]
		temp10 = (searchbinds jump)
		uivgradient $c_field1 $c_field2 $arg2 $arg3 [
			uikeyfield [temp10] 18 [ 
				bind $temp10 jump
			] 0.55 [uioutline (uifocus? $c_focus $c_line); uiclamp- 1 1 1 1]
		]
		uispace 0.003
        uialign* -1 0
    ]
	
    uitablerow [
        uihlist 0.01 [uitext "^fb Crouch" 0.55]
		temp12 = (searchbinds crouch)
		uivgradient $c_field1 $c_field2 $arg2 $arg3 [
			uikeyfield [temp12] 18 [ 
				bind $temp12 crouch
			] 0.55 [uioutline (uifocus? $c_focus $c_line); uiclamp- 1 1 1 1]
		]
		uispace 0.003
        uialign* -1 0
    ]
	
    uitablerow [
        uihlist 0.01 [uitext "^fb Shoot" 0.55]
		temp5 = (searchbinds shoot)
		uivgradient $c_field1 $c_field2 $arg2 $arg3 [
			uikeyfield [temp5] 18 [ 
				bind $temp5 shoot
			] 0.55 [uioutline (uifocus? $c_focus $c_line); uiclamp- 1 1 1 1]
		]
		uispace 0.003
        uialign* -1 0
    ]
	
    uitablerow [
        uihlist 0.01 [uitext "^fb Zoom" 0.55]
		temp6 = (searchbinds dozoom)
		uivgradient $c_field1 $c_field2 $arg2 $arg3 [
			uikeyfield [temp6] 18 [ 
				bind $temp6 dozoom
			] 0.55 [uioutline (uifocus? $c_focus $c_line); uiclamp- 1 1 1 1]
		]
		uispace 0.003
        uialign* -1 0
    ]
	
    uitablerow [
        uihlist 0.01 [uitext "^fb Ability 1" 0.55]
		temp7 = (searchbinds aptitude 1)
		uivgradient $c_field1 $c_field2 $arg2 $arg3 [
			uikeyfield [temp7] 18 [ 
				bind $temp7 aptitude 1
			] 0.55 [uioutline (uifocus? $c_focus $c_line); uiclamp- 1 1 1 1]
		]
		uispace 0.003
        uialign* -1 0
    ]
	
    uitablerow [
        uihlist 0.01 [uitext "^fb Ability 2" 0.55]
		temp8 = (searchbinds aptitude 2)
		uivgradient $c_field1 $c_field2 $arg2 $arg3 [
			uikeyfield [temp8] 18 [ 
				bind $temp8 aptitude 2
			] 0.55 [uioutline (uifocus? $c_focus $c_line); uiclamp- 1 1 1 1]
		]
		uispace 0.003
        uialign* -1 0
    ]
	
    uitablerow [
        uihlist 0.01 [uitext "^fb Ability 3" 0.55]
		temp9 = (searchbinds aptitude 3)
		uivgradient $c_field1 $c_field2 $arg2 $arg3 [
			uikeyfield [temp9] 18 [ 
				bind $temp9 aptitude 3
			] 0.55 [uioutline (uifocus? $c_focus $c_line); uiclamp- 1 1 1 1]
		]
		uispace 0.003
        uialign* -1 0
    ]
	
    uitablerow [
        uihlist 0.01 [uitext "^fb Interact" 0.55]
		temp15 = (searchbinds dodial)
		uivgradient $c_field1 $c_field2 $arg2 $arg3 [
			uikeyfield [temp15] 18 [ 
				bind $temp15 dodial
			] 0.55 [uioutline (uifocus? $c_focus $c_line); uiclamp- 1 1 1 1]
		]
        uialign* -1 0
    ]
	
    uitablerow [
        uihlist 0.01 [uitext "^fb Public chat" 0.55]
		temp13 = (searchbinds saycommand)
		uivgradient $c_field1 $c_field2 $arg2 $arg3 [
			uikeyfield [temp13] 18 [ 
				bind $temp13 saycommand
			] 0.55 [uioutline (uifocus? $c_focus $c_line); uiclamp- 1 1 1 1]
		]
		uispace 0.003
        uialign* -1 0
    ]

    uitablerow [
        uihlist 0.01 [uitext "^fb Team chat" 0.55]
		temp14 = (searchbinds sayteamcommand)
		uivgradient $c_field1 $c_field2 $arg2 $arg3 [
			uikeyfield [temp14] 18 [ 
				bind $temp14 sayteamcommand
			] 0.55 [uioutline (uifocus? $c_focus $c_line); uiclamp- 1 1 1 1]
		]
		uispace 0.003
        uialign* -1 0
    ]
	
	uispace 0 0.004
	
	uitablerow [
		uihlist 0.01 [uitext "^fb Default setting" 0.55]
		uihlist 0 [
			UIbutton "hold1" [
				uitext "^f7Reset" 0.55
			] 0 0 [exec "config/default_EN.cfg"] -1
		]
		uialign* -1 0
    ]
]

confilt1 = 0
confilt2 = 0

opt_console = [
	UItitle [
		uihlist 0 [
			uiimage "media/interface/hud/comp.jpg" 0.045 0.045
			uispace 0.003
			uitext "^fbGAME :" 0.65
			uispace 0.21
		]
		uialign* -1 0
	]
	
	uispace 0.02
	
	uitablerow [
		UIbutton "hold2" [
			UIcheckbox $contchat 0.016
			uitext "^fbChat" 0.6
		] $UI_optc1wB $UI_optrowh [contchat (! $contchat)] -1
		uialign* -1 0
	]
			
	uitablerow [
		UIbutton "hold2" [
			UIcheckbox $conteamtchat 0.016
			uitext "^fbTeam chat" 0.6
		] $UI_optc1wB $UI_optrowh [conteamtchat (! $conteamtchat)] -1
		uialign* -1 0
	]
	
	uitablerow [
		UIbutton "hold2" [
			UIcheckbox $congame 0.016
			uitext "^fbGame events" 0.6
		] $UI_optc1wB $UI_optrowh [congame (! $congame)] -1
		uialign* -1 0
	]
	
	uitablerow [
		UIbutton "hold2" [
			UIcheckbox $conkills 0.016
			uitext "^fbSelf frags" 0.6
		] $UI_optc1wB $UI_optrowh [conkills (! $conkills)] -1
		uialign* -1 0
	]

	uitablerow [
		UIbutton "hold2" [
			UIcheckbox $conallkills 0.016
			uitext "^fbOther player frags" 0.6
		] $UI_optc1wB $UI_optrowh [conallkills (! $conallkills)] -1
		uialign* -1 0
	]
	
	UItitle [
		uihlist 0 [
			uiimage "media/interface/hud/comp.jpg" 0.045 0.045
			uispace 0.003
			uitext "^fbENGINE :" 0.65
		]
		uialign* -1 0
	]
	
	uispace 0.02
	
	uitablerow [
		UIbutton "hold2" [
			UIcheckbox $coninfos 0.016
			uitext "^fbImportant information" 0.6
		] $UI_optc1wB $UI_optrowh [coninfos (! $coninfos)] -1
		uialign* -1 0
	]

	uitablerow [
		UIbutton "hold2" [
			UIcheckbox $conwarnings 0.016
			uitext "^fbWarnings" 0.6
		] $UI_optc1wB $UI_optrowh [conwarnings (! $conwarnings)] -1
		uialign* -1 0
	]

	uitablerow [
		UIbutton "hold2" [
			UIcheckbox $conerrors 0.016
			uitext "^fbErrors" 0.6
		] $UI_optc1wB $UI_optrowh [conerrors (! $conerrors)] -1
		uialign* -1 0
	]
	
	uitablerow [
		UIbutton "hold2" [
			UIcheckbox $coninit 0.016
			uitext "^fbInit messages" 0.6
		] $UI_optc1wB $UI_optrowh [coninit (! $coninit)] -1
		uialign* -1 0
	]
	
	uitablerow [
		UIbutton "hold2" [
			UIcheckbox $conscripts 0.016
			uitext "^fbScript messages" 0.6
		] $UI_optc1wB $UI_optrowh [conscripts (! $conscripts)] -1
		uialign* -1 0
	]
]	

show_options = [
	uihlist 0 [
		uihlist 0 [
			uivlist 0 [
				UIvtab 0.16 (*f 0.048 10.4) UI_opttabs [
					0 "^foGameplay"     	[]
					1 "^foGraphics"    		[]
					2 "^foDisplay"  		[]
					3 "^foAudio"    		[]
					4 "^foControls"   		[]
					5 "^foConsole"   		[]
				] 1
			]
			uialign- 0 -1
			UIbar 0 1
			uiscroll 0.67 0.6 [
				uialign 0 -1
				uitable 0 0.005 [
					uialign -1 -1
					case $UI_opttabs 0 [opt_jouabilite]
					case $UI_opttabs 1 [opt_graphismes]
					case $UI_opttabs 2 [opt_affichage]
					case $UI_opttabs 3 [opt_audio]
					case $UI_opttabs 4 [opt_clavier]
					case $UI_opttabs 5 [opt_console]
				]	
			]
		]
	]
][
   UI_aspectratio = ""
   UI_resolution  = (concat $screenw $screenh)
   UI_findratio
]