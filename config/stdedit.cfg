///////////////////////////////////////////////////////////////////////////////
//  Entity primary actions                                                   //
///////////////////////////////////////////////////////////////////////////////

=enttype = [|| (=s * $arg1) [=s (enttype) $arg1]]

=entattr = [|| (=s * $arg2) [= (entattr $arg1) $arg2]]

// clear ents of given type
clearents = [ 
   if $editing [
      entcancel
      entselect [=enttype $arg1]
      echo Deleted (enthavesel) $arg1 entities
      delent
   ] 
]

// replace all ents that match current selection
// with the values given
replaceents = [
   if $editing [
      do [
         entfind @(entget)
         entset @(loopconcat+ i 1 $numargs [result $[arg@i]])
      ]
      echo Replaced (enthavesel) entities
   ]
]

toggleeditent = [	
	if (enthavesel) [
		etype = $enttype
		eattr0 = (entattr 0)
		eattr1 = (entattr 1)
		eattr2 = (entattr 2)
		eattr3 = (entattr 3)
		eattr4 = (entattr 4)
		eattr5 = (entattr 5)
		eattr6 = (entattr 6)
		eattr7 = (entattr 7)
		eattr8 = (entattr 8)
		eattr9 = (entattr 9)
		if (uivisible "editent") [hideui "editent"][showui "editent"]
	][
		popupmsg "Veuillez sélectionner une entité d'abord" "Please select an entity first" 2000 -1
	]
]

updateEntAttrs = [
	(entattr 0 $eattr0)
	(entattr 1 $eattr1)
	(entattr 2 $eattr2)
	(entattr 3 $eattr3)
	(entattr 4 $eattr4)
	(entattr 5 $eattr5)
	(entattr 6 $eattr6)
	(entattr 7 $eattr7)
	(entattr 8 $eattr8)
]

getParticlesAttrs = [
	textval = 0
	
	if (= $eattr0 0) [ // weather
		attr1text = (at $Map_Editor_EditWeatherType) ; 			attr1min = 0 ; 		attr1max = 5 ; 		textval = 1
		attr2text = (at $Map_Editor_EditWeatherIntensity) ; 	attr2min = 0 ; 		attr2max = 2000
		attr6text = (at $Map_Editor_EditWind) ; 				attr6min = -500 ; 	attr6max = 500
		attr7text = ""
		attr8text = ""
	]
	
	if (|| (= $eattr0 1) (= $eattr0 2) (= $eattr0 3)) [ // fire&smoke, fire, smoke
		attr1text = (at $Map_Editor_EditWidth) ; 				attr1min = 0 ; 		attr1max = 2000
		attr2text = (at $Map_Editor_EditHeight) ; 				attr2min = 0 ; 		attr2max = 2000
		attr6text = (at $Map_Editor_EditColorOffset) ; 			attr6min = 0 ; 		attr6max = 255
		attr7text = (at $Map_Editor_EditSizeMod) ; 				attr7min = -24 ;    attr7max = 24
		attr8text = (at $Map_Editor_EditSparks) ; 				attr8min = 0 ;      attr8max = 100
	]
	
	if (= $eattr0 4) [ // steam
		attr1text = (at $Map_Editor_EditDir) ; 					attr1min = 0 ; 		attr1max = 5 ;		textval = 1
		attr2text = (at $Map_Editor_EditSize) ; 				attr2min = 0 ; 		attr2max = 300
		attr6text = (at $Map_Editor_EditSpeed) ; 				attr6min = 0 ; 		attr6max = 200
		attr7text = (at $Map_Editor_EditFade) ; 				attr7min = 0 ;      attr7max = 5000
		if (= $eattr0 8) [attr8text = (at $Map_Editor_EditProbability) ; attr8min = 0 ; attr8max = 100] [attr8text = ""]
	]
	
	if (= $eattr0 5) [ // energy ball
		attr1text = (at $Map_Editor_EditType) ; 				attr1min = 0 ; 		attr1max = 3
		attr2text = (at $Map_Editor_EditRadius) ; 				attr2min = 0 ; 		attr2max = 1000
		attr6text = ""
		attr7text = ""
		attr8text = ""
	]
	
	if (= $eattr0 6) [ // cloud
		attr1text = (at $Map_Editor_EditSize) ; 				attr1min = 0 ; 		attr1max = 1000
		attr2text = (at $Map_Editor_EditType) ; 				attr2min = 0 ; 		attr2max = 5
		attr6text = ""
		attr7text = ""
		attr8text = ""
	]
	
	if (= $eattr0 7) [ // rainbow
		attr1text = (at $Map_Editor_EditSize) ; 				attr1min = 0 ; 		attr1max = 1000
		attr2text = ""
		attr6text = ""
		attr7text = ""
		attr8text = ""
	]
	
	if (= $eattr0 8) [ // sparks
		attr1text = (at $Map_Editor_EditNum) ; 					attr1min = 0 ; 		attr1max = 100
		attr2text = (at $Map_Editor_EditSize) ; 				attr2min = 0 ; 		attr2max = 300
		attr6text = (at $Map_Editor_EditSpeed) ; 				attr6min = 0 ; 		attr6max = 200
		attr7text = (at $Map_Editor_EditFade) ; 				attr7min = 0 ;      attr7max = 5000
		attr8text = (at $Map_Editor_EditProbability) ;		    attr8min = 0 ; 		attr8max = 100
	]
	
	if (= $eattr0 9) [ // water drops
		attr1text = (at $Map_Editor_EditNum) ; 					attr1min = 0 ; 		attr1max = 100
		attr2text = (at $Map_Editor_EditSize) ; 				attr2min = 0 ; 		attr2max = 300
		attr6text = (at $Map_Editor_EditRadius) ; 				attr6min = 0 ; 		attr6max = 500
		attr7text = (at $Map_Editor_EditFade) ; 				attr7min = 0 ;      attr7max = 5000
		attr8text = (at $Map_Editor_EditProbability) 			attr8min = 0 ;      attr8max = 100
	]
	
	if (= $eattr0 10) [ // lava bubble
		attr1text = (at $Map_Editor_EditProbability) ; 			attr1min = 0 ; 		attr1max = 100
		attr2text = ""
		attr6text = ""
		attr7text = ""
		attr8text = ""
	]
]

selreplaceents = [saycommand (concatword "/replaceents " (entget))]
selentfindall  = [do [entfind @(entget)]]

// modify given attribute of ent by a given amount
// arg1 attribute
// arg2 value
entproperty = [entattr $arg1 (+ (entattr $arg1) $arg2)]

enttypelist = [ 
   light mapmodel playerstart envmap particles sound spotlight decal
   teleport teledest jumppad
   flag
]

enttypeselect = [     
   enttypelength = (listlen $enttypelist)
   next = (mod (+ (indexof $enttypelist (enttype)) $arg1) $enttypelength)
   if (< $next 0) [ next = (+ $next $enttypelength) ]
   do [entset @(listsplice (entget) (at $enttypelist $next) 0 1)]
]


///////////////////////////////////////////////////////////////////////////////
//  Copy & Paste                                                             //
///////////////////////////////////////////////////////////////////////////////

// 3 types of copying and pasting
// 1. select only cubes      -> paste only cubes
// 2. select cubes and ents  -> paste cubes and ents. same relative positions
// 3. select only ents       -> paste last selected ent. if ents are selected, replace attrs as paste

opaquepaste = 1
entcopymode = 0

editcopy   = [
   if (|| (havesel) [! (enthavesel)]) [
      entcopymode = 0
      entcopy
      copy
   ] [
      entcopymode = 1
      entcopy
   ]
]

editpaste  = [ 
   cancelpaste = (! (|| (enthavesel) [havesel]));
   if $entcopymode [
      entreplace
      if $cancelpaste [cancelsel]
   ] [
      pastehilite
      reorient // temp; real fix will be in octaedit
      onrelease [ 
         if $opaquepaste delcube 
         paste; entpaste
         if $cancelpaste [cancelsel] 
      ] 
   ]
]


///////////////////////////////////////////////////////////////////////////////
//  Selection                                                                //
///////////////////////////////////////////////////////////////////////////////

// select ents with given properties
// '*' is wildcard
entfind = [
   if $numargs [
      entselect (concat [&& [=enttype @@arg1]] (loopconcat i (- $numargs 1) [
         result [[=entattr @@i @@[arg@(+ $i 2)]]]
      ]))
   ] [entselect 1]
]

entfindinsel = [
   if $numargs [
      entselect (concat [&& insel [=enttype @@arg1]] (loopconcat i (- $numargs 1) [
         result [[=entattr @@i @@[arg@(+ $i 2)]]]
      ]))
   ] [entselect insel]
]

drag       = [dragging 1; onrelease [dragging 0]]
corners    = [selectcorners 1; dragging 1; onrelease [selectcorners 0; dragging 0]]
editmove   = [moving 1; onrelease [moving 0]; result $moving]
entdrag    = [entmoving 1; onrelease [entmoving 0]; result $entmoving] 
editdrag   = [cancelsel; || (entdrag) [drag]]
selcorners = [if $hmapedit [hmapselect] [cancelsel; || (entdrag) [corners]]]
editextend = [|| (entdrag) [if (havesel) [selextend; reorient; editmovecut] [drag]]]

editmovewith = [
   if (havesel) [
      || (editmove) [arg1]
      onrelease [moving 0; dragging 0]
   ] [
      arg1
   ]
]

editmovecorner = [editmovewith selcorners]
editmovedrag   = [editmovewith editdrag]


///////////////////////////////////////////////////////////////////////////////
//  Miscellaneous Editing Commands                                           //
///////////////////////////////////////////////////////////////////////////////

editfacewsel = [
   if (|| havesel [! (enthavesel)]) [
      if $moving [pushsel $arg1] [
         entcancel
         editface $arg1 $arg2
      ]
   ]
]

entswithdirection = [playerstart teledest mapmodel decal flag]
entdirectionprop  = [0           0        1        1    ]

entdirection = [
   if (&& (enthavesel) [= (havesel) 0]) [
      entdiridx = (indexof $entswithdirection (enttype))
      if (>= $entdiridx 0) [
         entdirprop = (at $entdirectionprop $entdiridx)
         if (> $arg1 0) [
            entproperty $entdirprop $arg2
            if (> (entattr $entdirprop) 360) [entproperty $entdirprop -360]          
         ] [
            entproperty $entdirprop (- 0 $arg2)
            if (< (entattr $entdirprop) 0) [entproperty $entdirprop 360]
         ]
      ]
      result 1
   ] [
      result 0
   ]
]

selmovecut = 1
toggleeditmovecut = [
   selmovecut = (! $selmovecut)
   echo (? $selmovecut "Move Selection" "Cut Selection")
]
editmovecut = [
   if $selmovecut [editmove] [editcut]
]

editdel    = [if (! (enthavesel)) [delcube]; delent]
editflip   = [flip; entflip]
editrotate = [|| (entdirection $arg1 15) [rotate $arg1; entrotate $arg1]]
editcut    = [  
   if (moving 1) [
      if (= $moving 1) [selsave]
      onrelease [ 
         moving 0
         if (selmoved) [
            selswap
            copy; entcopy
            delcube; delent
            selrestore
            paste; entpaste
         ]
      ]
   ]
]

entcomplete = [listcomplete $arg1 $enttypelist]
entcomplete newent
entcomplete entfind
entcomplete clearents

listcomplete editmat "air water clip glass noclip lava gameclip death nogi alpha"
air = [ editmat air $arg1 ]
looplist i ["" 2 3 4] [
   [water@i] = [editmat water@i $arg1]
   [lava@i]  = [editmat lava@i $arg1]
   [glass@i] = [editmat glass@i $arg1]
]
clip     = [editmat clip $arg1]
noclip   = [editmat noclip $arg1]
gameclip = [editmat gameclip $arg1]
death    = [editmat death $arg1]
nogi     = [editmat nogi $arg1]
alpha    = [editmat alpha $arg1]

blendpaintmodes = ["off" "replace" "dig" "fill" "inverted dig" "inverted fill"]

minimaphere = [minimapheight (at (getcampos) 2)]

getsundir = [sunlightyaw (getcamyaw); sunlightpitch (getcampitch)]

defvarp editfloatspeed 1 1000 10000
