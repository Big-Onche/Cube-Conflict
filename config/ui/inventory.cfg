///////////////////////////////////////////////////////////////////////////////
//  Inventory menu                                                           //
///////////////////////////////////////////////////////////////////////////////

weapon_logo = [
  // Regular weapons
  "media/interface/weapons/electricrifle.png"
  "media/interface/weapons/plasmarifle.png"
  "media/interface/weapons/smaw.png"
  "media/interface/weapons/minigun.png"
  "media/interface/weapons/spockgun.png"
  "media/interface/weapons/m32.png"
  "media/interface/weapons/flamethrower.png"
  "media/interface/weapons/uzi.png"
  "media/interface/weapons/famas.png"
  "media/interface/weapons/mossberg500.png"
  "media/interface/weapons/hydra.png"
  "media/interface/weapons/sv98.png"
  "media/interface/weapons/sks.png"
  "media/interface/weapons/crossbow.png"
  "media/interface/weapons/ak47.png"
  "media/interface/weapons/gapb1.png"
  "media/interface/weapons/fireworks.png"
  "media/interface/weapons/molotov.png"
  "media/interface/weapons/glock.png"
  // Super weapons
  "media/interface/weapons/nuke.png"
  "media/interface/weapons/gau8.png"
  "media/interface/weapons/minirockets.png"
  "media/interface/weapons/camper2000.png"
  // Melee weapons
  "media/interface/weapons/bustersword.png"
  "media/interface/weapons/banhammer.png"
  "media/interface/weapons/mastersword.png"
  "media/interface/weapons/flail.png"
  // Special
  "media/interface/weapons/kamikaze.png"
  "null" // powerarmor
  "media/interface/weapons/saber.png"
]


inv_num_rows = 2
inv_num_columns = 4
inv_max_rows = (div $maxweapons $inv_num_rows)
inv_max_columns = (div $maxweapons $inv_num_columns)

inv_tab_target_x = 0.220
inv_tab_target_y = 0.140
inv_tab_font_size = 0.45
inv_tab_image_x = 0.22
inv_tab_image_y = 0.11

set_tab_size = [
  if $arg1 [
    tab_bump = (uibump 500 0.04 $arg2)
    tab_target_x = (*f $inv_tab_target_x $tab_bump)
    tab_target_y = (*f $inv_tab_target_y $tab_bump)
    tab_font_size = (*f $inv_tab_font_size $tab_bump)
    tab_image_x = (*f $inv_tab_image_x $tab_bump)
    tab_image_y = (*f $inv_tab_image_y $tab_bump)
  ] [
    tab_target_x = $inv_tab_target_x
    tab_target_y = $inv_tab_target_y
    tab_font_size = $inv_tab_font_size
    tab_image_x = $inv_tab_image_x
    tab_image_y = $inv_tab_image_y
  ]
]

inv_tab_hover = [
  set_tab_size $TRUE $FALSE
  tab_color_up = $c_tab_hv_up
  tab_color_down = $c_tab_hv_dn 
]

inv_tab_selected = [
  set_tab_size $TRUE $TRUE
  tab_color_up = $c_tab_on_up
  tab_color_down = $c_tab_on_dn
]

inv_tab_nothover = [
  set_tab_size $FALSE
  tab_color_up = $c_tab_up
  tab_color_down = $c_tab_dn
]

inv_tab_desactivated = [
  set_tab_size $FALSE
  tab_color_up = (+ $c_tab_up 0x333333)
  tab_color_down = (+ $c_tab_dn 0x333333)
]

UI_window "inventory" "media/interface/hud/flingue.jpg" @Inventory_Menu_Title [
  if (|| $mainmenu isdead) [hideui "inventory"]
  uivgradient 0xEEEEEE 0xBBBBBB 0.85 0.50 [
    uivlist 0 [
      uihlist 0 [
        gun_num = 0
        uispace 0.005
        loop i $inv_max_rows [ //////////////////////////////////////// Regular weapon tabs rows ////////////////////////////////////////
          uivlist 0 [
            loop i $inv_max_columns [ //////////////////////////////////////// Regular weapon tabs columns ////////////////////////////////////////
              uivlist 0 [
                
                if (getweaponcurammo $gun_num) [
                  is_empty = $FALSE
                  if (currentgun $gun_num) [
                    inv_tab_selected
                  ] [
                    uihover [inv_tab_hover] [inv_tab_nothover]
                  ]
                ] [
                  is_empty = $TRUE
                  inv_tab_desactivated
                ]
                
                uitarget 0.245 0.155 [ // rendering a tab
                  uivgradient $tab_color_up $tab_color_down $tab_target_x $tab_target_y [
                    UI_outline
                    uivlist 0.003 [
                      uispace 0.005
                      if (is_empty) [
                        uicolortext (concatword "[" (at $Inventory_Menu_Empty) "]") 0xEEEEEE $tab_font_size
                      ] [
                        uiimage (at $weapon_logo (getweaponid $gun_num)) $tab_image_x $tab_image_y -90 -90 0 $FALSE
                        uicolortext (concatword (getweaponname $gun_num) " (" (concatword (getweaponcurammo $gun_num) "/" (getweaponmaxammo $gun_num)) ")") 0xFFFFFF $tab_font_size
                      ]
                    ]
                  ]
                  uirelease [(selectgun $gun_num)] // button
                ]
              ]
              uispace 0 0.003
              gun_num = (+ $gun_num 1)
            ]
          ]
          uispace 0.005
        ]
      ]
      uihlist 0 [ //////////////////////////////////////// Class, melee, and super weapon tabs ////////////////////////////////////////
        if (= $playerclass $KAMIKAZE) [
          uitarget 0.245 0.155 [ //////////////////////////////////////// Class weapon tab ////////////////////////////////////////
            if (getclassweaponcurammo) [
              is_empty = $FALSE
              if (classweaponselected) [inv_tab_selected] [uihover [inv_tab_hover] [inv_tab_nothover]]
            ] [
              is_empty = $TRUE
              inv_tab_desactivated
            ]

            uivgradient $tab_color_up $tab_color_down $tab_target_x $tab_target_y [
              UI_outline
              uivlist 0.003 [
                uispace 0.005
                if ($is_empty) [
                  uicolortext (concatword "[" (at $Inventory_Menu_ClassWeapon) "]") 0xEEEEEE $tab_font_size
                ] [
                  uiimage (at $weapon_logo (getclasswweaponid)) $tab_image_x $tab_image_y
                  uicolortext (concatword (getclasswweaponname $gun_num) " (" (concatword (getclassweaponcurammo) "/" (getclassweaponmaxammo)) ")") 0xFFFFFF $tab_font_size
                ]
              ]
            ]
            uirelease [selectclasswweapon]
          ]
        ]

        uitarget 0.245 0.155 [ //////////////////////////////////////// Melee weapon tab ////////////////////////////////////////
        
          if (meleeweaponselected) [inv_tab_selected] [uihover [inv_tab_hover] [inv_tab_nothover]]
          
          uivgradient $tab_color_up $tab_color_down $tab_target_x $tab_target_y [
            UI_outline
            uivlist 0.003 [
              uispace 0.005
              uiimage (at $weapon_logo (getmeleeweaponid)) $tab_image_x $tab_image_y
              uicolortext (getmeleeweaponname) 0xFFFFFF $tab_font_size
            ]
          ]
          uirelease [selectmeleeweapon]
        ]
        
        uispace 0.005
        
        uitarget 0.245 0.155 [ //////////////////////////////////////// Super weapon tab ////////////////////////////////////////
        
          if (hassuperweapon) [
            is_empty = $FALSE
            if (superweaponselected) [inv_tab_selected] [uihover [inv_tab_hover] [inv_tab_nothover]]
          ] [
            is_empty = $TRUE
            inv_tab_desactivated
          ]

          uivgradient $tab_color_up $tab_color_down $tab_target_x $tab_target_y [
            UI_outline
            uivlist 0.003 [
              uispace 0.005
              if ($is_empty) [
                uicolortext (concatword "[" (at $Weapon_Superweapon) "]") 0xEEEEEE $tab_font_size
              ] [
                uiimage (at $weapon_logo (getsuperweaponid)) $tab_image_x $tab_image_y
                uicolortext (concatword (getsuperweaponname $gun_num) " (" (concatword (getweaponcurammo $gun_num $TRUE) "/" (getweaponmaxammo $gun_num $TRUE)) ")") 0xFFFFFF $tab_font_size
              ]
            ]
          ]
          uirelease [selectsuperweapon]
        ]
      ]
    ]
  ]
]