///////////////////////////////////////////////////////////////////////////////
//  Inventory menu                                                           //
///////////////////////////////////////////////////////////////////////////////

inv_num_rows = 2
inv_num_columns = 4
inv_max_rows = (div $maxweapons $inv_num_rows)
inv_max_columns = (div $maxweapons $inv_num_columns)

inv_tab_selected = [
  tab_color_up = $c_tab_on_up
  tab_color_down = $c_tab_on_dn
]

inv_tab_hover = [
  tab_target_x = 0.230
  tab_target_y = 0.150
  font_size = 0.50
  tab_color_up = $c_tab_hv_up
  tab_color_down = $c_tab_hv_dn 
]

inv_tab_nothover = [
  tab_target_x = 0.220
  tab_target_y = 0.140
  font_size = 0.45
  tab_color_up = $c_tab_up
  tab_color_down = $c_tab_dn
]

inv_tab_desactivated = [
  tab_target_x = 0.220
  tab_target_y = 0.140
  font_size = 0.45
  tab_color_up = (+ $c_tab_up 0x333333)
  tab_color_down = (+ $c_tab_dn 0x333333)
]

UI_window "inventory" "media/interface/hud/flingue.jpg" @Inventory_Menu_Title [
  if (|| $mainmenu isdead) [hideui "inventory"]
  uivgradient 0xEEEEEE 0xBBBBBB 0.85 0.50 [
    uivlist 0 [
      uihlist 0 [
        gun_num = 0
        uispace 0.005
        loop i $inv_max_rows [ //////////////////////////////////////// Regular weapon tabs rows ////////////////////////////////////////
          uivlist 0 [
            loop i $inv_max_columns [ //////////////////////////////////////// Regular weapon tabs columns ////////////////////////////////////////
              uivlist 0 [
              
                if (getweaponcurammo $gun_num) [
                  is_empty = $FALSE
                  uihover [inv_tab_hover] [inv_tab_nothover]
                ] [
                  is_empty = $TRUE
                  inv_tab_desactivated
                ]
                
                uitarget 0.230 0.150 [ // rendering a tab
                  if (currentgun $gun_num) [inv_tab_selected]
                  uivgradient $tab_color_up $tab_color_down $tab_target_x $tab_target_y [
                    UI_outline
                    uivlist 0.003 [
                      uispace 0.005
                      if (is_empty) [
                        uicolortext (concatword "[" (at $Inventory_Menu_Empty) "]") 0xEEEEEE $font_size
                      ] [
                        uimodelpreview (getweaponmodel $gun_num) "mapmodel" 0.22 0.11 -90 -90 0 $FALSE
                        uicolortext (concatword (getweaponname $gun_num) " (" (concatword (getweaponcurammo $gun_num) "/" (getweaponmaxammo $gun_num)) ")") 0xFFFFFF $font_size
                      ]
                    ]
                  ]
                  uirelease [(selectgun $gun_num)] // button
                ]
              ]
              uispace 0 0.003
              gun_num = (+ $gun_num 1)
            ]
          ]
          uispace 0.005
        ]
      ]
      uihlist 0 [ //////////////////////////////////////// Melee and superweapon tabs ////////////////////////////////////////
        uitarget 0.105 0.120 [ //////////////////////////////////////// Melee weapon tab ////////////////////////////////////////
          if (= $gun_num $currentgun) [inv_tab_selected] [uihover [inv_tab_hover] [inv_tab_nothover]]
          uivgradient $tab_color_up $tab_color_down $tab_target_x $tab_target_y [
            UI_outline
            uivlist 0.003 [
              uispace 0.005
              uimodelpreview (getmeleeweaponmodel) "mapmodel" 0.22 0.11 -90 -90 0 $FALSE
              uicolortext (getmeleeweaponname) 0xFFFFFF $font_size
            ]
          ]
          uirelease [selectmeleeweapon]
        ]
        
        uispace 0.005
        
        uitarget 0.105 0.120 [ //////////////////////////////////////// Super weapon tab ////////////////////////////////////////
        
          if (hassuperweapon) [
            is_empty = $FALSE
            uihover [inv_tab_hover] [inv_tab_nothover]
          ] [
            is_empty = $TRUE
            inv_tab_desactivated
          ]

          uivgradient $tab_color_up $tab_color_down $tab_target_x $tab_target_y [
            UI_outline
            uivlist 0.003 [
              uispace 0.005
              uimodelpreview (getsuperweaponmodel) "mapmodel" 0.22 0.11 -90 -90 0 $FALSE
              if (is_empty) [
                uicolortext (concatword "[" (at $Weapon_Superweapon) "]") 0xEEEEEE $font_size
              ] [
                uicolortext (getsuperweaponname) 0xFFFFFF $font_size
              ]
            ]
          ]
          uirelease [selectsuperweapon]
        ]
      ]
    ]
  ]
]