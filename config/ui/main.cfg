///////////////////////////////////////////////////////////////////////////////
// main menu                                                                 //
///////////////////////////////////////////////////////////////////////////////
newui "main" [
	uitarget 1.6 0 [
		uivlist 0 [

			uiimage "media/interface/logo.png" 0.572 0.1991
			
			uispace 0 0.01
			
			uihlist 0 [
				uispace 0.06
				uivlist 0 [
					uitarget 0 0.03 [uihlist 0 [uispace 0.005 ; uicolortext (at $Main_Menu_Cat_Play) 0xFFFFFF 0.45]]
					
					if (! $isconnected) [
						UI_Main_Button $Main_Menu_SinglePlayer [showui "solo"]
						UI_Main_Button $Main_Menu_Multiplayer [showui "multiplayer"]
					
						uitarget 0 0.03 [uihlist 0 [uispace 0.005 ; uicolortext (at $Main_Menu_Cat_Cust) 0xFFFFFF 0.45]]
						
						UI_Main_Button $Main_Menu_MyAccount [showui "account"]
					][
						if (@(isspectator 0 1)) [
							UI_Main_Button $Main_Menu_DisableSpectator [if (@(isspectator 0 1)) [spectator] [spectator 1]]
						][
							UI_Main_Button $Main_Menu_ToggleSpectator [if (@(isspectator 0 1)) [spectator] [spectator 1]]
						]
						
						if (> $getmode 5) [UI_Main_Button $Main_Menu_ChangeTeam [if ($isteamone) [team 2] [team 1]] ]
					]
					
					UI_Main_Button $Main_Menu_Classes [showui "classes"]
					
					if (! $isconnected) [
						uitarget 0 0.03 [uihlist 0 [uispace 0.005 ; uicolortext (at $Main_Menu_Cat_Stats) 0xFFFFFF 0.45]]
						
						UI_Main_Button $Main_Menu_GameStats [showui "stats"]
						UI_Main_Button $Main_Menu_Achievements [showui "achievements"]
					]
					
					uitarget 0 0.03 [uihlist 0 [uispace 0.005 ; uicolortext (at $Main_Menu_Cat_Other) 0xFFFFFF 0.45]]
					
					UI_Main_Button $Main_Menu_Settings [showui "settings"]
					
					if (! $isconnected) [ 
						UI_Main_Button $Main_Menu_MapEditor [showui "mapeditor"]
						UI_Main_Button $Main_Menu_Credits [showui "credits"]
					] [
						UI_Main_Button $Main_Menu_Disconnect [disconnect ; clearsleep ; forcecampos = -1] 
					]
					
					UI_Main_Button $Main_Menu_Quit [showui "quit"]
					
					uialign* -1
				]
				uialign* -1
			]
			uialign* -1
			uispace 0 0.012
		]
		uialign* -1
	]
]

///////////////////////////////////////////////////////////////////////////////
// quit confirmation                                                         //
///////////////////////////////////////////////////////////////////////////////
UI_window "quit" "media/interface/hud/blip_red_dead.png" @Quit_Title [
	uivgradient 0xCCCCCC 0x999999 0.1 0.02 [
		uivlist 0.02 [
			uispace 0.02
			uicolortext (at $Quit_Confirmation) 0x000000 0.6
			uihlist 0 [
				UIbutton "hold5" [uiimage "media/interface/hud/checkbox_on.jpg" 0.03 0.03 ; uitext (at $Misc_Yes) 0.6] 0.25 0.035 [quit]
				UIbutton "hold5" [uiimage "media/interface/hud/checkbox_off.jpg" 0.03 0.03 ; uitext (at $Misc_No) 0.6] 0.25 0.035 [hideui "quit" ; showui "main"]
			]
	    ]
	]
] [hideui "main"] [showui "main"] $TRUE

translationPopupToMenus = [ if ($veryfirstlaunch) [showui "firstlaunch"] [showui "settings"] ]

UI_window "translationwarning" "media/interface/hud/info.jpg" @Warning_Title [
	uivgradient 0xDDCCCC 0xAA9999 0.1 0.02 [
	   uivlist 0.005 [
			uispace 0.005
			uihlist 0 [
				uispace 0.005
				uivlist 0 [
					uiwrapcolortext (at $NotNativeDisclaimer) 0.88 0x303030 0.60
					uialign* -1
				]
			]
			uispace 0.005
			
			uihlist 0 [UIbutton "hold5" [uiimage "media/interface/hud/checkbox_on.jpg" 0.035 0.035 ; uicolortext (at $Misc_Ok) 0x181818 0.65] 0.90 0.045 [hideui "translationwarning" ; translationPopupToMenus]]
	    ]
	]
] [hideui "settings" ; hideui "firstlaunch" ; hideui "main"] [hideui "translationwarning" ; translationPopupToMenus]

///////////////////////////////////////////////////////////////////////////////
//  Master                                                                   //
///////////////////////////////////////////////////////////////////////////////
menu_master = [if $isconnected [if $scoreboardmultiplayer [toggleui "master"]]]

UImenu "master" [
   uifill 0 0 [
      uivlist 0 [
         uihlist 0.01 [
            if (ismaster $getclientnum) [
               UIbutton "" [uitext "Relinquish Master" 0.65] 0.24 0.06 [setmaster 0]
            ] [
               if (issomeoneelsemaster) [
                  UIbutton "" [uitext "^f4Claim Master" 0.65] 0.24 0.06 []
               ] [
                  UIbutton "" [uitext "Claim Master" 0.65] 0.24 0.06 [setmaster 1]
               ]
            ]
            if (ismaster $getclientnum) [
               uigrid 2 0 0 [
                  looplist2 n i [
                     "^f0open ^f4(0)"   0 "^f0veto ^f4(1)"    1
                     "^f2locked ^f4(2)" 2 "^f3private ^f4(3)" 3
                  ] [
                     UIbutton "" [
                        UIradio [= $getmastermode $i] 0.016
                        uitext $n 0.6
                     ] 0.16 0.03 [mastermode $i] -1
                  ]
               ]
               uivlist 0 [
                  UIbutton "" [uitext "Clearbans" 0.65] 0.16 0.03 [clearbans]
                  UIbutton "" [uitext "Bots" 0.65] 0.16 0.03 [hideui "main" ; showui "bots"]
               ]
            ] [
               uifill 0.49
            ]
         ]
         uifill 0 0.02 [UIbar 1 ; uiclamp 1 1 1 1]
         if (ismaster $getclientnum) [
            if (> $getmode 2) [UI_clientlistteam] [UI_clientlistsolo]
         ] [
            if (> $getmode 2) [uifill 1.43 0.312] [uifill 0 0.624]
         ]
      ]
   ]
] [] [] [] "Master"

UI_clientlistsolo = [
   uihlist 0 [
      uiscroll 0.71 0.624 [
         uifill 0.71 0.624 [
            uitable 0 0 [
               UI_mlc = 0
               looplist cn (listclients 1 0) [
                  uitablerow [
                     uifill 0.078 0.026 [
                        uicolortext (concatword $cn " : ") (scoreboardstatus $cn) 0.57
                        uialign- 1
                     ]
                     uifill 0.362 0.026 [
                        uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.57
                        uialign- -1
                     ]
                     if (= $cn $getclientnum)[
                        uifill 0.077 0.026
                     ] [
                        UIbutton "hold2" [
                           UItriangle 0xCCCCCC 0.01 0.01 270
                           uitext "Kick" 0.57
                        ] 0.077 0.026 [kick @cn] -1
                     ]
                     UIbutton "hold2" [
                        UIradio [= (isspectator $cn) 1] 0.016
                        uitext "Spec" 0.57
                     ] 0.087 0.026 [spectator @(= (isspectator $cn) 0) @cn] -1
                     UIbutton "hold2" [
                        UIradio [= (ismaster $cn) 1] 0.016
                        uitext (concatword (? (isadmin $cn) "Admin" (? (isauth $cn) "Auth" "Master"))) 0.57
                     ] 0.105 0.026 [setmaster (? (ismaster $cn) 0 1) @cn] -1
                  ] [
                     UI_mlc = (! $UI_mlc)
                     uicolor (? $UI_mlc 0x99323232 0x99262626) 0 0 [
                        uiclamp 1 1 1 1
                        pushif highlight (scoreboardhighlight $cn) [
                           uioutline 0xA0A0A0 ; uiclamp- 1 1 1 1
                        ]
                     ]
                  ]
               ]
            ]
            uialign* -1 -1
         ]
      ]
      uifill 0.01
      UIvscroll 0.02 0.624 1.56
   ]
]

UI_clientlistteam = [
   uihlist 0 [
      uiscroll 1.4 0.312 [
         uifill 1.4 0.312 [
            uihlist 0 [
               uialign 0 -1
               uitable 0 0 [
                  uifill 0.695
                  UI_mlc = 0
                  looplist cn (listclients 1 0) [
                     if (= (getclientteam $cn) 1) [
                        uitablerow [
                           uifill 0.078 0.026 [
                              uicolortext (concatword $cn " : ") (scoreboardstatus $cn) 0.57
                              uialign- 1
                           ]
                           uifill 0.26 0.026 [
                              uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.57
                              uialign- -1
                           ]
                           if (= $cn $getclientnum)[
                              uifill 0.077 0.026
                           ] [
                              UIbutton "hold2" [
                                 UItriangle 0xCCCCCC 0.01 0.01 270
                                 uitext "Kick" 0.57
                              ] 0.077 0.026 [kick @cn] -1
                           ]
                           UIbutton "hold2" [
                              UItriangle 0xCCCCCC 0.01 0.01 270
                              uitext "Team" 0.57
                           ] 0.0875 0.026 [setteam @cn @(? (=s (getclientteam $cn) "1") "2" "1")] -1
                           UIbutton "hold2" [
                              UIradio [= (isspectator $cn) 1] 0.016
                              uitext "Spec" 0.57
                           ] 0.087 0.026 [spectator @(= (isspectator $cn) 0) @cn] -1
                           UIbutton "hold2" [
                              UIradio [= (ismaster $cn) 1] 0.016
                              uitext (concatword (? (isadmin $cn) "Admin" (? (isauth $cn) "Auth" "Master"))) 0.57
                           ] 0.105 0.026 [setmaster (? (ismaster $cn) 0 1) @cn] -1
                        ] [
                           UI_mlc = (! $UI_mlc)
                           uicolor (? $UI_mlc 0x99333b40 0x99262b33) 0 0 [
                              uiclamp 1 1 1 1
                              pushif highlight (scoreboardhighlight $cn) [
                                 uioutline 0xA0A0A0 ; uiclamp- 1 1 1 1
                              ]
                           ]
                        ]
                     ]
                  ]
               ]
               uifill 0.01
               uitable 0 0 [
                  uifill 0.695
                  UI_mlc = 0
                  looplist cn (listclients 1 0) [
                     if (= (getclientteam $cn) 2) [
                        uitablerow [
                           uifill 0.078 0.026 [
                              uicolortext (concatword $cn " : ") (scoreboardstatus $cn) 0.57
                              uialign- 1
                           ]
                           uifill 0.26 0.026 [
                              uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.57
                              uialign- -1
                           ]
                           if (= $cn $getclientnum)[
                              uifill 0.077 0.026
                           ] [
                              UIbutton "hold2" [
                                 UItriangle 0 0.01 0.01 270
                                 uitext "Kick" 0.57
                              ] 0.077 0.026 [kick @cn] -1
                           ]
                           UIbutton "hold2" [
                              UItriangle 0 0.01 0.01 270
                              uitext "Team" 0.57
                           ] 0.0875 0.026 [setteam @cn @(? (=s (getclientteam $cn) "1") "2" "1")] -1
                           UIbutton "hold2" [
                              UIradio [= (isspectator $cn) 1] 0.016
                              uitext "Spec" 0.57
                           ] 0.087 0.026 [spectator @(= (isspectator $cn) 0) @cn] -1
                           UIbutton "hold2" [
                              UIradio [= (ismaster $cn) 1] 0.016
                              uitext (concatword (? (isadmin $cn) "Admin" (? (isauth $cn) "Auth" "Master"))) 0.57
                           ] 0.105 0.026 [setmaster (? (ismaster $cn) 0 1) @cn] -1
                        ] [
                           UI_mlc = (! $UI_mlc)
                           uicolor (? $UI_mlc 0x99403333 0x99332626) 0 0 [
                              uiclamp 1 1 1 1
                              pushif highlight (scoreboardhighlight $cn) [
                                 uioutline 0xA0A0A0 ; uiclamp- 1 1 1 1
                              ]
                           ]
                        ]
                     ]
                  ]
               ]
               uialign* 0 -1
            ]
         ]
      ]
      uifill 0.01
      UIvscroll 0.02 0.312 1.56
   ]
]

///////////////////////////////////////////////////////////////////////////////
//  Bots                                                                     //
///////////////////////////////////////////////////////////////////////////////
UI_botskill = 87
UI_botaddnum = 9

UImenu "bots" [
   uifill 0.65 0.27 [
      uivlist 0.01 [
         uitext (concatword "Bots: " (numbots)) 0.6
         uifill 0 0.01
         uihlist 0.01 [
            uialign 1 ; uitext "Skill Level" 0.6 ; UIhslider UI_botskill 1 100 1 0.5 0.032 [uitext $UI_botskill 0.6]
         ]
         uihlist 0.01 [
            uialign 1 ; uitext "#" 0.6 ; UIhslider UI_botaddnum 2 32 1 0.5 0.032 [uitext $UI_botaddnum 0.6]
         ]
         uifill 0 0.015
         uihlist 0.05 [
            uivlist 0.01 [
               UIbutton "" [uitext "Add Bot" 0.65 ; UI_keyref 1 0.027 0.027 "Ins" "" 0.4] 0.2 0.04 [addbot $UI_botskill]
               UIbutton "" [uitext (concatword "Add " $UI_botaddnum " Bots") 0.65] 0.2 0.04 [loop a (UI_botaddnum) [addbot $UI_botskill]]
            ]
            uivlist 0.01 [
               UIbutton "" [uitext "Delete Bot" 0.65 ; UI_keyref 1 0.027 0.027 "Del" "" 0.4] 0.2 0.04 [delbot]
               UIbutton "" [uitext "Clear Bots" 0.65] 0.2 0.04 [clearbots]
            ]
         ]
      ]
   ]
] [] [] [] "Bots"

///////////////////////////////////////////////////////////////////////////////
//  Notepad                                                                  //
///////////////////////////////////////////////////////////////////////////////
UI_notepadfile = "untitled.txt"
UI_cmc_from   = ""
UI_cmc_to     = ""

UImenu "notepad" [
   uivlist 0.01 [
      uihlist 0.01 [
         uialign -1 -2
         uifield UI_notepadfile -35 [] 0.6 [
            uioutline (uifocus? 0xFF0000 $c_line)
            uiclamp- 1 1 1 1
         ]
         UIbutton "hold2" [uitext "Load"       0.6] 0.04 0.032 [textfocus $UI_notepadfile ; textload $UI_notepadfile]
         UIbutton "hold2" [uitext "Save"       0.6] 0.04 0.032 [textfocus $UI_notepadfile ; textsave $UI_notepadfile]
         UIbutton "hold2" [uitext "Exec"       0.6] 0.04 0.032 [textfocus $UI_notepadfile ; textexec]
         uispace 0.01 0 [UIbar 0 1]
         UIbutton "hold2" [uitext "Copy"       0.6] 0.04 0.032 [textfocus $UI_notepadfile ; textcopy]
         UIbutton "hold2" [uitext "Paste"      0.6] 0.04 0.032 [textfocus $UI_notepadfile ; textpaste]
         UIbutton "hold2" [uitext "Select All" 0.6] 0.04 0.032 [textfocus $UI_notepadfile ; textselectall]
         uispace 0.01 0 [UIbar 0 1]
         UIbutton "hold2" [uitext "Clear All"  0.6] 0.04 0.032 [textfocus $UI_notepadfile ; textclear]
      ]
      uitexteditor $UI_notepadfile -80 20 1 "" 0 [
         uioutline (uifocus? 0xFF0000 $c_line)
         uiclamp- 1 1 1 1
      ]
      textinit $UI_notepadfile $UI_notepadfile
   ]
] [] [] [] "Notepad"

notepad = [
   if $numargs [UI_notepadfile = $arg1]
   showui "notepad"
]

mapnotepad = [
   UI_notepadfile = (concatword "media/map/" (? $numargs $arg1 $mapname) ".cfg")
   showui "notepad"
]

copymapcfg = [
   if $numargs [
      UI_cmc_from = $arg1
      UI_cmc_to   = (? (= $numargs 2) $arg2 $mapname)
      if (findfile (concatword "media/map/" $UI_cmc_from ".cfg")) [
         if (findfile (concatword "media/map/" $UI_cmc_to ".cfg")) [showui "cmc_confirm"] [
            textfocus "#copymapcfg"
            textload (concatword "media/map/" $UI_cmc_from ".cfg")
            textsave (concatword "media/map/" $UI_cmc_to   ".cfg")
         ]
      ] [
         echo (concatword "^f7Map config "^f8^" $UI_cmc_from "^f7^" does not exist!")
         UI_cmc_from = "" ; UI_cmc_to = ""
      ]
   ]
]

UImenu "cmc_confirm" [
   uivlist 0.02 [
      uiclamp 1 1
      UItitle [uitext "Copy map config:" 0.6] 0 0.03
      uiclamp- 1 1
      uivlist 0.01 [
         uitext (concatword "^f2" $UI_cmc_from) 0.6
         UItriangle 0 0.01 0.01 180
         uitext (concatword "^f2" $UI_cmc_to) 0.6
      ]
      uihlist 0 [
         UIbutton "hold2" [uitext "Proceed" 0.6] 0.18 0.032 [
            textfocus "#copymapcfg"
            textload (concatword "media/map/" $UI_cmc_from ".cfg")
            textsave (concatword "media/map/" $UI_cmc_to   ".cfg")
            hideui "cmc_confirm"
         ]
         UIbutton "hold2" [uitext "Cancel"  0.6] 0.18 0.032 [hideui "cmc_confirm"]
      ]
   ]
] [
   if (|| [=s $UI_cmc_from ""] [=s $UI_cmc_to ""]) [hideui "cmc_confirm"]
] [UI_cmc_from = "" ; UI_cmc_to = ""]

