// npc name
curNpcName = ""	

// npc speech
curSpeech = ""
setSpeech = [curSpeech = $arg1]

// npc answers
curAnswers = []
setAnswers = [curAnswers = $arg1]

// answers actions
curActions = []
setActions = [curActions = $arg1]

showInteract = [
	local spacer
	diagbind = (searchbinds [interact])
	if (>s $arg2 "") [spacer = " "] 
	helpmsg (concatword (getText $arg1) " " "^ff" $arg2 $spacer "^f7(^fe" $diagbind "^f7)")
]

clearDialogue = [
	curSpeech = ""
	curAnswers = []
	curActions = []
]

setDialogue = [
	clearDialogue		//resetting each inputs before setting new ones
	curNpcName = $arg1
	setSpeech $arg2
	setAnswers $arg3
	setActions $arg4
	showui "dialogue" 	// show dialogue gui when each vars are set
]

leaveDialogue = [hideui "dialogue"]

newui "dialogue" [
	uivlist 0 [
        uiclamp 1 1 1 1
		uifill 0 0.55
        uicolor $c_menu 0.01 0.01 [
            uiclamp 1 1 1 1
            uispace $UIwindow_padding $UIwindow_padding [
                uiclamp 1 1 1 1
				uivgradient 0xEEEEEE 0xBBBBBB 0 0 [
					uivlist 0 [
						UItitle [
							uihlist 0 [
								uispace 0.005  0.03
								uiimage "media/interface/hud/bulle.png" 0.05 0.05
								uispace 0.005
								uitext (at $curNpcName) 0.8
							]
							uialign* -1 0
						]
						
						uispace 0 0.007
						
						uihlist 0 [
							uispace 0.005
							uivlist 0 [
								if $isdead [uiwrapcolortext (getText $Dead_Speech) 0.95 0x222222 0.65] [uiwrapcolortext (at $curSpeech) 0.95 0x222222 0.65]
								uialign* -1 0
							]
						]
						
						uispace 0 0.007
						
						uivlist 0 [
							uivlist 0 [
								if $isdead [UIbutton "hold5" [UItriangle 0x777777 0.015 0.015 270 ; uiwrapcolortext "..." 1 0x555555 0.65] 1 0.037 [hideui "dial"] -1] [
									loop i (listlen $curAnswers) [
										UIbutton "hold5" [UItriangle 0x777777 0.015 0.015 270 ; uiwrapcolortext (at $curAnswers $i) 1 0x555555 0.65] 1 0.037 (at $curActions $i) -1
									]
								]
							]
						]
						uialign* -1 0
					]
				]
			]
			uioutline $c_line; uiclamp- 1 1 1 1
        ]
    ]
] [hidehud 1] [hidehud 0]