//used for dialogues with npcs
npcname = ["" ""]	//npc's french then english name
speech = ["" ""]	//npc's french then english speech
ans_1 = ["" ""]		//french then english answers (x5)
ans_2 = ["" ""]
ans_3 = ["" ""]
ans_4 = ["" ""]
ans_5 = ["" ""]
act_1 = []			//actions for each answer and no translation needed, cool!
act_2 = []
act_3 = []
act_4 = []
act_5 = []

clear_dial = [
	speech = 0
	ans_1 = 0 ; ans_2 = 0 ; ans_3 = 0 ; ans_4 = 0 ; ans_5 = 0 ;
	act_1 = [] ; act_2 = [] ; act_3 = [] ; act_4 = [] ; act_5 = [] ;
]

deadspeech = [
		"Alors ça fait quoi d'être mort hein ? T'as vraiment cru que c'était un bug (à la base ça en était un) et que t'allais continuer de me parler ?"
		"So how does it feel to be dead, huh? Did you think it was a bug (originally it was) and that you were allowed to keep talking to me?"
]

newui "dial" [
	uivlist 0 [
        uiclamp 1 1 1 1
		uifill 0 0.55
        uicolor $c_menu 0.01 0.01 [
            uiclamp 1 1 1 1
            uispace $UIwindow_padding $UIwindow_padding [
                uiclamp 1 1 1 1
				uivgradient 0xEEEEEE 0xBBBBBB 0 0 [
					uivlist 0 [
						UItitle [
							uihlist 0 [
								uispace 0.005  0.03
								uiimage "media/interface/hud/bulle.png" 0.05 0.05
								uispace 0.005
								uitext (at $npcname @GAME_LANG) 0.8
							]
							uialign* -1 0
						]
						
						uispace 0 0.007
						
						uihlist 0 [
							uispace 0.005
							uivlist 0 [
								if $isdead [uiwrapcolortext (at $deadspeech @GAME_LANG) 0.95 0x222222 0.65] [uiwrapcolortext (at $speech @GAME_LANG) 0.95 0x222222 0.65]
								
								uialign* -1 0
							]
						]
						
						uispace 0 0.007
						
						uivlist 0 [
							uivlist 0 [
								if $isdead [UIbutton "hold5" [UItriangle 0x777777 0.015 0.015 270 ; uiwrapcolortext "..." 1 0x555555 0.65] 1 0.037 [hideui "dial"] -1] [
									if $ans_1 [UIbutton "hold5" [UItriangle 0x777777 0.015 0.015 270 ; uiwrapcolortext (at $ans_1 @GAME_LANG) 1 0x555555 0.65] 1 0.037 [act_1] -1]
									if $ans_2 [UIbutton "hold5" [UItriangle 0x777777 0.015 0.015 270 ; uiwrapcolortext (at $ans_2 @GAME_LANG) 1 0x555555 0.65] 1 0.037 [act_2] -1]
									if $ans_3 [UIbutton "hold5" [UItriangle 0x777777 0.015 0.015 270 ; uiwrapcolortext (at $ans_3 @GAME_LANG) 1 0x555555 0.65] 1 0.037 [act_3] -1]
									if $ans_4 [UIbutton "hold5" [UItriangle 0x777777 0.015 0.015 270 ; uiwrapcolortext (at $ans_4 @GAME_LANG) 1 0x555555 0.65] 1 0.037 [act_4] -1]
									if $ans_5 [UIbutton "hold5" [UItriangle 0x777777 0.015 0.015 270 ; uiwrapcolortext (at $ans_5 @GAME_LANG) 1 0x555555 0.65] 1 0.037 [act_5] -1]
								]
							]
						]
						uialign* -1 0
					]
				]
			]
			uioutline $c_line; uiclamp- 1 1 1 1
        ]
    ]
] [hidehud 1] [hidehud 0]