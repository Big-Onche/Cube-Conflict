_sb_mm = 0
_sb_mode = 0
_sb_ping = 9999
_sb_players = -1
_sb_sort = 1
_sb_showwait = 1

UI_window "multiplayer" "media/interface/hud/map.jpg" @Multiplayer_Menu_Title [
	refreshservers
	uihgradient 0xDBD7D7 0xB2B2B9 0.8 0.005 [
	    uivlist 0.005 [
			uihlist 0.011 [
				uispace 0.0001
				uialign 1 0
				uiscroll 0.90 0.65 [
					uifill 0.90 0.65 [
						if $_sb_sort [autosortservers (! (uihover?))]
						uivlist 0.0035 [
							uialign 0 -1
							uiclamp 1 1
							loop i $numservers [
								if (servinfovalid $i) [
									if (&& [
									   <= (servinfoping $i) $_sb_ping
									] [
									   ! (& (<< 1 (servinfomastermode $i)) $_sb_mm)
									] [
									   ! (& (<< 1 (servinfomode $i)) $_sb_mode)
									] [
									   != (servinfonumplayers $i) (* $_sb_players (servinfomaxplayers $i))
									]) [
										_servers_map = (servinfomap $i)
										UIbuttonarea "server_yes" 0 0.06 [
											uirelease [connectservinfo $i ; hideallui]
											uispace 0.01 0 [
												uiclamp 1 1
												uihlist 0.01 [
													uialign -1
													uifill 0.09 0 [
														uivlist 0.002 [
															uihlist 0.006 [
																uicolortext (servinfonumplayers $i) 0x444444 0.9
																uicolortext "/" 0x777777 0.6
																uicolortext (servinfomaxplayers $i) 0x444444 0.7
															]
															uihlist 0.006 [
																uicolortext (servinfoping $i) 0x777777 0.5
																uicolortext "ms" 0x777777 0.5
															]
														]
													]
														uiimage "media/interface/cclogo.png" 0.06 0.06 [
														uiimage (concatword "media/map/" $_servers_map "_sq.png") 0.08 0.08
													]
													uifill 0.6 0 [
														uiclip 0.6 0 [
														uialign -1
														uivlist 0.0015 [
															uispace 0.01 0 [uicolortext (servinfodesc $i) 0x252525 0.6]
															uialign- -1
																uihlist 0.01 [
																	uialign -1
																	uifill 0.005
																	uihlist 0.005 [
																		uicolortext "^fs[" 0x333333 0.45
																		uitext (servinfomastermodename $i) 0.45
																		uicolortext "^fS]" 0x333333 0.45
																	]
																	UItriangle 0x606060 0.008 0.008 270
																	uicolortext (servinfomodename $i) 0x333333 0.45
																	if (!=s $_servers_map "") [
																		UItriangle 0x606060 0.008 0.008 270
																		uicolortext $_servers_map 0x333333 0.45
																	]
																	if (m_timed (servinfomode $i)) [
																		UItriangle 0x606060 0.008 0.008 270
																		uicolortext (concatword (servinfotime $i)) 0x333333 0.45
																	]
																]	
															]
														]
														UIbar 0 0 ; uialign- 1
													]
												]
												uihlist 0 [
													uialign 1 -1
													uicolortext (servinfoname $i) 0x225599 0.42
													uicolortext ":" 0x004488 0.42
													uicolortext (servinfoport $i) 0x225599 0.42
												]
											]
										]
									]
								] [
									if $_sb_showwait [UIbutton "server_no" [uicolortext (servinfodesc $i) 0x555555 0.6] 0 0.065]
								]
							]
							uiclamp* 1 1
						]
					]
				]
				UIvscroll 0.02 0.65 1.3
			]
			UIbar 1 //----------------------------------------------------------------------------------------------------	
			uihlist 0 [
				uiclamp 1 1
				//UIbutton "" [uitext "Update List" 0.6] 0.2 0.032 [updatefrommaster] 0 [uialign -1 0]
				uihlist 0 [
					uispace 0.005
					UIbutton "" [
						UIcheckbox $searchlan 0.016
						uitext (at $Multiplayer_Menu_SearchLan) 0.6
					] 0.18 0.032 [searchlan (! $searchlan)] -1
				//	UIbutton "" [
				//		UIcheckbox $autoupdateservers 0.016
				//		uitext "Auto-refresh" 0.6
				//	] 0.18 0.032 [autoupdateservers (! $autoupdateservers)] -1
				//	UIbutton "" [
				//		UIcheckbox $autosortservers 0.016
				//		uitext "Auto-sort" 0.6
				//	] 0.18 0.032 [
				//		_sb_sort = (! $_sb_sort)
				//		autosortservers $_sb_sort
				//	] -1
				]
				//UIbutton "" [
				//	UItriangle 0xCCCCCC 0.01 0.01 270
				//	uitext "Filters" 0.6
				//] 0.2 0.032 [showui "sb_filters"] 0 [uialign 1 0]
			]
			UIbar 1 //----------------------------------------------------------------------------------------------------
			uiwrapcolortext (at $Multiplayer_Menu_ConditionsRead) 0x111111 0.70 0.45	
			uihlist 0 [
				UIbutton "hold1" [
					uitext (at $Multiplayer_Menu_Terms) 0.5
				] 0 0.025 [showui "rules"] -1
				uispace 0.002
				UIbutton "hold1" [
					uitext (at $Multiplayer_Menu_Privacy) 0.5
				] 0 0.025 [showui "privacy"] -1
			]
			uialign -1 -1
			uispace 0 0.005
		]
	]
] [hideui "main"] [if (|| (uivisible "rules") (uivisible "privacy")) [] [showui "main"]]

UI_rulelist = [
    uispace 0 0.01 [
        uispace 0.02 0 [
            uivlist 0 [
			   uiwrapcolortext [@@@@arg1] 0.9 0x000000 0.6
            ]
		]
		uialign* -1 0
    ]
    uispace 0 0 [
        uispace 0.02 0 [
            uivlist 0 [
			   uiwrapcolortext [@@@@arg2] 0.78 0x555555 0.45
			   
            ]
        ]
		uialign* -1 0
	]
	uispace 0 0.005
]

UI_window "rules" "media/interface/hud/info.jpg" @Multiplayer_Menu_Terms [
	uivgradient 0xEEEEEE 0xBBBBBB $arg2 $arg3 [
	    uihlist 0 [
			uiscroll 0.85 0.75 [
				uifill 0.85
				uivlist 0 [
					loop i (div (listlen $Multiplayer_FullTerms) 2) [
						title = (at $Multiplayer_FullTerms (* $i 2))
						text = (at $Multiplayer_FullTerms (+ (* $i 2) 1))
						UI_rulelist $title $text
					]
					uiclamp* 1 1
				]
			]
			UIvscroll 0.02 0.75 2.0
	    ]
	]
] [hideui "multiplayer"] [showui "multiplayer"]

UI_window "privacy" "media/interface/hud/info.jpg" @Multiplayer_Menu_Privacy [
	uivgradient 0xEEEEEE 0xBBBBBB $arg2 $arg3 [
	    uihlist 0 [
			uiscroll 0.85 0.75 [
				uifill 0.85
				uivlist 0 [
					loop i (div (listlen $Multiplayer_FullPrivacy) 2) [
						title = (at $Multiplayer_FullPrivacy (* $i 2))
						text = (at $Multiplayer_FullPrivacy (+ (* $i 2) 1))
						UI_rulelist $title $text
					]
					uiclamp* 1 1
				]
			]
			UIvscroll 0.02 0.75 2.0
	    ]
	]
] [hideui "multiplayer"] [showui "multiplayer"]

// quickfilterlist ["NAME" [children] [action] ..] <COLUMNS> <X PADDING> <Y PADDING> <BUTTON WIDTH> <BUTTON HEIGHT>
quickfilterlist = [
   uigrid $arg2 $arg3 $arg4 [
      looplist3 n c a $arg1 [
         if (!=s $n "") [
            UIbutton "hold2" [
               c ; uitext $n 0.6
            ] $arg5 $arg6 $a -1
         ] [uifill $arg5 $arg6]
      ]
   ]
]

UImenu "sb_filters" [
   uigrid 2 0.01 0.02 [
      uivlist 0.01 [
         UItitle [uitext "Mastermode" 0.7] 0.28 0.03
         quickfilterlist [
            "Open"     [UIcheckbox [& $_sb_mm  1] 0.016] [_sb_mm = (^ $_sb_mm  1)]
            "Veto"     [UIcheckbox [& $_sb_mm  2] 0.016] [_sb_mm = (^ $_sb_mm  2)]
            "Locked"   [UIcheckbox [& $_sb_mm  4] 0.016] [_sb_mm = (^ $_sb_mm  4)]
            "Private"  [UIcheckbox [& $_sb_mm  8] 0.016] [_sb_mm = (^ $_sb_mm  8)]
            "Password" [UIcheckbox [& $_sb_mm 16] 0.016] [_sb_mm = (^ $_sb_mm 16)]
            "Disable"  [UIradio    [= $_sb_mm  0] 0.016] [_sb_mm = 0]
         ] 2 0 0 0.14 0.032
      ]
      uivlist 0.01 [
         UItitle [uitext "Ping" 0.7] 0.28 0.03
         quickfilterlist [
            "50+"     [UIradio [= $_sb_ping   55] 0.016] [_sb_ping =   55]
            "100+"    [UIradio [= $_sb_ping  110] 0.016] [_sb_ping =  110]
            "150+"    [UIradio [= $_sb_ping  165] 0.016] [_sb_ping =  165]
            "250+"    [UIradio [= $_sb_ping  270] 0.016] [_sb_ping =  270]
            "500+"    [UIradio [= $_sb_ping  525] 0.016] [_sb_ping =  525]
            "Disable" [UIradio [= $_sb_ping 9999] 0.016] [_sb_ping = 9999]
         ] 2 0 0 0.14 0.032
      ]
      uivlist 0.01 [
         UItitle [uitext "Gamemode" 0.7] 0.28 0.03
         quickfilterlist [
            "rDM"       [UIcheckbox [& $_sb_mode  2] 0.016] [_sb_mode = (^ $_sb_mode  2)]
            "pDM"       [UIcheckbox [& $_sb_mode  4] 0.016] [_sb_mode = (^ $_sb_mode  4)]
            "rTDM"      [UIcheckbox [& $_sb_mode  8] 0.016] [_sb_mode = (^ $_sb_mode  8)]
            "pTDM"      [UIcheckbox [& $_sb_mode 16] 0.016] [_sb_mode = (^ $_sb_mode 16)]
            "rCTF"      [UIcheckbox [& $_sb_mode 32] 0.016] [_sb_mode = (^ $_sb_mode 32)]
            "pCTF"      [UIcheckbox [& $_sb_mode 64] 0.016] [_sb_mode = (^ $_sb_mode 64)]
            "Edit"      [UIcheckbox [& $_sb_mode  1] 0.016] [_sb_mode = (^ $_sb_mode  1)]
            "Disable"   [UIradio    [= $_sb_mode  0] 0.016] [_sb_mode = 0]
         ] 2 0 0 0.14 0.032
      ]
      uivlist 0.01 [
         UItitle [uitext "Players" 0.7] 0.28 0.03
         quickfilterlist [
            "Empty"   [UIradio [= $_sb_players  0] 0.016] [_sb_players =  0]
            "Full"    [UIradio [= $_sb_players  1] 0.016] [_sb_players =  1]
            "" "" ""
            "Disable" [UIradio [= $_sb_players -1] 0.016] [_sb_players = -1]
         ] 2 0 0 0.14 0.032
      ]
   ]
] [] [] [] "Server Filters"

overflowtitle = ["Déconnecté : Débordement de donnée.""Disconnected: Connection overflow."]
overflowdesc = [
	"Si votre connexion internet est faible, essayez de ne rien télécharger en jouant et assurez-vous que personne d'autre n'utilise votre connexion lorsque vous êtes connecté au jeu.^n^n^fhVos statistiques, succès et crédits gagnés durant cette session sont conservés."
	"If your internet connection is weak, try not downloading anything while playing and check that no one else is using your internet connection when you are connected to the game.^n^n^fhYour stats, achievements, and credits earned during this session are kept."
]
UImenu "overflowpopup" [ /////////////////////////////////////////////////////////////////////////////// DISC OVERFLOW POPUP //////////////////////////////////////////////////////////////////////
	uivgradient 0xDDCCCC 0xAA9999 0.1 0.02 [
	   uivlist 0.005 [
			uispace 0.005
			uicolortext (at $overflowtitle @GAME_LANG) 0xCC0000 0.8
			UIbar 1
			uihlist 0 [uispace 0.005 ; uiwrapcolortext (at $overflowdesc @GAME_LANG) 0.95 0x333333 0.65 ; uialign -1]
			uihlist 0 [UIbutton "hold5" [uiimage "media/interface/hud/checkbox_on.jpg" 0.04 0.04 ; uicolortext "Ok" 0x222222 0.8] 0.90 0.035 [hideallui ; showui "multiplayer"]]
			uispace 0.005
	    ]
	]
] [] [hideui "main" ; showui "multiplayer"] [] 0

servstoptitle = ["Déconnecté : Serveur en cours d'arrêt." "Disconnected: Server shutting down."]
servstopdesc = [
	"Le serveur est sur le point d'être arrêté par un administrateur. Si vous jouiez sur un serveur officiel, il redémarrera dans les prochaines minutes.^n^n^fhVos statistiques, succès et crédits gagnés durant cette session sont conservés."
	"The server is about to be shut down by an administrator. If you were playing on an official server, it will restart in the next few minutes.^n^n^fhYour stats, achievements, and credits earned during this session are kept."
]
UImenu "servstoppopup" [ /////////////////////////////////////////////////////////////////////////////// DISC SERVER STOP POPUP //////////////////////////////////////////////////////////////////////
	uivgradient 0xDDCCCC 0xAA9999 0.1 0.02 [
	   uivlist 0.005 [
			uispace 0.005
			uicolortext (at $servstoptitle @GAME_LANG) 0xCC0000 0.8
			UIbar 1
			uihlist 0 [uispace 0.005 ; uiwrapcolortext (at $servstopdesc @GAME_LANG) 0.95 0x333333 0.65 ; uialign -1]
			uihlist 0 [UIbutton "hold5" [uiimage "media/interface/hud/checkbox_on.jpg" 0.04 0.04 ; uicolortext "Ok" 0x222222 0.8] 0.90 0.035 [hideallui ; showui "multiplayer"]]
			uispace 0.005
	    ]
	]
] [] [hideui "main" ; showui "multiplayer"] [] 0

msgerrtitle = ["Déconnecté : Erreur de client/serveur.""Disconnected: Client/server error."]
msgerrdesc = [
	"Une erreur lors d'un échange entre votre jeu et le serveur est survenue. Cela peut survenir si un développeur s'est chié de dessus ou si vous utiliez un jeu modifié.^n^n^fhVos statistiques, succès et crédits gagnés durant cette session sont conservés."
	"An error occurred during an exchange between your game and the server. This can happen if a developer was high while coding or if you were using a modded game.^n^n^fhYour stats, achievements, and credits earned during this session are kept."
]
UImenu "msgerrpopup" [ /////////////////////////////////////////////////////////////////////////////// DISC SERVER STOP POPUP //////////////////////////////////////////////////////////////////////
	uivgradient 0xDDCCCC 0xAA9999 0.1 0.02 [
	   uivlist 0.005 [
			uispace 0.005
			uicolortext (at $msgerrtitle @GAME_LANG) 0xCC0000 0.8
			UIbar 1
			uihlist 0 [uispace 0.005 ; uiwrapcolortext (at $msgerrdesc @GAME_LANG) 0.95 0x333333 0.65 ; uialign -1]
			uihlist 0 [UIbutton "hold5" [uiimage "media/interface/hud/checkbox_on.jpg" 0.04 0.04 ; uicolortext "Ok" 0x222222 0.8] 0.90 0.035 [hideallui ; showui "multiplayer"]]
			uispace 0.005
	    ]
	]
] [] [hideui "main" ; showui "multiplayer"] [] 0