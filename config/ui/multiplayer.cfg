///////////////////////////////////////////////////////////////////////////////
//  Server Browser                                                           //
///////////////////////////////////////////////////////////////////////////////

_sb_mm = 0
_sb_mode = 0
_sb_ping = 9999
_sb_players = -1
_sb_sort = 1
_sb_showwait = 1

mapnames_en = [
	"Tutorial"
	"Village"
	"Factory"
	"Moon"
	"Castle"
	"Volcano"
]

mapnames_fr = [
	"Tutoriel"
	"Village"
	"Usine"
	"Lune"
	"Chateau"
	"Volcan"
]

// quickfilterlist ["NAME" [children] [action] ..] <COLUMNS> <X PADDING> <Y PADDING> <BUTTON WIDTH> <BUTTON HEIGHT>
quickfilterlist = [
   uigrid $arg2 $arg3 $arg4 [
      looplist3 n c a $arg1 [
         if (!=s $n "") [
            UIbutton "hold2" [
               c ; uitext $n 0.6
            ] $arg5 $arg6 $a -1
         ] [uifill $arg5 $arg6]
      ]
   ]
]

UImenu "sb_filters" [
   uigrid 2 0.01 0.02 [
      uivlist 0.01 [
         UItitle [uitext "Mastermode" 0.7] 0.28 0.03
         quickfilterlist [
            "Open"     [UIcheckbox [& $_sb_mm  1] 0.016] [_sb_mm = (^ $_sb_mm  1)]
            "Veto"     [UIcheckbox [& $_sb_mm  2] 0.016] [_sb_mm = (^ $_sb_mm  2)]
            "Locked"   [UIcheckbox [& $_sb_mm  4] 0.016] [_sb_mm = (^ $_sb_mm  4)]
            "Private"  [UIcheckbox [& $_sb_mm  8] 0.016] [_sb_mm = (^ $_sb_mm  8)]
            "Password" [UIcheckbox [& $_sb_mm 16] 0.016] [_sb_mm = (^ $_sb_mm 16)]
            "Disable"  [UIradio    [= $_sb_mm  0] 0.016] [_sb_mm = 0]
         ] 2 0 0 0.14 0.032
      ]
      uivlist 0.01 [
         UItitle [uitext "Ping" 0.7] 0.28 0.03
         quickfilterlist [
            "50+"     [UIradio [= $_sb_ping   55] 0.016] [_sb_ping =   55]
            "100+"    [UIradio [= $_sb_ping  110] 0.016] [_sb_ping =  110]
            "150+"    [UIradio [= $_sb_ping  165] 0.016] [_sb_ping =  165]
            "250+"    [UIradio [= $_sb_ping  270] 0.016] [_sb_ping =  270]
            "500+"    [UIradio [= $_sb_ping  525] 0.016] [_sb_ping =  525]
            "Disable" [UIradio [= $_sb_ping 9999] 0.016] [_sb_ping = 9999]
         ] 2 0 0 0.14 0.032
      ]
      uivlist 0.01 [
         UItitle [uitext "Gamemode" 0.7] 0.28 0.03
         quickfilterlist [
            "rDM"       [UIcheckbox [& $_sb_mode  2] 0.016] [_sb_mode = (^ $_sb_mode  2)]
            "pDM"       [UIcheckbox [& $_sb_mode  4] 0.016] [_sb_mode = (^ $_sb_mode  4)]
            "rTDM"      [UIcheckbox [& $_sb_mode  8] 0.016] [_sb_mode = (^ $_sb_mode  8)]
            "pTDM"      [UIcheckbox [& $_sb_mode 16] 0.016] [_sb_mode = (^ $_sb_mode 16)]
            "rCTF"      [UIcheckbox [& $_sb_mode 32] 0.016] [_sb_mode = (^ $_sb_mode 32)]
            "pCTF"      [UIcheckbox [& $_sb_mode 64] 0.016] [_sb_mode = (^ $_sb_mode 64)]
            "Edit"      [UIcheckbox [& $_sb_mode  1] 0.016] [_sb_mode = (^ $_sb_mode  1)]
            "Disable"   [UIradio    [= $_sb_mode  0] 0.016] [_sb_mode = 0]
         ] 2 0 0 0.14 0.032
      ]
      uivlist 0.01 [
         UItitle [uitext "Players" 0.7] 0.28 0.03
         quickfilterlist [
            "Empty"   [UIradio [= $_sb_players  0] 0.016] [_sb_players =  0]
            "Full"    [UIradio [= $_sb_players  1] 0.016] [_sb_players =  1]
            "" "" ""
            "Disable" [UIradio [= $_sb_players -1] 0.016] [_sb_players = -1]
         ] 2 0 0 0.14 0.032
      ]
   ]
] [] [] [] "Server Filters"


show_servers = [
	if $_sb_sort [autosortservers (! (uihover?))]			
	uivlist 0.0035 [			
		uialign 0 -1
		uiclamp 1 1
		loop i $numservers [
			if (servinfovalid $i) [
				if (&& [
					<= (servinfoping $i) $_sb_ping
					] [
						 ! (& (<< 1 (servinfomastermode $i)) $_sb_mm)
					] [
						 ! (& (<< 1 (servinfomode $i)) $_sb_mode)
					] [
						 != (servinfonumplayers $i) (* $_sb_players (servinfomaxplayers $i))
					]) [
						_servers_map = (servinfomap $i)
						UIbuttonarea "" 0 0.065 [
							uirelease [connectservinfo $i]
							uispace 0.01 0 [
								uiclamp 1 1
								uihlist 0.01 [
									uialign -1
									uifill 0.09 0 [
										uivlist 0.002 [
											uihlist 0.006 [
												uitext (servinfonumplayers $i) 0.8
												uitext "/" 0.6
												uitext (servinfomaxplayers $i) 0.6
											]
											uihlist 0.006 [
												uitext (servinfoping $i) 0.6
															uitext "ms" 0.55
											]
										]
									]
									
									uiimage "media/interface/cclogo.png" 0.06 0.06 [
										uiimage (concatword "media/map/" $_servers_map ".png") 0.06 0.06
									]
									
									uifill 0.10 0 [
										uialign -1
										uivlist 0 [
											uihlist 0 [	
												uialign -1	
												uitext (servinfodesc $i) 0.55
											]
											uihlist 0 [	
												uialign -1
												uicolortext "(" 0xBBCCFF 0.4
												uicolortext (servinfoname $i) 0xBBCCFF 0.4
												uicolortext ":" 0xBBCCFF 0.4
												uicolortext (servinfoport $i) 0xBBCCFF 0.4
												uicolortext ")" 0xBBCCFF 0.4
											]
											uihlist 0.010 [
												uialign -1
												uihlist 0.005 [
													uitext "^fs[" 0.4
													uitext (servinfomastermodename $i) 0.4
													uitext "^fS]" 0.4
												]
												UItriangle 0xFFFFFF 0.007 0.007 270
												uitext (servinfomodename $i) 0.4
												if (!=s $_servers_map "") [
													UItriangle 0xFFFFFF 0.007 0.007 270
													if @GAME_LANG [maptitle = (at $mapnames_en $_servers_map)][maptitle = (at $mapnames_fr $_servers_map)]
													uitext $maptitle 0.4
												]
												if (m_timed (servinfomode $i)) [
													UItriangle 0xFFFFFF 0.007 0.007 270
													uitext (concatword "^f8" (servinfotime $i)) 0.4
												]
											]
										]
									uialign 1
									]
								]
								uihlist 0.001 [
									uialign 1
									if (&& [=s $connectedip (servinfoname $i)] [= $connectedport (servinfoport $i)]) [
										UItriangle 0xCCCCCC 0.04 0.015 90
									] [uifill 0.015 0]
								]
							]
						]
					]
				] 	[
				if $_sb_showwait [
					// could offer customised messages one day
					UIbutton "" [uitext (servinfodesc $i) 0.6] 0 0.065
				]
			]
		]
	uiclamp* 1 1
	uifill 0.64
	]			
]

UI_rulelist = [
    uispace 0 0.01 [
        uispace 0.02 0 [
            uivlist 0 [
			   uiwrapcolortext [@@@@arg1] 0.9 0x000000 0.6
            ]
		]
		uialign* -1 0
    ]
    uispace 0 0 [
        uispace 0.02 0 [
            uivlist 0 [
			   uiwrapcolortext [@@@@arg2] 0.78 0x555555 0.45
			   
            ]
        ]
		uialign* -1 0
	]
	uispace 0 0.005
]

UImenu "rules" [
	uivgradient 0xEEEEEE 0xBBBBBB $arg2 $arg3 [
	    uihlist 0 [
			uiscroll 0.85 0.75 [
				uifill 0.85
				uivlist 0 [
					if @GAME_LANG [
						UI_rulelist "1. Preamble" "To ensure the best experience for all users, please read and apply these terms of service."
						UI_rulelist "2. Reprehensible behaviour" "Please do not send unlawful messages or commit forbidden acts under applicable law in your geographic area while connected to our official servers."
						UI_rulelist "3. Cheats and program modding" "Modification of the program (^"modding^") is allowed and has no limit, thanks to the availability of the source code. However, when playing online, please do not use modifications that could:^n - Give you an advantage over a player with an unmodified program.^n - Harm the stability and (or) efficiency of our servers.^n - Violate applicable laws in your geographic area."
						UI_rulelist "4. Restrictions on access to our online services" "We reserve the right to limit access to online features temporarily or permanently for any user who does not comply with these rules."
						UI_rulelist "5. Age restrictions" "You agree that you have the legal age under applicable law in your geographic area to use our online services. If your geographical area does not have any law regarding the legal age to access online services, you agree to be at least 16 years old."
						UI_rulelist "6. Disclaimer" "We cannot be held responsible if these rules are not applied to third-party servers. If you want to play safely, please play only on our official servers."
						UI_rulelist "7. Terms of service modifications" "These terms of service may be modified at any time without notice, and we encourage our users to consult them regularly. By connecting to our servers, you agree to be bound by the then-current version of these Terms of service."
						UI_rulelist "8. Your acceptance of these terms of service" "By playing on our official servers, you acknowledge having read and accepted these terms of service. If you disagree with these rules, please do not play on our official servers."
					][
						UI_rulelist "1. Préambule" "Afin de garantir une expérience optimale pour tous les utilisateurs, veuillez lire et appliquer ce règlement."
						UI_rulelist "2. Comportement répréhensibles par la loi" "Veuillez ne pas tenir des propos ou commettre des actes répréhensibles par la loi en vigueur dans votre zone géographique lorsque vous êtes connectés sur nos serveurs officiels."
						UI_rulelist "3. Triche et modification du programme" "La modification du programme (^"modding^") est autorisée et n'a aucune limite, étant-donné la nature ouverte du projet et la mise à disposition du code source. Cependant, lors de votre utilisation du jeu en ligne, veuillez ne pas utiliser des modifications qui pourraient :^n - Vous donner un avantage par rapport à un joueur avec un programme non modifié.^n - Nuire à la stabilité et/ou à l'efficacité de nos serveurs.^n - Contrevenir aux lois en vigueur dans votre zone géographique."
						UI_rulelist "4. Limitations des accès à nos services en ligne" "Nous nous réservons le droit de limiter l'accès aux fonctionnalités en ligne de manière temporaire à définitive pour tout utilisateur ne respectant pas ce règlement."
						UI_rulelist "5. Restrictions d'âge" "Vous vous engagez à avoir la majorité numérique appliquée dans votre zone géographique pour utiliser nos services en ligne. Si votre zone géographique ne possède aucune loi en matière de majorité numérique, vous vous engagez à être âgé d'au minimum 16 ans."
						UI_rulelist "6. Clause de non-responsabilité" "Nous ne pouvons pas être tenus responsables dans le cas où ce règlement ne serait pas appliqué sur des serveurs tiers. Si vous souhaitez jouer en toute sécurité, ne jouez que sur nos serveurs officiels."
						UI_rulelist "7. Modifications des conditions d'utilisation." "Ces conditions d'utilisation peuvent êtres modifiées à tout moment. Nous invitons nos utilisateurs à consulter régulièrement celles-ci. En vous connectant sur nos serveurs, vous acceptez d'être lié par la version alors en vigueur de ces conditions d'utilisation."
						UI_rulelist "8. Votre acceptation de ces conditions" "En vous connectant à nos serveurs officiels, vous reconnaissez avoir lu et accepté ces conditions. Si vous n'acceptez pas ce règlement, veuillez ne pas jouer en ligne sur nos serveurs officiels."
					]
					uiclamp* 1 1
				]
			]
			UIvscroll 0.02 0.75 2.0
	    ]
	]
] [] [] [] 0 "Terms of service" "Conditions d'utilisation" "media/interface/hud/info.jpg"

UImenu "privacy" [
	uivgradient 0xEEEEEE 0xBBBBBB $arg2 $arg3 [
	    uihlist 0 [
			uiscroll 0.85 0.75 [
				uifill 0.85
				uivlist 0 [
					if @GAME_LANG [
						UI_rulelist "1. Preamble" "This program collects minimal data about you. Only connecting to our official servers through the program will result in minimal data collection. This includes your IP address as well as game usage statistics."
						UI_rulelist "2. IP addresses" "By connecting to an official server, your IP address is temporarily stored, which is unavoidable for any online communication. However, we reserve the right to permanently store the IP addresses of users violating our rules for banning purposes to guarantee an optimal experience for other users."
						UI_rulelist "3. Statistical data" "Statistical data related to your use of the game will be collected when you connect online, such as your game statistics or your use of virtual elements (weapons, classes, shields, etc.) present in the game. These data allow the game to be analyzed to track bugs or make necessary balances to improve the game."
						UI_rulelist "4. Disclaimer" "We cannot be held responsible for data collected while playing on third-party servers. If you want to play safely, please play only on our official servers."
						UI_rulelist "5. Privacy policy modifications" "This Privacy policy may be modified at any time without notice, and we encourage our users to consult them regularly. By connecting to our servers, you agree to be bound by the then-current version of this Privacy policy."
						UI_rulelist "6. Your acceptance of this Privacy policy" "By connecting to our official servers, you acknowledge having read and accepted these conditions. If you disagree with this privacy policy, please do not play online on our servers."
					][
						UI_rulelist "1. Préambule" "Ce programme collecte le minimum de données vous concernant. Seule une connexion sur nos serveurs officiels via le programme engendrera une collecte minimale de données. Ceci inclus votre adresse IP ainsi que des statistiques sur l'utilisation du jeu."
						UI_rulelist "2. Adresses IP" "La connexion à nos serveurs officiels via le programme engendre uniquement le stockage temporaire de votre adresse IP, ce qui est inévitable pour toute communication en ligne. Cependant, nous nous réservons le droit de stocker de manière permanante, les adresses IP d'utilisateurs enfreignant notre règlement à des fins de bannissement, afin de garantir une expérience optimale pour les autres utilisateurs."
						UI_rulelist "3. Données statistiques" "Des données statistiques liées à votre utilisation du jeu seront collectées lors de votre connexion en ligne, comme vos statistiques de jeu ou votre utilisation d'éléments virtuels (armes, aptitudes, boucliers, etc...) présents en jeu. Ces données permettent d'analyser le jeu afin de procéder à diverses corrections de bugs ou équilibrages nécessaires à l'amélioration du jeu."
						UI_rulelist "4. Clause de non-responsabilité" "Nous ne pouvons pas être tenus responsables des données collectées en jouant sur des serveurs tiers. Si vous souhaitez jouer en toute sécurité, ne jouez que sur nos serveurs officiels."
						UI_rulelist "5. Modifications de la politique de confidentialité." "Cette politique de confidentialité peut être modifiée à tout moment. Nous invitons nos utilisateurs à consulter régulièrement celle-ci. En vous connectant sur nos serveurs, vous acceptez d'être lié par la version alors en vigueur de cette politique de confidentialité."
						UI_rulelist "6. Votre acceptation de ces conditions" "En vous connectant à nos serveurs officiels, vous reconnaissez avoir lu et accepté ces conditions. Si vous n'acceptez pas cette politique de confidentialité, veuillez ne pas jouer en ligne sur nos serveurs officiels."
					]
					uiclamp* 1 1
				]
			]
			UIvscroll 0.02 0.75 2.0
	    ]
	]
] [] [] [] 0 "Privacy policy" "Politique de confidentialité" "media/interface/hud/info.jpg"

show_multijoueurs = [
    refreshservers
	uitarget 0.7 0.7 [
		uivlist 0 [
			UItitle [
				uihlist 0.01 [
					uialign -1 0
					uiimage "media/interface/hud/server.jpg" 0.045 0.045
					if @GAME_LANG [uitext "^fbMULTIPLAYER" 0.65][uitext "^fbMULTIJOUEUR" 0.65]
				]
			]
			
			uifill 0 0.35 [
				show_servers
			]
			
			uihlist 0 [
				UIbutton "" [
					UIcheckbox $searchlan 0.016
					if @GAME_LANG [uitext "Search LAN server" 0.6][uitext "Rechercher un serveur LAN" 0.6]
				] 0.18 0.032 [searchlan (! $searchlan)] -1
			]
			uialign -1 -1
			
			uispace 0 0.0025
			UIbar 1 1
			uispace 0 0.0025
			
			if @GAME_LANG [uiwraptext "^fbBy playing on our servers, you acknowledge having read our conditions:" 0.70 0.45][uiwraptext "^fbEn jouant sur nos serveurs, vous reconnaissez avoir lu nos conditions :" 0.70 0.45]
			
			uispace 0 0.005
			
			uihlist 0 [
				UIbutton "hold1" [
					if @GAME_LANG [uitext "^f7Terms of service" 0.5][uitext "^f7Conditions d'utilisation" 0.5]
				] 0 0.025 [showui "rules"] -1
				uispace 0.002
				UIbutton "hold1" [
					if @GAME_LANG [uitext "^f7Privacy policy" 0.5][uitext "^f7Politique de confidentialité" 0.5]
				] 0 0.025 [showui "privacy"] -1
			]
			uialign -1 -1
		]
	]
]