Cust_Icons = ["media/interface/hud/cool.jpg" "media/interface/hud/bave.jpg" "media/interface/hud/cape.png" "media/interface/hud/grave.png"]

sel_cust_tab = 0
c_unknown = 0x999999
c_common = 0xC0C0C0
c_uncommon = 0x90EE90
c_rare = 0x87CEEB
c_legendary = 0xDDA0DD
c_godly = 0xEEE544

UI_window "account" "media/interface/cclogo.png" @Account_Menu_Title [
	uivgradient 0xEEEEEE 0xBBBBBB 0.9 0.7 [
		uihlist 0 [
		
			uispace 0.001
			
			uivlist 0 [
				loop i 4 [
					uihlist 0 [
						uihover [
							w = (*f $tab_w 1.03) ; h = (*f $tab_h 1.05) ; ico_s = (*f $tab_ico_s 1.05) ; text_s = (*f $tab_txt_s 1.05) ; col_up = $c_tab_hv_up ; col_dn = $c_tab_hv_dn
						][
							w = $tab_w ; h = $tab_h ; ico_s = $tab_ico_s ; text_s = $tab_txt_s ; col_up = $c_tab_up ; col_dn = $c_tab_dn
						]
						if (= $i $sel_cust_tab) [col_up = $c_tab_on_up ; col_dn = $c_tab_on_dn]
						uitarget 0.29 0.11 [
							uivgradient $col_up $col_dn $w $h [
								UI_outline
								uihlist 0 [
									uispace 0.003
									uiimage (at $Cust_Icons $i) $ico_s $ico_s
									uispace 0.003
									uicolortext (at $Account_Menu_Tabs $i) 0xFFFFFF $text_s
								]
								uialign- -1
							]
							uirelease [sel_cust_tab = $i]
						]
					]
				]
			]
			
			uispace 0.001
		
			UIbar 0 1
			
			uivlist 0 [
				uivgradient 0x96B9D0 0x76A0CA 0.65 0.1 [
					uispace 0.3 0.03
					uihlist 0 [
						uispace 0.005
						uiimage (at $Cust_Icons $sel_cust_tab) 0.07 0.07
						uispace 0.01
						uicolortext (at $Account_Menu_Tabs $sel_cust_tab) 0x252525 0.8
					]
					uialign* -1
					uiclamp 1 1 1 1
				]
				
				uihlist 0 [
					if (= $sel_cust_tab 0) [uifill 0.72 0.607 [show_account]] [
						uiscroll 0.7 0.607 [
							uifill 0.7 0.607 [
								uivlist 0 [
									case $sel_cust_tab 1 [show_smileys]
									case $sel_cust_tab 2 [show_capes]	
									case $sel_cust_tab 3 [show_graves]
								]
							]
						]
						UIvscroll 0.02 0.6 1.3
					]
				]
			]
		]
	]
] [hideui "main"] [showui "main"]

acc_weapon = 0
acc_team = 1
acc_preview_yaw = 0

show_account = [
	uivlist 0 [
		uispace 0 0.0015
		uitarget 0.7 0.05 [
			uihover [setgradcol = 0xE5DFDB] [setgradcol = 0xDAD4D1]
			uimodhgradient $setgradcol 0xFFFFFF 0.7 0.05 [
				uihlist 0 [
					uispace 0.005
					uitarget 0.32 0 [uicolortext (at $Account_Menu_Nickname) 0x222222 0.65 ; uialign* -1]	
					uivgradient $c_field1 $c_field2 $arg2 $arg3 [uifield name 25 [] 0.7 [uioutline (uifocus? $c_focus 0x444444)]]
				]
				uialign* -1
			]
		]
		uialign* -1
		
		uispace 0 0.0015
		
		uitarget 0.7 0.05 [
			uihover [setgradcol = 0xE5DFDB] [setgradcol = 0xDAD4D1]
			uimodhgradient $setgradcol 0xFFFFFF 0.7 0.05 [
				uihlist 0 [
					uispace 0.005
					uitarget 0.42 0 [uicolortext (at $Account_Menu_CreateName) 0x222222 0.65 ; uialign* -1]	
					UIbutton "hold5" [uicolortext (at $Account_Menu_GenerateName) 0x222222 0.65 ; uialign 0] 0.25 0 [createNickname $TRUE] -1
				]
				uialign* -1
			]
		]
		uialign* -1
		
		uispace 0 0.0015
		
		uitarget 0.7 0.05 [
			if (usingsteam) [txtcol = 0x222222 ; mingradcol = 0x000000 ; btnstyle = "hold5"][txtcol = 0x777777 ; mingradcol = 0x222222 ; btnstyle = "off"]
			uihover [setgradcol = 0xE5DFDB] [setgradcol = 0xDAD4D1]
			setgradcol = (- $setgradcol $mingradcol)
			uimodhgradient $setgradcol 0xFFFFFF 0.7 0.05 [
				uihlist 0 [
					uispace 0.005
					uitarget 0.42 0 [uicolortext (at $Account_Menu_UseSteamName) $txtcol 0.65 ; uialign* -1]	
					UIbutton $btnstyle [uicolortext (at $Account_Menu_GetSteamName) $txtcol 0.65 ; uialign 0] 0.25 0 [getsteamname] -1
				]
				uialign* -1
			]
		]
		uialign* -1
		
		uihlist 0 [
            acc_preview_yaw_fix = (+ $acc_preview_yaw 200)
			uiplayerpreview $playermodel $capeskin 1 $acc_team $acc_weapon 0.38 0.3 $acc_preview_yaw_fix
			uimodelpreview (concatword "graves/" (at $grave_dir $graveskin)) "mapmodel" 0.32 0.3 $acc_preview_yaw_fix
		]

		uitarget 0.6 0.045 [
			uihover [setgradsize = 0.038 ; setgradcol = 0xE5DFDB] [setgradsize = 0.035 ; setgradcol = 0xDAD4D1]
			uimodhgradient $setgradcol 0xFFFFFF 0.71 0.045 [
				uihlist 0 [
					uispace 0.005
					UIcheckbox $acc_team 0.02
					uispace 0.005
					uicolortext (at $Account_Menu_TeamColor) 0x222222 0.6
				]
				uialign* -1
				uirelease [acc_team (! $acc_team)]
			]
		]
		
		uispace 0 0.0015
		
		uitarget 0.6 0.045 [
			uihover [setgradsize = 0.038 ; setgradcol = 0xE5DFDB] [setgradsize = 0.035 ; setgradcol = 0xDAD4D1]
			uimodhgradient $setgradcol 0xFFFFFF 0.71 0.045 [
				uihlist 0 [
					uispace 0.005
					uitarget 0.25 0.03 [uicolortext (at $Account_Menu_ViewAngle) 0x222222 0.6 ; uialign* -1]
					UIhslider acc_preview_yaw 0 360 1 0.42 0.025 [uitext $acc_preview_yaw 0.6]
				]
			]
		]
		
		uispace 0 0.0015
		
		uitarget 0.6 0.045 [
			uihover [setgradsize = 0.038 ; setgradcol = 0xE5DFDB] [setgradsize = 0.035 ; setgradcol = 0xDAD4D1]
			uimodhgradient $setgradcol 0xFFFFFF 0.71 0.045 [
				uihlist 0 [
					uispace 0.005
					uitarget 0.25 0.03 [uicolortext (at $Account_Menu_WieldedWeapon) 0x222222 0.6 ; uialign* -1]
					UIhslider acc_weapon 0 25 1 0.42 0.025 [uitext $acc_weapon 0.6]
				]
			]
		]
	]
	uialign* -1
	uispace 0 0.01
]

buyItemButton = [
	UIbutton "hold1" [
		uicolortext (at $Account_Menu_Buy) 0xEEEEEE 0.65
		uicolortext "(" 0xEEEEEE 0.65
		uicolortext (getitemprice $arg1 $arg2) 0xFFCC00 0.75
		uiimage "media/interface/hud/cislacoin.jpg" 0.03 0.03
		uicolortext ")" 0xEEEEEE 0.65
		uialign 0 0
	] 0.25 0.04 [buyitem $arg1 $arg2] -1
]

show_smileys = [
	loop i (getnumsmileys) [
		if (! (hasitem 0 $i)) [smi_col = $c_unknown] [
			smi_col = $c_common ; text_col = "^f4"
			if (> $i 1) [smi_col = $c_uncommon ; text_col = "^fh"]
			if (> $i 3) [smi_col = $c_rare ; text_col = "^ff"]
			if (> $i 6) [smi_col = $c_legendary; text_col = "^fi"]
			if (> $i 8) [smi_col = $c_godly ; text_col = "^f6"]
		]
		uihlist 0 [
			uihover [txtsize = 0.7 ; smi_col = (+ $smi_col 0x111111)][txtsize = 0.65]
			uihgradient $smi_col 0xCDCDCD 0.7 0.18 [
				uihlist 0 [
					uispace 0.005
					uivlist 0 [
						uitarget 0.4 0.05 [uicolortext (concat $text_col (at $Smileys_Names $i)) 0x333333 $txtsize] ; uialign* -1
						if (hasitem 0 $i) [
							if (!= $i $playermodel) [uicolortext (at $Account_Menu_OwnSmiley) 0x22AA22 0.6]
							uispace 0 0.005
							if (= $i $playermodel) [uicolortext (at $Account_Menu_CurrentSmiley) 0x22AA22 0.65][
								uivlist 0 [
									UIbutton "hold1" [
										uicolortext (at $Account_Menu_Use) 0xEEEEEE 0.65
										uialign 0 0
									] 0.2 0.04 [playermodel $i] -1
								]
							]
						][
							uicolortext (at $Account_Menu_NotOwnedSmiley) 0x883333 0.6
							uispace 0 0.005
							buyItemButton 0 $i
						]
					]
					uiline 0x555555
					uiclamp- 0 0 1 1
					uitarget 0.28 0.15 [uihover [previewsize = 0.28][previewsize = 0.15] ; uiplayerpreview $i $capeskin 1 1 1 0.28 $previewsize 195]
				]
				uialign* -1
			]		
		]
	]
]

show_capes = [
	loop i (getnumcapes) [
		if (! (hasitem 1 $i)) [cape_col = $c_unknown] [
			cape_col = $c_common ; text_col = "^f4"
			if (> $i 2) [cape_col = $c_uncommon ; text_col = "^fh"]
			if (> $i 5) [cape_col = $c_rare ; text_col = "^ff"]
			if (> $i 9) [cape_col = $c_legendary ; text_col = "^fi"]
			if (> $i 12) [cape_col = $c_godly ; text_col = "^f6"]
		]
		uihlist 0 [
			uihover [txtsize = 0.7 ; cape_col = (+ $cape_col 0x111111)][txtsize = 0.65]
			uihgradient $cape_col 0xCDCDCD 0.7 0.18 [
				uihlist 0 [
					uispace 0.005
					uivlist 0 [
						uitarget 0.4 0.05 [uicolortext (concat $text_col (at $Capes_Names $i)) 0x333333 $txtsize] ; uialign* -1
						if (hasitem 1 $i) [
							if (!= $i $capeskin) [uicolortext (at $Account_Menu_OwnCape) 0x22AA22 0.6]
							uispace 0 0.005
							if (= $i $capeskin) [uicolortext (at $Account_Menu_CurrentCape) 0x22AA22 0.65][
								uivlist 0 [
									UIbutton "hold1" [
										uicolortext (at $Account_Menu_Use) 0xEEEEEE 0.65
										uialign 0 0
									] 0.2 0.04 [capeskin $i] -1
								]
							]
						][
							uicolortext (at $Account_Menu_NotOwnedCape) 0x883333 0.6
							uispace 0 0.005
							buyItemButton 1 $i
						]
					]
					uiline 0x555555
					uiclamp- 0 0 1 1
					uitarget 0.25 0.15 [uihover [previewsize = 0.28][previewsize = 0.15] ; uiplayerpreview $playermodel $i 1 1 1 0.28 $previewsize 15]
				]
				uialign* -1
			]
		]
	]
]

grave_dir = [
    "merde"
    "basique1"
    "basique2"
    "fleur"
    "cristal"
    "minigolf"
    "oeil/ui/nospin"
    "excalibur"
    "couronne"
    "crime"
    "fuck"
    "monument"
    "lingots"
]

show_graves = [
	loop i (getnumgraves) [
		if (! (hasitem 2 $i)) [grave_col = $c_unknown] [
			grave_col = $c_common ; text_col = "^f4"
			if (> $i 2) [grave_col = $c_uncommon ; text_col = "^fh"]
			if (> $i 5) [grave_col = $c_rare ; text_col = "^ff"]
			if (> $i 8) [grave_col = $c_legendary ; text_col = "^fi"]
			if (> $i 11) [grave_col = $c_godly ; text_col = "^f6"]
		]
		uihlist 0 [
			uihover [txtsize = 0.7 ; grave_col = (+ $grave_col 0x111111)][txtsize = 0.65]
			uihgradient $grave_col 0xCDCDCD 0.7 0.18 [
				uihlist 0 [
					uispace 0.005
					uivlist 0 [
						uitarget 0.4 0.05 [uicolortext (concat $text_col (at $Graves_Names $i)) 0x333333 $txtsize] ; uialign* -1
						if (hasitem 2 $i) [
							if (!= $i $graveskin) [uicolortext (at $Account_Menu_OwnGrave) 0x22AA22 0.6]
							uispace 0 0.005
							if (= $i $graveskin) [uicolortext (at $Account_Menu_CurrentGrave) 0x22AA22 0.65][
								uivlist 0 [
									UIbutton "hold1" [
										uicolortext (at $Account_Menu_Use) 0xEEEEEE 0.65
										uialign 0 0
									] 0.2 0.04 [graveskin $i] -1
								]
							]
						][
							uicolortext (at $Account_Menu_NotOwnedGrave) 0x883333 0.6
							uispace 0 0.005
							buyItemButton 2 $i
						]
					]
					uiline 0x555555
					uiclamp- 0 0 1 1
					uitarget 0.25 0.15 [uihover [previewsize = 0.28][previewsize = 0.15] ; uimodelpreview (concatword "graves/" (at $grave_dir $i) "/ui") "mapmodel" 0.28 $previewsize]
				]
				uialign* -1
			]
		]
	]
]