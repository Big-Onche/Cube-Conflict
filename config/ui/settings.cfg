setting_names = ["Jouabilité" "Gameplay" "Graphismes" "Graphics" "Affichage" "Display" "Audio" "Audio" "Contrôles" "Controls" "ATH" "HUD"]
setting_ico = ["media/interface/hud/souris.png" "media/interface/hud/pixel.png" "media/interface/hud/screen.png" "media/interface/hud/hp.png" "media/interface/hud/souris.png" "media/interface/hud/comp.jpg"]

sel_setting = 0

UImenu "settings" [ /////////////////////////////////////////////////////////////////////////////// MENU TABS //////////////////////////////////////////////////////////////////////
	infotxt = ""
	uivgradient 0xEEEEEE 0xBBBBBB 0.9 0.7 [
		uihlist 0 [
			uispace 0.005
			uivlist 0 [
				loop i 6 [
					uihlist 0 [
						uihover [
							setgradx = 0.24 ; setgrady = 0.11 ; imgsize = 0.09 ; txtsize = 0.65 ; tabcola = 0xCCCCAA ; tabcolb = 0xAAAA88][
							setgradx = 0.23 ; setgrady = 0.10 ; imgsize = 0.08 ; txtsize = 0.6 ; tabcola = 0x9F9F9F ; tabcolb = 0x7F7F7F
						]
						uitarget 0.24 0.11 [
							if (= $i $sel_setting)[tabcola = 0xAACCAA ; tabcolb = 0x88AA88]
							uivgradient $tabcola $tabcolb $setgradx $setgrady [
								uihlist 0 [
									uispace 0.003
									uiimage (at $setting_ico $i) $imgsize $imgsize
									uispace 0.003
									uicolortext (at $setting_names (+ (* $i 2) @GAME_LANG)) 0xFFFFFF $txtsize
								]
								uialign* -1
							]
							uirelease [sel_setting = $i]
						]
					]
				]
			]
			
			uispace 0.005
		
			UIbar 0 1
			
			uitarget 0.65 0.7 [
				uivlist 0 [
					uivgradient 0x96B9D0 0x76A0CA 0.65 0.1 [
						uispace 0.3 0.03
						uihlist 0 [
							uispace 0.005
							uiimage (at $setting_ico $sel_setting) 0.07 0.07
							uispace 0.01
							uicolortext (at $setting_names (+ (* $sel_setting 2) @GAME_LANG)) 0x252525 0.8
						]
						uialign* -1
						uiclamp 1 1 1 1
					]
					
					uihlist 0 [
						uivlist 0 [
							case $sel_setting 0 [show_gameplay]
							case $sel_setting 1 [show_gfx]
							case $sel_setting 2 [show_display]
							case $sel_setting 3 [show_audio]
							case $sel_setting 4 [show_commands]
							case $sel_setting 5 [show_console]
						]
						uialign* -1 -1
					]
					uialign* -1 -1
					if $infotxt [showui "infobox"][hideui "infobox"]
				]
				uialign* -1 -1
			]
		]
	]
] [hideui "main" ; UI_resolution  = (concat $screenw $screenh) ; UI_findratio] [showui "main" ; if (pendingchanges) [showui "changes"]] [] 0 "SETTINGS" "OPTIONS" "media/interface/hud/options.jpg"

show_gameplay = [ /////////////////////////////////////////////////////////////////////////////// MAIN GAME SETTINGS //////////////////////////////////////////////////////////////////////
	uispace 0 0.0035
	uitarget 0.8 0 [
		uihover [setgradsize = 0.038 ; setgradcol = 0xE5DFDB] [setgradsize = 0.035 ; setgradcol = 0xDAD4D1]
		uimodhgradient $setgradcol 0xFFFFFF 0.8 0 [
			uihlist 0 [
				uihlist 0 [
					uispace 0.005
					redcol1 = 0xFF0000 ; redcol2 = 0xAA0000 ; greencol1 = 0x00FF00 ; greencol2 = 0x00AA00
					uitarget 0.62 0 [
						if @GAME_LANG [uitext "^fbLanguage:" 0.6 ; uialign* -1; frflagcol1 = $redcol1 ; frflagcol2 = $redcol2 ; enflagcol1 = $greencol1 ; enflagcol2 = $greencol2][
							uitext "^fbLangue :" 0.6 ; uialign* -1 ; frflagcol1 = $greencol1 ; frflagcol2 = $greencol2 ; enflagcol1 = $redcol1 ; enflagcol2 = $redcol2
						]
					]
				]
				uispace 0.005
				uihlist 0 [
					uivgradient $frflagcol1 $frflagcol2 0.07 0.05 [uiimage "media/interface/hud/flag_fr.png" 0.06 0.042 ; uioutline 0x000000 ; uiclamp- 1 1 1 1 ; uirelease [if @GAME_LANG [GAME_LANG = 0]]]
					uispace 0.005
					uivgradient $enflagcol1 $enflagcol2 0.07 0.05 [uiimage "media/interface/hud/flag_en.png" 0.06 0.042 ; uioutline 0x000000 ; uiclamp- 1 1 1 1 ; uirelease [if (! @GAME_LANG) [GAME_LANG = 1]]]
				]
			]
			uialign* -1
		]
	]
	uialign* -1
	
	uispace 0 0.0015
	
	UIsettingcheckbox ["Third person"] ["Caméra détachée"] [thirdperson (! $thirdperson)] [$thirdperson]
	
	UIsettingcheckbox ["Invert mouse"] ["Inverser la souris"] [invmouse (! $invmouse)] [$invmouse]
	
	UIsettingcheckbox ["Show blood"] ["Afficher le sang"] [blood (! $blood)] [$blood]
	
	UIsettingcheckbox ["Limit light flashes"] ["Limiter les flashs lumineux"] [epilepsyfriendly (! $epilepsyfriendly)] [$epilepsyfriendly] ["Reduces the intensity of light flashes. Useful if you have a headache or epilepsy."]["Réduit l'intensité des flashs lumineux, pratique si tu as un mal de crâne ou de l'épilepsie."]
	
	UIsettingcheckbox ["Show minimap"] ["Afficher la mini-carte"] [showminimap (! $showminimap)] [$showminimap]

	UIsettingcheckbox ["Show FPS"] ["Afficher le taux de rafraîchissement"] [showfps (! $showfps)] [$showfps]
	
	UIsettingcheckbox ["Show ping"] ["Afficher le ping"] [showmyping (! $showmyping)] [$showmyping]
	
	uivlist 0 [
		UIsettingslider ["Field of view"] ["Champ de vision"] [fov] [$fov] [10] [150] [1]
		
		UIsettingslider ["Mouse sensitivity"] ["Sensibilité de la souris"] [sensitivity] [$sensitivity] [0.1] [40.0] [0.1]
	]
	uialign* -1
]

/////////////////////////////////////////////////////////////////////////////// GFX SETTINGS //////////////////////////////////////////////////////////////////////
gfxsettings = ["^f0Basse" "^ffLow" "^fdMoyenne" "^f0Medium" "^f2Élevée" "^f2High" "^f3Ultra" "^f3Ultra"]

show_gfx = [
	uispace 0 0.002

	uitarget 0.8 0 [] (at $grave_names (+ (* $i 2) @GAME_LANG))
	
	UIsettingslider ["Shadows quality"] ["Qualité des ombres"] [shadowslod] [(at $gfxsettings (+ (* $shadowslod 2) @GAME_LANG))] [0] [3] [1] [setshadowslod] [1] ["Accuracy, render distance, and anti-aliasing of shadows."]["Précision, distance d'affichage et anti-crélénage des ombres."]

	UIsettingslider ["Lighting quality"] ["Qualité de l'éclairage"] [lightlod] [(at $gfxsettings (+ (* $lightlod 2) @GAME_LANG))] [0] [3] [1] [setlightlod] [1] ["Volumetric lighting, global illumination, and ambient occlusion quality."]["Eclairage volumétrique, illumination globale et qualité d'occlusion ambiante."]
	
	UIsettingslider ["Water quality"] ["Qualité de l'eau"] [waterlod] [(at $gfxsettings (+ (* $waterlod 2) @GAME_LANG))] [0] [3] [1] [setwaterlod] [1] ["Water geometry, animations, and visual effects."]["Géométrie de l'eau, animations et effets visuels."]

	UIsettingslider ["Reflexions quality"] ["Qualité des réflexions"] [reflectionslod] [(at $gfxsettings (+ (* $reflectionslod 2) @GAME_LANG))] [0] [3] [1] [setreflectionslod] [1] ["Quality of reflections of stuff that reflects things."]["Qualité des réflexions des trucs qui reflètent des choses."]

	UIsettingslider ["Particles quality"] ["Qualité des particules"] [particleslod] [(at $gfxsettings (+ (* $particleslod 2) @GAME_LANG))] [0] [3] [1] [setparticleslod] [1] ["Maximum amount of particles/decals, and rendering distance for them."] ["Quantité maximale de particules/décalcomanies et distance de rendu pour celles-ci."]
	
	UIsettingslider ["3D models rendering"] ["Qualité d'affichage 3D"] [trislod] [(at $gfxsettings (+ (* $trislod 2) @GAME_LANG))] [0] [3] [1] [settrislod] [1] ["3D models rendering distance and grass overall quality."]["Distance d'affichage des modèles 3D et qualité d'afficage de l'herbe."]
	
	UIsettingcheckbox ["Ambient occlusion"] ["Occlusion ambiante"] [ao (! $ao)] [$ao]
]

/////////////////////////////////////////////////////////////////////////////// DISPLAY SETTINGS //////////////////////////////////////////////////////////////////////
UI_ratio4:3 = [
    "640 480"
    "800 600"
    "1024 768"
    "1152 864"
    "1280 960"
    "1400 1050"
    "1600 1200"
    "1920 1440"
    "2560 1920"
]
UI_ratio5:4 = [
    "800 640"
    "960 768"
    "1280 1024"
    "1440 1152"
    "1600 1280"
    "1920 1536"
    "2560 2048"
]
UI_ratio16:9 = [
    "800 450"
    "1024 600"
    "1280 720"
    "1366 768"
    "1600 900"
    "1920 1080"
    "2048 1152"
    "2560 1440"
]
UI_ratio16:10 = [
    "768 480"
    "1024 640"
    "1280 800"
    "1440 900"
    "1680 1050"
    "1920 1200"
    "2560 1600"
]

opt_ratios = [4:3 5:4 16:9 16:10]
opt_ratios_assoc = ((looplistconcat ratio $opt_ratios [concat $ratio $ratio]))
UI_findratio = [
   looplist ratio $opt_ratios [
      looplist res $[UI_ratio@ratio] [
         if (=s $res $UI_resolution) [UI_aspectratio = $ratio]
      ]
   ]
]

show_display = [
	uispace 0 0.0035
	uitarget 0.8 0 [
		uihover [setgradsize = 0.038 ; setgradcol = 0xE5DFDB] [setgradsize = 0.035 ; setgradcol = 0xDAD4D1]
		uimodhgradient $setgradcol 0xFFFFFF 0.8 0 [
			uihlist 0 [
				uispace 0.005
				uitarget 0.53 0 [
					uivlist 0 [
						uispace 0.005
						if @GAME_LANG [uitext "^fkAspect ratio:" 0.6 ; uialign* -1][uitext "^fkRatio d'affichage :" 0.6 ; uialign* -1]
						uispace 0 0.005
						if @GAME_LANG [uitext "^fkScreen resolution:" 0.6][uitext "^fkRésolution d'affichage :" 0.6]
					]
					uialign* -1
				]
				uispace 0.01
				uivlist 0 [
					uihlist 0 [
						UIlistslider UI_aspectratio =s $opt_ratios_assoc 0.2 0.025 [
							if (=s $UI_aspectratio "custom") [] [
								UI_resolution = (at $[UI_ratio@UI_aspectratio] 1)
									screenw (at $UI_resolution 0)
									screenh (at $UI_resolution 1)
							]
						]
					]
					uispace 0 0.005
					UIlistslider UI_resolution = (
						looplistconcat i $[UI_ratio@UI_aspectratio] [
							result (concat (escape $i) (strreplace $i " " "x"))
						]
					) 0.2 0.025 [
						screenw (at $UI_resolution 0)
						screenh (at $UI_resolution 1)
					]
				]
			]
			uialign* -1
		]
	]
	uialign* -1
	
	uispace 0 0.0015
	
	UIsettingcheckbox ["Exclusive fullscreen"] ["Plein écran exclusif"] [fullscreen (! $fullscreen)] [$fullscreen] ["Setting to play in fullscreen, fake fullscreen mode, and windowed mode."] ["Permet de jouer en plein écran, faux plein écran ou en mode fenêtré."]
	
	UIsettingslider ["Render scale"] ["Échelle de rendu"] [gscale] [$gscale] [25] [100] [1]
	
	UIsettingcheckbox ["Vertical synchronization"] ["Synchronisation verticale"] [vsync (! $vsync)] [$vsync] ["Enable vertical synchronization if you have screen tearing."]["Activez cette option si l'affichage présente des artéfacts."]
	
	UIsettingslider ["Gamma"] ["Luminosité"] [gamma] [$gamma] [20] [200] [1]
	
	tmpvals = ["Désactivé" "Off" "Bas" "Low" "Moyen" "Medium" "Elevé" "High"]
	UIsettingslider ["Anisotropic filtering"] ["Filtrage anisotrope"] [aniso] [(if (= $aniso 0) [at $tmpvals (+ @GAME_LANG)] $aniso)] [0] [16] [4] [] [1]
	
	UIsettingslider ["Anti-aliasing (SMAA)"] ["Anti-crélénage (SMAA)"] [smaaquality] [(at $tmpvals (+ (* $smaaquality 2) @GAME_LANG))] [0] [3] [1] [if (= @smaaquality 0) [smaa = 0][smaa = 1]] [1] ["Smoothing of jagged edges. For best rendering, it is recommended to use only one anti-aliasing method.^nSMAA is one of the cleanest anti-aliasing, but have a higher impact on performance than FXAA."]["Améliore les rebords en les lissant. Pour meilleur rendu, mieux vaut n'utiliser qu'une seule méthode d'anti-crélénage.^nLe SMAA est l'un des anti-aliasing les plus propres, mais impacte un peu plus les performances que le FXAA."]
	
	UIsettingslider ["Anti-aliasing (FXAA)"] ["Anti-crélénage (FXAA)"] [fxaaquality] [(at $tmpvals (+ (* $fxaaquality 2) @GAME_LANG))] [0] [3] [1] [if (= @fxaaquality 0) [fxaa = 0][fxaa = 1]] [1] ["Smoothing of jagged edges. For best rendering, it is recommended to use only one anti-aliasing method.^nFXAA is a nice anti-aliasing while having little impact on performance."]["Améliore les rebords en les lissant. Pour meilleur rendu, mieux vaut n'utiliser qu'une seule méthode d'anti-crélénage.^nLe FXAA est un anti-crélénage efficace tout en ayant peu d'impact sur les performances."]
	
	UIsettingcheckbox ["Temporal anti-aliasing"] ["Anti-crélénage (TXAA)"] [tqaa (! $tqaa)] [$tqaa] ["Smooths jagged edges by blurring them. (You can also remove your glasses to get the same effect.)"]["Lisse les bords irréguliers en les floutant. C'est un peu comme si tu retirais tes lunettes."]
]

/////////////////////////////////////////////////////////////////////////////// AUDIO SETTINGS //////////////////////////////////////////////////////////////////////
speaker_ico = ["media/interface/hud/speaker_off.png" "media/interface/hud/speaker_on.png"]

show_audio = [
	uispace 0 0.0035
	uitarget 0.8 0.05 [
		uihover [setgradsize = 0.038 ; setgradcol = 0xE5DFDB] [setgradsize = 0.035 ; setgradcol = 0xDAD4D1]
		uimodhgradient $setgradcol 0xFFFFFF 0.8 0.06 [
			uihlist 0 [
				uihlist 0 [
					uispace 0.005
					if @GAME_LANG [uitext "^fbSounds:" 0.6][uitext "^fbSons :" 0.6]
				]
				uispace 0.005
				uihlist 0 [
					if @sound [col1 = 0x00FF00 ; col2 = 0x00AA00][col1 = 0xFF0000 ; col2 = 0xAA0000]
					uivgradient $col1 $col2 0.07 0.05 [uiimage (at $speaker_ico @sound) 0.06 0.042 ; uioutline 0x000000 ; uiclamp- 1 1 1 1 ; uirelease [sound (! $sound)]]
				]
			]
			uialign* -1
		]
	]
	uialign* -1
	
	uispace 0 0.0015
	
	tmpset = (* (*f $soundvol 0.393) 1)
	UIsettingslider ["Sound volume"] ["Volume des sons"] [soundvol] [$tmpset] [0] [255] [1]
	
	tmpset = (* (*f $musicvol 0.393) 1)
	UIsettingslider ["Music volume"] ["Volume des musiques"] [musicvol] [$tmpset] [0] [255] [1]
	
	tmpvals = ["512" "1024" "2048" "4096"]
	UIsettingslider ["Sound buffer"] ["Mémoire tampon"] [soundbufferlen_ui] [$soundbufferlen] [0] [3] [1] [soundbufferlen = (at $tmpvals $soundbufferlen_ui)] [] ["Increase the buffer if you hear stuttering sounds like in Fallout 3."]["Augmenter la mise en mémoire tampon est utile si le son est saccadé (comme dans Fallout 3)."]
	
	UIsettingslider ["Max sounds at once"] ["Limite de sons"] [maxsoundsatonce] [$maxsoundsatonce] [32] [512] [1] [soundchans = @maxsoundsatonce] [1] ["Max amount of sounds that can be played simultaneously. A high amount can burn your CPU."]["Quantité de sons pouvant êtres lus simultanémant, impact important sur l'utilisation du processeur."]
	
	tmpvals = ["11025" "22050" "44100" "48000"]
	UIsettingslider ["Sampling frequency"] ["Taux d'échantillonnage"] [soundfreq_ui] [$soundfreq] [0] [3] [1] [soundfreq = (at $tmpvals $soundfreq_ui)] [1]
	
	UIsettingcheckbox ["Play sounds when the game is minimized"] ["Jouer les sons lorsque le jeu est minimisé"] [minimizedsounds (! $minimizedsounds)] [$minimizedsounds]
]

/////////////////////////////////////////////////////////////////////////////// KEYS SETTINGS //////////////////////////////////////////////////////////////////////
cmd_names = [
	"Avancer" 	  				"Forward"
	"Reculer" 	  				"Backward" 
	"Gauche"  	  				"Left"
	"Droite"  	  				"Right"
	"Sauter"	  				"Jump"
	"S'accroupir" 				"Crouch"
	"Tirer"   	  				"Shoot"
	"Viser"  	  				"Aim"
	"Attaque au corps à corps"  "Melee attack"
	"Lancer une grenade"  	  	"Throw grenade"
	"Capacité 1"  				"Ability 1"
	"Capacité 2"  				"Ability 2"
	"Capacité 3"  				"Ability 3"
	"Equiper l'arme ramassée"  	"Equip picked up weapon"
	"Interagir"     			"Interact"
	"Abandonner le drapeau"  	"Drop flag"
	"Tchat publique"			"Public chat"
	"Tchat d'équipe" 			"Team chat"
]

cmd_action = [forward backward left right jump crouch shoot dozoom meleeattack launchgrenade "aptitude 0" "aptitude 1" "aptitude 2" doautowield interact dropflag saycommand sayteamcommand]

show_commands = [
	bindidx = 0
	uihlist 0 [
		uiscroll 0.85 0.6 [
			uivlist 0.0010 [
				uispace 
				looplist i $cmd_action [
					uispace 0 0.0005
					uitarget 0.775 0.02 [
						uihover [setgradcol = 0xE5DFDB] [setgradcol = 0xDAD4D1]
						uimodhgradient $setgradcol 0xFFFFFF 0.7 0.035 [
							uihlist 0 [
								uispace 0.005
								uitarget 0.36 0.02 [uicolortext (at $cmd_names (+ (* $bindidx 2) @GAME_LANG)) 0x000000 0.6 ; uialign* -1]
								[newbinds@bindidx] = (searchbinds $i)
								uihgradient 0x777777 0x888888 $arg2 $arg3 [
									uikeyfield [newbinds@bindidx] 30 [ 
										oldbinds = (searchbinds [@@i])
										looplist j $oldbinds [bind $j ""]
										looplist j $[newbinds@@bindidx] [bind $j [@@@i]]
									] 0.6 [uioutline (uifocus? $c_focus 0x444444); uiclamp- 1 1 1 1]
									bindidx = (+ $bindidx 1)
								]
							]
							uialign* -1
						]
						uialign* -1
					]
					uispace 0 0.00015	
				]
				
				uispace 0 0.01

				uitarget 0.775 0.05 [
					uihover [setgradcol = 0xE5DFDB] [setgradcol = 0xDAD4D1]
					uimodhgradient $setgradcol 0xFFFFFF 0.7 0.05 [
						uihlist 0 [
							uispace 0.005
							uitarget 0.36 0.02 [if @GAME_LANG [uitext "^fbDefault settings" 0.6][uitext "^fbConfiguration par défaut" 0.6] ; uialign* -1]
							uihlist 0 [
								UIbutton "hold5" [
									if @GAME_LANG [uitext "^fbReset" 0.6][uitext "^fbRéinitialiser" 0.6]
								] 0.25 0.03 [clearallbinds ; exec "config/default_binds.cfg"]
							]
						]
						uialign* -1
					]
					uialign* -1
				]
				uialign* -1
			]
		]
		UIvscroll 0.025 0.59 2.5
	]
	uialign* -1
]

show_console = [/////////////////////////////////////////////////////////////////////////////// HUD SETTINGS //////////////////////////////////////////////////////////////////////
	uispace 0 0.0035

	uitarget 0.8 0 []

	UIsettingslider ["Head up display size"] ["Taille de l'ATH"] [hudscale] [$hudscale] [0] [100] [1]
	
	UIsettingslider ["Crosshair size"] ["Taille du viseur"] [crosshairsize] [$crosshairsize] [20] [50] [1]
	
	UIsettingcheckbox ["Show XP bar"] ["Afficher barre d'XP"] [hudxpbar (! $hudxpbar)] [$hudxpbar]

	UIsettingcheckbox ["Public chat"] ["Tchat publique"] [conchat (! $conchat) ; if @conchat [confilter = (+ @confilter 0x100)][confilter = (- @confilter 0x100)]] [$conchat]
	
	UIsettingcheckbox ["Team chat"] ["Tchat d'équipe"] [conteamtchat (! $conteamtchat) ; if @conteamtchat [confilter = (+ @confilter 0x200)][confilter = (- @confilter 0x200)]] [$conteamtchat]

	UIsettingcheckbox ["Game events"] ["Événements de jeu"] [congame (! $congame) ; if @congame [confilter = (+ @confilter 0x400)][confilter = (- @confilter 0x400)]] [$congame]
			
	UIsettingcheckbox ["My frags"] ["Mes éliminations"] [conkills (! $conkills) ; if @conkills [confilter = (+ @confilter 0x800)][confilter = (- @confilter 0x800)]] [$conkills]
	
	UIsettingcheckbox ["Other frags"] ["Autres éliminations"] [conkills (! $conkills) ; if @conkills [confilter = (+ @confilter 0x1000)][confilter = (- @confilter 0x1000)]] [$conkills]

	//UIsettingcheckbox ["[Engine] Important informations"] ["[Moteur] Information importantes"] [coninfos (! $coninfos) ; if @coninfos [confilter = (+ @confilter 0x01)][confilter = (- @confilter 0x01)]] [$coninfos] ["Important information from the program."] ["Informations importantes liées au fonctionnement du programme."]
	
	UIsettingcheckbox ["[Engine] Warnings"] ["[Moteur] Messages d'avertissement"] [conwarnings (! $conwarnings) ; if @conwarnings [confilter = (+ @confilter 0x02)][confilter = (- @confilter 0x02)]] [$conwarnings] ["Warnings messages from the game."] ["Messages d'avertissement liés au fonctionnement du programme."]
	
	UIsettingcheckbox ["[Engine] Errors"] ["[Moteur] Messages d'erreurs"] [conerrors (! $conerrors) ; if @conerrors [confilter = (+ @confilter 0x04)][confilter = (- @confilter 0x04)]] [$conerrors] ["Error messages from the game."] ["Messages d'erreurs liés au fonctionnement du programme."]
	
	//UIsettingcheckbox ["[Engine] Init messages"] ["[Moteur] Messages d'initialisation"] [coninit (! $coninit) ; if @coninit [confilter = (+ @confilter 0x10)][confilter = (- @confilter 0x10)]] [$coninit] ["Game initialization messages."] ["Messages d'initialisation du programme."]
	
	//UIsettingcheckbox ["[Engine] Script messages"] ["[Moteur] Messages de scripts"] [conscripts (! $conscripts) ; if @conscripts [confilter = (+ @confilter 0x20)][confilter = (- @confilter 0x20)]] [$conscripts] ["Messages related to the use of scripts."] ["Messages liés à l'utilisation de scripts annexes."]
]


UImenu "changes" [ /////////////////////////////////////////////////////////////////////////////// PENDING CHANGES //////////////////////////////////////////////////////////////////////
	uivgradient 0xCCCCCC 0x999999 0.1 0.02 [
	   uivlist 0.02 [
			uiclamp 1 1
			if (> $pendingchanges 1) [entxt = "Apply changes:" ; frtxt = "Appliquer les changements:"][entxt = "Apply change:" ; frtxt = "Appliquer le changement :"]
			UItitle [if @GAME_LANG [uitext $entxt 0.6][uitext $frtxt 0.6]] 0 0.03
			uiclamp- 1 1
			uivlist 0.01 [
				loop i $pendingchanges [
					uitext (concatword "^fb" (pendingchanges $i)) 0.6
				]
			]
			if @GAME_LANG [yes = "Yes" ; no = "No"][yes = "Oui" ; no = "Non"]
			uihlist 0 [
				UIbutton "hold5" [uiimage "media/interface/hud/checkbox_on.jpg" 0.03 0.03 ; uitext $yes 0.6] 0.25 0.035 [applychanges]
				UIbutton "hold5" [uiimage "media/interface/hud/checkbox_off.jpg" 0.03 0.03 ; uitext $no 0.6] 0.25 0.035 [hideui "changes" ; clearchanges]
			]
	    ]
	]
] [if (= $pendingchanges 0) [hideui "changes"]] [] [] 0

/////////////////////////////////////////////////////////////////////////////// LODS //////////////////////////////////////////////////////////////////////
setshadowslod = [
	case $shadowslod 0 [
        csmfarplane = 512
        smfilter = 1
		smcache = 2
        smdynshadow = 0
        smnoshadow = 1
        smsize = 9
	]
	case $shadowslod 1 [
        csmfarplane = 768
        smfilter = 2
		smcache = 2
        smdynshadow = 0
        smnoshadow = 0
        smsize = 10
	]
	case $shadowslod 2 [
        csmfarplane = 1024
        smfilter = 3
		smcache = 1
        smdynshadow = 1
        smnoshadow = 0
        smsize = 11
	]
	case $shadowslod 3 [	
        csmfarplane = 1280
        smfilter = 3
		smcache = 1
        smdynshadow = 1
        smnoshadow = 0
        smsize = 13
	]
]

setlightlod = [
	case $lightlod 0 [
		dynlightdist = 256
        rhfarplane = 512
        volumetric = 0
        aocutoff = 4
	]
	case $lightlod 1 [
		dynlightdist = 512
        rhfarplane = 512
        volumetric = 1
        volsteps = 10;
        aocutoff = 8
	]
	case $lightlod 2 [
		dynlightdist = 1536
        rhfarplane = 1024
        volumetric = 1
        volsteps = 20
        aocutoff = 16
	]
	case $lightlod 3 [
		dynlightdist = 3072
        rhfarplane = 2048
        volumetric = 1
        volsteps = 40
        aocutoff = 32
	]
]

setwaterlod = [
	case $waterlod 0 [
        vertwater = 0
        caustics = 0
	]
	case $waterlod 1 [
        vertwater = 0
        caustics = 1
	]
	case $waterlod 2 [
        vertwater = 1
        caustics = 1
        waterlod = 2
        watersubdiv = 2
	]
	case $waterlod 3 [	
        vertwater = 1
        caustics = 1
        waterlod = 3
        watersubdiv = 3
	]
]

setparticleslod = [
	case $particleslod 0 [
        maxparticles = 512
        softparticles = 0
		maxparticledistance = 400
		maxstaintris = 512
        stainfade = 5
	]
	case $particleslod 1 [
        maxparticles = 2048
        softparticles = 1
		maxparticledistance = 800
        maxstaintris = 2048
        stainfade = 15
	]
	case $particleslod 2 [
        maxparticles = 8192
        softparticles = 1
		maxparticledistance = 1600
        maxstaintris = 8192
        stainfade = 60
	]
	case $particleslod 3 [	
        maxparticles = 16384
        softparticles = 1
		maxparticledistance = 2400
        maxstaintris = 20480
        stainfade = 300
	]
]

settrislod = [
	case $trislod 0 [
        grass 0
		temptrisfade = 3000
		ragdollmillis = 3000
		maxmodelradiusdistance = 35
		loddistfactor = 3
		bnclifetime = 2500
	]
	case $trislod 1 [
		grass 1
        grassdist = 512
        grassdist = 150
        grassstep = 3.0
		temptrisfade = 6000
		ragdollmillis = 6000
		maxmodelradiusdistance = 70
		loddistfactor = 5
		bnclifetime = 5000
	]
	case $trislod 2 [
		grass 1
	    grassdist = 578
        grassstep = 2.5
		temptrisfade = 8000
		ragdollmillis = 8000
		maxmodelradiusdistance = 100
		loddistfactor = 8
	]
	case $trislod 3 [
		grass 1
		grassdist = 1024
        grassstep = 2.0
		temptrisfade = 15000
		ragdollmillis = 10000
		maxmodelradiusdistance = 150
		loddistfactor = 12
	]
]

setreflectionslod = [
	case $reflectionslod 0 [
        envmapsize = 4
		waterreflect = 0
	]
	case $reflectionslod 1 [
        envmapsize = 6
		waterreflect = 1
		waterreflectstep = 2
	]
	case $reflectionslod 2 [
        envmapsize = 8
		waterreflect = 1
		waterreflectstep = 4
	]
	case $reflectionslod 3 [	
        envmapsize = 10
		waterreflect = 1
		waterreflectstep = 32
	]
]