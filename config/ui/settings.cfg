setting_ico = ["media/interface/hud/souris.png" "media/interface/hud/pixel.png" "media/interface/hud/screen.png" "media/interface/hud/hp.png" "media/interface/hud/souris.png" "media/interface/hud/comp.jpg"]
sel_setting_tab = 0

UI_window "settings" "media/interface/hud/options.jpg" (getText $Settings_Menu_Title) [
	uivgradient 0xEEEEEE 0xBBBBBB 0.9 0.7 [
		uihlist 0 [
			uispace 0.005
			uivlist 0 [
				loop i 6 [
					uihlist 0 [
						uihover [
							w = (*f $tab_w 1.03) ; h = (*f $tab_h 1.05) ; ico_s = (*f $tab_ico_s 1.05) ; text_s = (*f $tab_txt_s 1.05) ; col_up = $c_tab_hv_up ; col_dn = $c_tab_hv_dn
						][
							w = $tab_w ; h = $tab_h ; ico_s = $tab_ico_s ; text_s = $tab_txt_s ; col_up = $c_tab_up ; col_dn = $c_tab_dn
						]
						uitarget 0.29 0.11 [
							if (= $i $sel_setting_tab) [col_up = 0xAACCAA ; col_dn = 0x88AA88]
							uivgradient $col_up $col_dn $w $h [
								UI_outline
								uihlist 0 [
									uispace 0.003
									uiimage (at $setting_ico $i) $ico_s $ico_s
									uispace 0.003
									uicolortext (getText $Settings_Menu_Tabs $i) 0xFFFFFF $text_s
								]
								uialign- -1
							]
							uirelease [sel_setting_tab = $i]
						]
					]
				]
			]
			
			uispace 0.005
		
			UIbar 0 1
			
			uitarget 0.65 0.7 [
				uivlist 0 [
					uivgradient 0x96B9D0 0x76A0CA 0.65 0.1 [
						uispace 0.3 0.03
						uihlist 0 [
							uispace 0.005
							uiimage (at $setting_ico $sel_setting_tab) 0.07 0.07
							uispace 0.01
							uicolortext (getText $Settings_Menu_Tabs $sel_setting_tab) 0x252525 0.8
						]
						uialign* -1
						uiclamp 1 1 1 1
					]
					
					uihlist 0 [
						uivlist 0 [
							case $sel_setting_tab 0 [show_gameplay]
							case $sel_setting_tab 1 [show_gfx]
							case $sel_setting_tab 2 [show_display]
							case $sel_setting_tab 3 [show_audio]
							case $sel_setting_tab 4 [show_commands]
							case $sel_setting_tab 5 [show_console]
						]
						uialign* -1 -1
					]
					uialign* -1 -1
				]
				uialign* -1 -1
			]
		]
	]
] [hideui "main" ; UI_resolution  = (concat $screenw $screenh) ; UI_findratio] [showui "main" ; if (pendingchanges) [showui "changes"]]

show_gameplay = [ /////////////////////////////////////////////////////////////////////////////// MAIN GAME SETTINGS //////////////////////////////////////////////////////////////////////
	uihlist 0 [
		uiscroll 0.85 0.6 [
			uivlist 0 [
				uispace 0 0.0035
				uitarget 0.8 0 [
					uihover [setgradsize = 0.038 ; setgradcol = 0xE5DFDB] [setgradsize = 0.035 ; setgradcol = 0xDAD4D1]
					uimodhgradient $setgradcol 0xFFFFFF 0.8 0 [
						uihlist 0 [
							uihlist 0 [
								uispace 0.005
								uitarget 0.54 0 [
									uicolortext (getText $Settings_Menu_Language) 0x222222 0.6 ; uialign* -1
								]
							]
							uispace 0.005
							uihlist 0 [
								loop i (listlen $languages) [
									if (= $language $i) [col_up = 0x00FF00 ; col_down = 0x00AA00] [col_up = 0xAA0000 ; col_down = 0xFF0000]
									uivgradient $col_up $col_down 0.07 0.05 [uiimage (concatword "media/interface/flags/" (at $languages $i) ".png") 0.06 0.042 ; uioutline 0x000000 ; uiclamp- 1 1 1 1 ; uirelease [language = $i ; selectlanguage]]
									uispace 0.005
								]
							]
						]
						uialign* -1
					]
				]
				uialign* -1
				
				uispace 0 0.0015
				
				UI_settingcheckbox $Settings_Menu_ThirdPerson [thirdperson (! $thirdperson)] [$thirdperson]
				
				UI_settingcheckbox $Settings_Menu_InvertMouse [invmouse (! $invmouse)] [$invmouse]
				
				UI_settingcheckbox $Settings_Menu_ShowBlood [blood (! $blood)] [$blood]

				UI_settingcheckbox $Settings_Menu_LimitFlashes [epilepsyfriendly (! $epilepsyfriendly)] [$epilepsyfriendly] $Settings_Menu_LimitFlashesDesc
				
				UI_settingslider $Settings_Menu_ColorBlindness [cbcompensation] [(getText $Settings_Menu_ColorBlindnessList $cbcompensation)] [0] [4] [1]
				
				uivlist 0 [
					UI_settingslider $Settings_Menu_FieldOfView [fov] [$fov] [10] [150] [1]
					
					UI_settingslider $Settings_Menu_MouseSensitivity [sensitivity] [$sensitivity] [0.1] [40.0] [0.1]
				]
				uialign* -1
				
				UI_settingcheckbox $Settings_Menu_GameIntro [gameintro (! $gameintro)] [$gameintro]
			]
		]
		UIvscroll 0.025 0.59 2.5
	]
]

/////////////////////////////////////////////////////////////////////////////// GFX SETTINGS //////////////////////////////////////////////////////////////////////
show_gfx = [
	uispace 0 0.002

	uitarget 0.825 0
	
	UI_settingslider $Settings_Menu_ShadowsQuality [shadowslod] [(getText $Settings_Menu_GfxSettings $shadowslod)] [0] [3] [1] [setshadowslod] $Settings_Menu_ShadowsQualityDesc $TRUE

	UI_settingslider $Settings_Menu_LightingQuality [lightlod] [(getText $Settings_Menu_GfxSettings $lightlod)] [0] [3] [1] [setlightlod] $Settings_Menu_LightingQualDesc $TRUE
	
	UI_settingslider $Settings_Menu_WaterQuality [waterlod] [(getText $Settings_Menu_GfxSettings $waterlod)] [0] [3] [1] [setwaterlod] $Settings_Menu_WaterQualDesc $TRUE

	UI_settingslider $Settings_Menu_ReflexionsQuality [reflectionslod] [(getText $Settings_Menu_GfxSettings $reflectionslod)] [0] [3] [1] [setreflectionslod] $Settings_Menu_ReflexionsQualDesc $TRUE

	UI_settingslider $Settings_Menu_ParticlesQuality [particleslod] [(getText $Settings_Menu_GfxSettings $particleslod)] [0] [3] [1] [setparticleslod] $Settings_Menu_ParticlesQualDesc $TRUE
	
	UI_settingslider $Settings_Menu_ModelsQuality [trislod] [(getText $Settings_Menu_GfxSettings $trislod)] [0] [3] [1] [settrislod] $Settings_Menu_ModelsQualDesc $TRUE
	
	UI_settingcheckbox $Settings_Menu_AmbientOcclusion [ao (! $ao)] [$ao] $Settings_Menu_AmbientOcclDesc
	
]

/////////////////////////////////////////////////////////////////////////////// DISPLAY SETTINGS //////////////////////////////////////////////////////////////////////
UI_ratio4:3 = [
    "640 480"
    "800 600"
    "1024 768"
    "1152 864"
    "1280 960"
    "1400 1050"
    "1600 1200"
    "1920 1440"
    "2560 1920"
]
UI_ratio5:4 = [
    "800 640"
    "960 768"
    "1280 1024"
    "1440 1152"
    "1600 1280"
    "1920 1536"
    "2560 2048"
]
UI_ratio16:9 = [
    "800 600"
    "1024 600"
    "1280 720"
    "1366 768"
    "1600 900"
    "1920 1080"
    "2048 1152"
	"2560 1440"
	"3840 2160"
]
UI_ratio16:10 = [
    "768 480"
    "1024 640"
    "1280 800"
    "1440 900"
    "1680 1050"
    "1920 1200"
    "2560 1600"
]

opt_ratios = [4:3 5:4 16:9 16:10]
opt_ratios_assoc = ((looplistconcat ratio $opt_ratios [concat $ratio $ratio]))
UI_findratio = [
   looplist ratio $opt_ratios [
      looplist res $[UI_ratio@ratio] [
         if (=s $res $UI_resolution) [UI_aspectratio = $ratio]
      ]
   ]
]

show_display = [
	uispace 0 0.0035
	uitarget 0.825 0 [
		uihover [setgradsize = 0.038 ; setgradcol = 0xE5DFDB] [setgradsize = 0.035 ; setgradcol = 0xDAD4D1]
		uimodhgradient $setgradcol 0xFFFFFF 0.825 0 [
			uihlist 0 [
				uispace 0.005
				uitarget 0.54 0 [
					uivlist 0 [
						uispace 0.005
						uicolortext (getText $Settings_Menu_AspectRatio) 0x222222 0.6 ; uialign* -1
						uispace 0 0.005
						uicolortext (getText $Settings_Menu_ScreenResolution) 0x222222 0.6 ; uialign* -1
					]
					uialign* -1
				]
				uispace 0.01
				uivlist 0 [
					uihlist 0 [
						UI_listslider UI_aspectratio =s $opt_ratios_assoc 0.2 0.025 [
							if (=s $UI_aspectratio "custom") [] [
								UI_resolution = (at $[UI_ratio@UI_aspectratio] 1)
									screenw (at $UI_resolution 0)
									screenh (at $UI_resolution 1)
							]
						]
					]
					uispace 0 0.005
					UI_listslider UI_resolution = (
						looplistconcat i $[UI_ratio@UI_aspectratio] [
							result (concat (escape $i) (strreplace $i " " "x"))
						]
					) 0.2 0.025 [
						screenw (at $UI_resolution 0)
						screenh (at $UI_resolution 1)
					]
				]
			]
			uialign* -1
		]
	]
	uialign* -1
	
	uispace 0 0.0015
	
	UI_settingcheckbox $Settings_Menu_FullScreen [fullscreen (! $fullscreen)] [$fullscreen] $Settings_Menu_FullScreenDesc
	
	UI_settingcheckbox $Settings_Menu_Vsync [vsync (! $vsync)] [$vsync] $Settings_Menu_VsyncDesc
	
	UI_settingslider $Settings_Menu_RenderScale [gscale] [$gscale] [25] [100] [1]
	
	UI_settingslider $Settings_Menu_Gamma [gamma] [$gamma] [20] [200] [1]
	
	UI_settingslider $Settings_Menu_AnisoFiltering [aniso] [(getText $Settings_Menu_FilteringQuality (div $aniso 4))] [0] [16] [4] [] $Settings_Menu_AnisoFilteringDesc $TRUE
	
	UI_settingslider $Settings_Menu_SmaaAntiAliasing [smaaquality] [(getText $Settings_Menu_FilteringQuality $smaaquality)] [0] [3] [1] [if (= $smaaquality 0) [smaa 0][smaa 1]] $Settings_Menu_SmaaAntiAliaDesc $TRUE
	
	UI_settingslider $Settings_Menu_FxaaAntiAliasing [fxaaquality] [(getText $Settings_Menu_FilteringQuality $fxaaquality)] [0] [3] [1] [if (= $fxaaquality 0) [fxaa 0][fxaa 1]] $Settings_Menu_FxaaAntiAliaDesc $TRUE
	
	UI_settingcheckbox $Settings_Menu_TxaaAntiAliasing [tqaa (! $tqaa)] [$tqaa] $Settings_Menu_TxaaAntiAliaDesc
]

/////////////////////////////////////////////////////////////////////////////// AUDIO SETTINGS //////////////////////////////////////////////////////////////////////
speaker_ico = ["media/interface/hud/speaker_on.png" "media/interface/hud/speaker_off.png"]

show_audio = [
	uispace 0 0.0035
	uitarget 0.825 0.05 [
		uihover [setgradsize = 0.038 ; setgradcol = 0xE5DFDB] [setgradsize = 0.035 ; setgradcol = 0xDAD4D1]
		uimodhgradient $setgradcol 0xFFFFFF 0.825 0.06 [
			uihlist 0 [
				uihlist 0 [
					uispace 0.005
					uicolortext (getText $Settings_Menu_Sounds) 0x222222 0.6
				]
				uispace 0.005
				uihlist 0 [
					if @mutesounds [col1 = 0xFF0000 ; col2 = 0xAA0000] [col1 = 0x00FF00 ; col2 = 0x00AA00]
					uivgradient $col1 $col2 0.07 0.05 [uiimage (at $speaker_ico @mutesounds) 0.06 0.042 ; uioutline 0x000000 ; uiclamp- 1 1 1 1 ; uirelease [mutesounds (! $mutesounds)]]
				]
			]
			uialign* -1
		]
	]
	uialign* -1
	
	uispace 0 0.0015
	
	UI_settingslider $Settings_Menu_MainVolume [soundvol] [$soundvol] [0] [100] [1]
	
	UI_settingslider $Settings_Menu_MusicVolume [musicvol] [$musicvol] [0] [100] [1]
	
	UI_settingslider $Settings_Menu_MaxSoundAtOnce [maxsoundsatonce] [$maxsoundsatonce] [32] [128] [1] [] $Settings_Menu_MaxSoundAtOnceDesc $TRUE
	
	//tmpvals = ["11025" "22050" "44100" "48000"]
	//UI_settingslider $Settings_Menu_SamplingFreq [soundfreq_ui] [$soundfreq] [0] [3] [1] [soundfreq = (at $tmpvals $soundfreq_ui)]
	
	UI_settingcheckbox $Settings_Menu_MinimizedSounds [minimizedmute (! $minimizedmute)] [$minimizedmute]
]

/////////////////////////////////////////////////////////////////////////////// KEYS SETTINGS //////////////////////////////////////////////////////////////////////

cmd_action = [forward backward left right jump crouch shoot dozoom meleeattack launchgrenade "aptitude 0" "aptitude 1" "aptitude 2" doautowield interact dropflag saycommand sayteamcommand]

show_commands = [
	bindidx = 0
	uihlist 0 [
		uiscroll 0.85 0.6 [
			uivlist 0.0010 [
				uispace 
				looplist i $cmd_action [
					uispace 0 0.0005
					uitarget 0.8 0.02 [
						uihover [setgradcol = 0xE5DFDB] [setgradcol = 0xDAD4D1]
						uimodhgradient $setgradcol 0xFFFFFF 0.7 0.035 [
							uihlist 0 [
								uispace 0.005
								uitarget 0.39 0.02 [uicolortext (getText $Settings_CommandsNames $bindidx) 0x222222 0.6 ; uialign* -1]
								[newbinds@bindidx] = (searchbinds $i)
								uihgradient 0x777777 0x888888 $arg2 $arg3 [
									uikeyfield [newbinds@bindidx] 30 [ 
										oldbinds = (searchbinds [@@i])
										looplist j $oldbinds [bind $j ""]
										looplist j $[newbinds@@bindidx] [bind $j [@@@i]]
									] 0.6 [uioutline (uifocus? $c_focus 0x444444); uiclamp- 1 1 1 1]
									bindidx = (+ $bindidx 1)
								]
							]
							uialign* -1
						]
						uialign* -1
					]
					uispace 0 0.00015	
				]
				
				uispace 0 0.01

				uitarget 0.775 0.05 [
					uihover [setgradcol = 0xE5DFDB] [setgradcol = 0xDAD4D1]
					uimodhgradient $setgradcol 0xFFFFFF 0.7 0.05 [
						uihlist 0 [
							uispace 0.005
							uitarget 0.39 0.02 [uicolortext (getText $Settings_Menu_DefaultKeysConfig) 0x222222 0.6 ; uialign* -1]
							uihlist 0 [
								UIbutton "hold5" [
									uicolortext (getText $Settings_Menu_DefaultKeysReset) 0x000000 0.6
								] 0.39 0.03 [clearallbinds ; exec "config/default_binds.cfg"]
							]
						]
						uialign* -1
					]
					uialign* -1
				]
				uialign* -1
			]
		]
		UIvscroll 0.025 0.59 2.5
	]
	uialign* -1
]

show_console = [/////////////////////////////////////////////////////////////////////////////// HUD SETTINGS //////////////////////////////////////////////////////////////////////
	uihlist 0 [
		uiscroll 0.85 0.6 [
			uivlist 0 [
				uispace 0 0.0035

				UI_settingslider $Settings_Menu_HudSize [hudscale] [$hudscale] [0] [100] [1]
				
				UI_settingslider $Settings_Menu_CrosshairSize [crosshairsize] [$crosshairsize] [20] [50] [1]
				
				UI_settingslider $Settings_Menu_DamagesSize [huddamagesize] [(*f $huddamagesize 10)] [0.05] [0.19] [0.01]
				
				UI_settingcheckbox $Settings_Menu_ShowMinimap [showminimap (! $showminimap)] [$showminimap]
				
				UI_settingcheckbox $Settings_Menu_ShowXpBar [hudxpbar (! $hudxpbar)] [$hudxpbar]
				
				UI_settingcheckbox $Settings_Menu_SpectatorInfos [showspecplayerinfo (! $showspecplayerinfo)] [$showspecplayerinfo]

				UI_settingcheckbox $Settings_Menu_ShowFps [showfps (! $showfps)] [$showfps]
						
				UI_settingcheckbox $Settings_Menu_ShowPing [showmyping (! $showmyping)] [$showmyping]

				UI_settingcheckbox $Settings_Menu_Con_PublicChat [conchat (! $conchat) ; if $conchat [confilter = (+ @confilter 0x100)][confilter = (- @confilter 0x100)]] [$conchat]
				
				UI_settingcheckbox $Settings_Menu_Con_TeamChat [conteamtchat (! $conteamtchat) ; if $conteamtchat [confilter = (+ @confilter 0x200)][confilter = (- @confilter 0x200)]] [$conteamtchat]

				UI_settingcheckbox $Settings_Menu_Con_GameEvents [congame (! $congame) ; if $congame [confilter = (+ @confilter 0x400)][confilter = (- @confilter 0x400)]] [$congame]
						
				UI_settingcheckbox $Settings_Menu_Con_MyFrags [conkills (! $conkills) ; if $conkills [confilter = (+ @confilter 0x800)][confilter = (- @confilter 0x800)]] [$conkills]
				
				UI_settingcheckbox $Settings_Menu_Con_OtherFrags [conallkills (! $conallkills) ; if $conallkills [confilter = (+ @confilter 0x1000)][confilter = (- @confilter 0x1000)]] [$conallkills]

				UI_settingcheckbox $Settings_Menu_Con_ImportantInfos [coninfos (! $coninfos) ; if @coninfos [confilter = (+ @confilter 0x01)][confilter = (- @confilter 0x01)]] [$coninfos] $Settings_Menu_Con_ImpInfosDesc
				
				UI_settingcheckbox $Settings_Menu_Con_Warnings [conwarnings (! $conwarnings) ; if @conwarnings [confilter = (+ @confilter 0x02)][confilter = (- @confilter 0x02)]] [$conwarnings] $Settings_Menu_Con_WarningsDesc
				
				UI_settingcheckbox $Settings_Menu_Con_Errors [conerrors (! $conerrors) ; if @conerrors [confilter = (+ @confilter 0x04)][confilter = (- @confilter 0x04)]] [$conerrors] $Settings_Menu_Con_ErrorsDesc
				
				UI_settingcheckbox $Settings_Menu_Con_InitMessages [coninit (! $coninit) ; if @coninit [confilter = (+ @confilter 0x10)][confilter = (- @confilter 0x10)]] [$coninit] $Settings_Menu_Con_InitMsgDesc
				
				UI_settingcheckbox $Settings_Menu_Con_ScriptMessages [conscripts (! $conscripts) ; if @conscripts [confilter = (+ @confilter 0x20)][confilter = (- @confilter 0x20)]] [$conscripts] $Settings_Menu_Con_ScriptMsgDesc
			]
		]
		UIvscroll 0.025 0.59 2.5
	]
]


///////////////////////////////////////////////////////////////////////////////
// pending changes                                                           //
///////////////////////////////////////////////////////////////////////////////
UI_window "changes" "media/interface/hud/options.jpg" (getText $Settings_PendingChanges_Title) [
	uivgradient 0xCCCCCC 0x999999 0.1 0.02 [
		uivlist 0 [
			uispace 0 0.005
			uicolortext (getText $Settings_PendingChanges_Desc) 0x000000 0.6
			uispace 0 0.005
			uivlist 0.01 [
				loop i $pendingchanges [
					uicolortext (concatword (+ $i 1) (getText $Misc_PrePuncSpace) ": " (pendingchanges $i)) 0x3A3A3A 0.6
				]
			]
			uispace 0 0.005
			uihlist 0 [
				UIbutton "hold5" [uiimage "media/interface/hud/checkbox_on.jpg" 0.03 0.03 ; uitext (getText $Misc_Yes) 0.6] 0.25 0.035 [applychanges]
				UIbutton "hold5" [uiimage "media/interface/hud/checkbox_off.jpg" 0.03 0.03 ; uitext (getText $Misc_No) 0.6] 0.25 0.035 [hideui "changes" ; clearchanges]
			]
	    ]
	]
] [if (= $pendingchanges 0) [hideui "changes"]] [] $TRUE

///////////////////////////////////////////////////////////////////////////////
// LODs                                                                      //
///////////////////////////////////////////////////////////////////////////////
setshadowslod = [
	case $shadowslod 0 [
        csmfarplane = 512
		csmmaxsize 512
        smfilter = 1
		smcache = 2
        smdynshadow = 0
        smnoshadow = 1
        smsize = 9
	]
	case $shadowslod 1 [
        csmfarplane = 768
		csmmaxsize 512
        smfilter = 2
		smcache = 2
        smdynshadow = 0
        smnoshadow = 0
        smsize = 10
	]
	case $shadowslod 2 [
        csmfarplane = 1024
		csmmaxsize 768
        smfilter = 3
		smcache = 1
        smdynshadow = 1
        smnoshadow = 0
        smsize = 11
	]
	case $shadowslod 3 [	
        csmfarplane = 1280
		csmmaxsize = 1024
        smfilter = 3
		smcache = 1
        smdynshadow = 1
        smnoshadow = 0
        smsize = 13
	]
]

setlightlod = [
	case $lightlod 0 [
		dynlightdist = 256
        rhfarplane = 512
        volumetric = 0
        aocutoff = 4
	]
	case $lightlod 1 [
		dynlightdist = 512
        rhfarplane = 512
        volumetric = 1
        volsteps = 10;
        aocutoff = 8
	]
	case $lightlod 2 [
		dynlightdist = 1536
        rhfarplane = 1024
        volumetric = 1
        volsteps = 20
        aocutoff = 16
	]
	case $lightlod 3 [
		dynlightdist = 3072
        rhfarplane = 2048
        volumetric = 1
        volsteps = 40
        aocutoff = 32
	]
]

setwaterlod = [
	case $waterlod 0 [
        vertwater = 0
        caustics = 0
	]
	case $waterlod 1 [
        vertwater = 0
        caustics = 1
	]
	case $waterlod 2 [
        vertwater = 1
        caustics = 1
        waterlod = 2
        watersubdiv = 2
	]
	case $waterlod 3 [	
        vertwater = 1
        caustics = 1
        waterlod = 3
        watersubdiv = 3
	]
]

setparticleslod = [
	case $particleslod 0 [
        maxparticles = 512
        softparticles = 0
		maxparticledistance = 400
		maxstaintris = 512
        stainfade = 5
	]
	case $particleslod 1 [
        maxparticles = 2048
        softparticles = 1
		maxparticledistance = 800
        maxstaintris = 2048
        stainfade = 15
	]
	case $particleslod 2 [
        maxparticles = 8192
        softparticles = 1
		maxparticledistance = 1600
        maxstaintris = 8192
        stainfade = 60
	]
	case $particleslod 3 [	
        maxparticles = 16384
        softparticles = 1
		maxparticledistance = 2400
        maxstaintris = 20480
        stainfade = 300
	]
]

settrislod = [
	case $trislod 0 [
        grass 0
		temptrisfade = 3000
		ragdollmillis = 3000
		maxmodelradiusdistance = 35
		loddistfactor = 3
		bnclifetime = 2500
	]
	case $trislod 1 [
		grass 1
        grassdist = 512
        grassdist = 150
        grassstep = 3.0
		temptrisfade = 6000
		ragdollmillis = 6000
		maxmodelradiusdistance = 70
		loddistfactor = 5
		bnclifetime = 5000
	]
	case $trislod 2 [
		grass 1
	    grassdist = 578
        grassstep = 2.5
		temptrisfade = 8000
		ragdollmillis = 8000
		maxmodelradiusdistance = 100
		loddistfactor = 8
	]
	case $trislod 3 [
		grass 1
		grassdist = 1024
        grassstep = 2.0
		temptrisfade = 15000
		ragdollmillis = 10000
		maxmodelradiusdistance = 150
		loddistfactor = 12
	]
]

setreflectionslod = [
	case $reflectionslod 0 [
        envmapsize = 4
		waterreflect = 0
	]
	case $reflectionslod 1 [
        envmapsize = 6
		waterreflect = 1
		waterreflectstep = 2
	]
	case $reflectionslod 2 [
        envmapsize = 8
		waterreflect = 1
		waterreflectstep = 4
	]
	case $reflectionslod 3 [	
        envmapsize = 10
		waterreflect = 1
		waterreflectstep = 32
	]
]