///////////////////////////////////////////////////////////////////////////////
//  Player classes menu                                                      //
///////////////////////////////////////////////////////////////////////////////
UI_window "classes" "media/interface/hud/sournois.jpg" @Classes_Menu_Title [
	uivgradient 0xEEEEEE 0xBBBBBB 0.9 0.7 [
		uihlist 0 [
			apt_num = 0
			col_space = 0.0
			uispace 0.005
			loop i 3 [ ////////////////////////////////////////CLASSES TABS/////////////////////////////////////////////////////////////////////////
				uivlist 0 [
					uispace 0 $col_space
					loop i 5 [
						uivlist 0 [
							uihover [
								aptgradx = 0.105 ; aptgrady = 0.120 ; imgsize = 0.065 ; txtsize = 0.54 ; tabcola = $c_tab_hv_up ; tabcolb = $c_tab_hv_dn][
								aptgradx = 0.100 ; aptgrady = 0.115 ; imgsize = 0.060 ; txtsize = 0.50 ; tabcola = $c_tab_up ; tabcolb = $c_tab_dn
							]
							uitarget 0.105 0.120 [
								if (= $apt_num @player1_aptitude) [tabcola = $c_tab_on_up ; tabcolb = $c_tab_on_dn]
								uivgradient $tabcola $tabcolb $aptgradx $aptgrady [
									UI_outline
									uivlist 0.003 [
										uispace 0.005
										uiimage (getclasslogo 0 $apt_num) $imgsize $imgsize
										uicolortext (at $Classes_Names $apt_num) 0xFFFFFF $txtsize
									]
								]
								uirelease [player1_aptitude = $apt_num]
							]
						]
						uispace 0 0.003
						apt_num = (+ $apt_num 1)
					]
				]
				uispace 0.005
				col_space = (+f $col_space 0.012)
			]
			
			UIbar 0 1
			
			uitarget 0.61 0.7 [ ////////////////////////////////////////CLASSE STATS/////////////////////////////////////////////////////////////////////////
				uivlist 0 [
					uivgradient 0x96B9D0 0x76A0CA 0.65 0.1 [
						uispace 0.3 0.03
						uihlist 0 [
							uispace 0.005
							uiimage (getclasslogo 0 -2) 0.07 0.07
							uispace 0.01
							uicolortext (at $Classes_Names @player1_aptitude)  0x252525 0.8
							uicolortext " - " 0x252525 0.8
							uicolortext (at $Classes_Descs @player1_aptitude) 0x252525 0.8
						]
						uialign* -1
						uioutline 0x000000
						uiclamp 1 1 1 1
					]
					
					uivlist 0 [
						uihlist 0 [
							uitarget 0.185 0 [
								uivlist 0 [
									loop i 4 [
										uihlist 0 [
											uispace 0.005
											uispace 0 0.006 [uicolortext (at $Classes_Stats $i) 0x444444 0.55]
											uialign -1
										]
									]
									uialign -1
								]
							]
							
							uivlist 0 [
								stat_num = 0
								loop i 4 [
									uihlist 0 [
										loop i 20 [
											if (< (getaptistatval @player1_aptitude $stat_num) $i)[barcola = 0xCCCCCC ; barcolb = 0xAAAAAA][barcola = (uirggradient $i 20 1.6) ; barcolb = (uirggradient $i 20 1)]
											uivgradient $barcola $barcolb 0.021 0.015
											uispace 0.001 0.018
										]
										uispace 0.005
									]
									stat_num = (+ $stat_num 1)
								]
							]
						]
					]
					
					uispace 0 0.005
					
					uivlist 0 [ ////////////////////////////////////////CLASSE DESC/////////////////////////////////////////////////////////////////////////
						uivgradient 0x96B9D0 0x76A0CA 0.65 0.04 [
							uispace 0.3 0.03
							uihlist 0 [
								uiimage "media/interface/hud/info.jpg" 0.05 0.05
								uispace 0.01
								uicolortext (at $Classes_Menu_Abilities) 0x252525 0.8
							]
							uioutline 0x000000
							uiclamp 1 1 1 1
							uialign* -1
						]
						
						uispace 0 0.005
						
						uihlist 0 [
							uispace 0.005
							uivlist 0 [
								loop i (listlen (at $Classes_Specs @player1_aptitude)) [
									uispace 0.002
									uihlist 0 [
										UItriangle 0x22CC22 0.015 0.015 270 ; uispace 0.005
										uiwrapcolortext (at (at $Classes_Specs @player1_aptitude) $i) 0.6 0x000000 0.52
									]
									uispace 0 0.01
								]
								uialign* -1 0
							]
						]
						uialign* -1 0
					]
				]
				uialign* -1 -1
			]
		]
	]
] [hideui "main"] [showui "main"]

UI_window "classessetup" "media/interface/hud/mapeditor.jpg" @ClassesSetup_Menu_Title [
	uivgradient 0xEEEEEE 0xBBBBBB 0.68 0.50 [
		uihlist 0 [
			apt_num = 0
			col_space = 0.0
			uispace 0.005
			loop i 5 [ ////////////////////////////////////////CLASSES TABS/////////////////////////////////////////////////////////////////////////
				uivlist 0 [
					uispace 0 $col_space
					loop i 3 [
						uivlist 0 [
							uihover [
								aptgradx = 0.105 ; aptgrady = 0.120 ; imgsize = 0.065 ; txtsize = 0.54 ; tabcola = $c_tab_hv_up ; tabcolb = $c_tab_hv_dn][
								aptgradx = 0.100 ; aptgrady = 0.115 ; imgsize = 0.060 ; txtsize = 0.50 ; tabcola = $c_tab_on_up ; tabcolb = $c_tab_on_dn
							]
							uitarget 0.105 0.120 [
								if (getclass $apt_num) [tabcola = $c_tab_up ; tabcolb = $c_tab_dn]
								if (= $apt_num $player1_aptitude) [tabcola = 0xCCCCCC ; tabcolb = 0xBBBBBB]
								uivgradient $tabcola $tabcolb $aptgradx $aptgrady [
									UI_outline
									uivlist 0.003 [
										uispace 0.005
										uiimage (getclasslogo 0 $apt_num) $imgsize $imgsize
										uicolortext (at $Classes_Names $apt_num) 0xFFFFFF $txtsize
									]
								]
								uirelease [(setclass $apt_num)]
							]
						]
						uispace 0 0.003
						apt_num = (+ $apt_num 1)
					]
				]
				uispace 0.005
				col_space = (+f $col_space 0.008)
			]
		]
	]
] [hideui "solo"] [showui "solo"]