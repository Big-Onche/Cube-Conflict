mode_ico = ["media/interface/hud/flingue.jpg" "media/interface/hud/teamdm.png" "media/interface/hud/drapeau_allie.png" "media/interface/hud/domination.png" "media/interface/hud/invasion.png" "media/interface/hud/info.jpg"]

UI_window "solo" "media/interface/hud/mapeditor.jpg" (getText $Solo_Play_Menu_Title) [
	uivgradient 0xEEEEEE 0xBBBBBB 0.9 0.6 [
		uihlist 0 [
			uispace 0.005
			uivlist 0 [
				loop i 6 [
					uihlist 0 [
						uihover [
							setgradx = 0.27 ; setgrady = 0.10 ; imgsize = 0.09 ; txtsize = 0.65 ; tabcola = $c_tab_hv_up ; tabcolb = $c_tab_hv_dn][
							setgradx = 0.26 ; setgrady = 0.09 ; imgsize = 0.08 ; txtsize = 0.6 ; tabcola = $c_tab_up ; tabcolb = $c_tab_dn
						]
						uitarget 0.27 0.10 [
							if (= $i $game_mode)[tabcola = 0xA5C9A5 ; tabcolb = 0x83A883]
							uivgradient $tabcola $tabcolb $setgradx $setgrady [
								uihlist 0 [
									uispace 0.003
									uiimage (at $mode_ico $i) $imgsize $imgsize
									uispace 0.003
									uiwrapcolortext (getText $Solo_Play_Menu_ModeNames $i) 0.16 0xFFFFFF $txtsize
								]
								uialign* -1
							]
							uirelease [game_mode = $i]
						]
					]
				]
			]
			
			uispace 0.005
		
			UIbar 0 1

			uitarget 0.4 0.6 [/////////////////////////////////////////////////////////////////////////////// GAME MODE CONFIG //////////////////////////////////////////////////////////////////////
				uivlist 0 [
					uivgradient 0x96B9D0 0x76A0CA 0.4 0.1 [
						uispace 0.3 0.03
						uihlist 0 [
							uispace 0.005
							uiimage (at $mode_ico $game_mode) 0.07 0.07
							uispace 0.01
							uicolortext (getText $Solo_Play_Menu_ModeNames $i) 0x252525 0.8
						]
						uialign* -1
						uiclamp 1 1 1 1
					]
							
					uispace 0 0.0025
							
					uihlist 0 [
						uivlist 0 [show_gamesettings]
						uispace 0.005
						uialign* -1 -1
					]
					uialign* -1 -1
					
					UIbar 1 0
				
					uitarget 0 0.08 [UI_Pretty_Button $Solo_Play_Menu_Play [calcmode ; mode $final_mode ; mapselect ; addbots $numbots -1 ; hideallui]]
				]
				uialign* -1 -1
			]
		]
	]
] [hideui "main"] [showui "main"]

selectedMap = 0

mapselect = [
	if (< $game_mode 5) [map (at $localMaps $selectedMap)][map "tutorial"]
]

show_gamesettings = [
	if (= $game_mode 4) [
		uihlist 0 [
			uispace 0.005
			uiwrapcolortext (getText $Solo_Play_Menu_InvasionDesc) 0.75 0x222222 0.55
		]
		uialign* -1
	]
	if (= $game_mode 5) [
		uihlist 0 [
			uispace 0.005
			uiwrapcolortext (getText $Solo_Play_Menu_TutorialDesc) 0.75 0x222222 0.55
		]
		uialign* -1
	]
	
	if (< $game_mode 4) [
		uispace 0 0.0015
		uitarget 0.6 0.05 [
			uihover [setgradsize = 0.038 ; setgradcol = 0xE5DFDB] [setgradsize = 0.035 ; setgradcol = 0xDAD4D1]
			uimodhgradient $setgradcol 0xFFFFFF 0.7 0.06 [
				uihlist 0 [
					uispace 0.005
					uitarget 0.25 0.03 [uicolortext (getText $Solo_Play_Menu_Mutator) 0x222222 0.6 ; uialign* -1]
					uispace 0.13
					if (= $game_mode 3) [
						UI_listslider muta_mode = [1 (@Mut_Pickup) 2 (@Mut_Random) 3 (@Mut_FullStuff) 4 (@Mut_Identical) 5 (@Mut_Regen)] 0.25 0.025
						] [
						if (= $muta_mode 5) [muta_mode = (- $muta_mode 1)]
						UI_listslider muta_mode = [1 (@Mut_Pickup) 2 (@Mut_Random) 3 (@Mut_FullStuff) 4 (@Mut_Identical)] 0.25 0.025
					]
				]
			]
		]
		uispace 0 0.0015

		UI_settingslider $Solo_Play_Menu_GameLength [gamelength] [$gamelength] [3] [60] [1] [] $Solo_Play_Menu_GameLengthDesc

		UI_settingslider $Solo_Play_Menu_BotSkill [botlevel] [(getText $Solo_Play_Menu_BotSettings $botlevel)] [0] [4] [1] [] [(getText $Solo_Play_Menu_BotSkillDesc $botlevel)] $TRUE
			
		UI_settingslider $Solo_Play_Menu_BotAmount [numbots] [$numbots] [0] [49] [1] [] $Solo_Play_Menu_BotAmountDesc
	]	
	
	if (< $game_mode 5) [
		uispace 0 0.0015
		uitarget 0.6 0.05 [
			uihover [setgradsize = 0.038 ; setgradcol = 0xE5DFDB] [setgradsize = 0.035 ; setgradcol = 0xDAD4D1]
			uimodhgradient $setgradcol 0xFFFFFF 0.7 0.06 [
				uihlist 0 [
					uispace 0.005
					uitarget 0.25 0.03 [uicolortext (getText $Solo_Play_Menu_MapSetup) 0x222222 0.6 ; uialign* -1]
					uispace 0.13
					UI_listslider selectedMap = [0 "" 1 "" 2 "" 3 "" 4 "" 5 ""] 0.25 0.025 [getcurmapsel (at $localMaps $selectedMap)] 0.5 [uitext (getText $Map_Names $selectedMap) 0.6]
				]
			]
		]
	]
	
	uispace 0 0.0030
	
	if (|| (= $selectedMap 0) (= $selectedMap 3) (= $selectedMap 5)) [txtcol = 0x222222 ; mingradcol = 0x000000] [txtcol = 0x777777 ; mingradcol = 0x222222]
	case $game_mode 5 [txtcol = 0x222222 ; mingradcol = 0x000000]
	
	uitarget 0.6 0.05 [
		uihover [setgradsize = 0.038 ; setgradcol = 0xE5DFDB] [setgradsize = 0.035 ; setgradcol = 0xDAD4D1]
		setgradcol = (- $setgradcol $mingradcol)
		uimodhgradient $setgradcol 0xFFFFFF 0.7 0.06 [
			uihlist 0 [
				uispace 0.005
				uitarget 0.25 0.03 [uicolortext (getText $Solo_Play_Menu_MapAtmosphere) $txtcol 0.6 ; uialign* -1]
				uispace 0.13
				if (= $mingradcol 0) [UI_listslider map_atmo = [0 "" 1 "" 2 "" 3 "" 4 "" 5 "" 6 "" 7 "" 8 "" 9 ""] 0.25 0.025 [] 0.5 [uitext (getText $Atmospheres_Names $map_atmo) 0.6]][uispace 0.1375]
			]
		]
	]
	uispace 0 0.0105
]