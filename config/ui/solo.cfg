mode_names = ["Tue les tous" "Deathmatch" "Tue les tous en équipe" "Team deatchmatch" "Capture de drapeau" "Capture the flag" "Conquête" "Domination" "Invasion" "Invasion" "Tutoriel" "Tutorial"]
mode_ico = ["media/interface/hud/flingue.jpg" "media/interface/hud/teamdm.png" "media/interface/hud/drapeau_allie.png" "media/interface/hud/domination.png" "media/interface/hud/invasion.png" "media/interface/hud/info.jpg"]

UImenu "solo" [ /////////////////////////////////////////////////////////////////////////////// MENU TABS //////////////////////////////////////////////////////////////////////
	infotxt = ""
	uivgradient 0xEEEEEE 0xBBBBBB 0.9 0.6 [
		uihlist 0 [
			uispace 0.005
			uivlist 0 [
				loop i 6 [
					uihlist 0 [
						uihover [
							setgradx = 0.27 ; setgrady = 0.10 ; imgsize = 0.09 ; txtsize = 0.65 ; tabcola = 0xCCCCAA ; tabcolb = 0xAAAA88][
							setgradx = 0.26 ; setgrady = 0.09 ; imgsize = 0.08 ; txtsize = 0.6 ; tabcola = 0x9F9F9F ; tabcolb = 0x7F7F7F
						]
						uitarget 0.27 0.10 [
							if (= $i $game_mode)[tabcola = 0xAACCAA ; tabcolb = 0x88AA88]
							uivgradient $tabcola $tabcolb $setgradx $setgrady [
								uihlist 0 [
									uispace 0.003
									uiimage (at $mode_ico $i) $imgsize $imgsize
									uispace 0.003
									uiwrapcolortext (at $mode_names (+ (* $i 2) @GAME_LANG)) 0.16 0xFFFFFF $txtsize
								]
								uialign* -1
							]
							uirelease [game_mode = $i ; if (< $game_mode 5) [if (= $map_sel 0) [map_sel = 1]][map_sel = 0]]
						]
					]
				]
			]
			
			uispace 0.005
		
			UIbar 0 1

			uitarget 0.4 0.6 [/////////////////////////////////////////////////////////////////////////////// GAME MODE CONFIG //////////////////////////////////////////////////////////////////////
				uivlist 0 [
					uivgradient 0x96B9D0 0x76A0CA 0.4 0.1 [
						uispace 0.3 0.03
						uihlist 0 [
							uispace 0.005
							uiimage (at $mode_ico $game_mode) 0.07 0.07
							uispace 0.01
							uicolortext (at $mode_names (+ (* $game_mode 2) @GAME_LANG)) 0x252525 0.8
						]
						uialign* -1
						uiclamp 1 1 1 1
					]
							
					uispace 0 0.0025
							
					uihlist 0 [
						uivlist 0 [show_gamesettings]
						uispace 0.005
						uialign* -1 -1
					]
					uialign* -1 -1
					if $infotxt [showui "infobox"][hideui "infobox"]
					
					UIbar 1 0
				
					play
				]
				uialign* -1 -1
			]
		]
	]
] [hideui "main"] [showui "main"] [] 0 "GAME MODES" "MODES DE JEUX" "media/interface/hud/souris.png"

botsettings = ["Nul" "Fag" "Bof" "n00b" "Moyen" "Average" "Bon" "Roxxor" "Expert" "Pro-gamer"]
botsdesc_fr = [
	"Tes adversaires seront juste débiles. A vrai dire je ne sais même pas pourquoi ce niveau existe...^nPeut-être utile si tu veux que ta mère essaie le jeu."
	"Tes adversaires seront similaires à des gamins de 15 ans qui passent leur vie sur TikTok. Ils s'appellent généralement ^"Kévin^" ou ^"Dylan^".^nIls sont pratiques pour s'entraîner si jamais tu n'as pas encore vraiment joué à ce jeu bizarre."
	"Tes adversaires seront des joueurs tout à fait moyens. QI de 100, petit pavillon, gros chien qui bave partout, bref le parfait french-dreamer.^nSi tu sais te servir de tes dix doigts autrement que pour la branlette, tu vas leur mettre une branlée."
	"Tes adversaires seront des giga chads prêts à en découdre avec un bon joueur. Ils maîtrisent le jeu, mais pas au point de perdre toute vie sociale.^nIls peuvent de te mettre la misère, mais tu devrais t'en sortir si tu as un peu d'expérience !"
	"Tes adversaires seront principalement des joueurs professionnels. Ils ont la souris réglée à 50 dpi, un champ de vision réglé au max,^nainsi que la dernière carte graphique de la mort. Attends-toi à te faire humilier, car si toi tu as des amis et une vie, eux non."
]
botsdesc_en = [
	"Your opponents will be fucking stupid. Honestly, I don't even know why this setting exists...^nBut this can be useful if you want to play the game with your mom."
	"Your opponents will be like 15-year-old kids who spend their lives on TikTok.^nThey're handy for practice if you haven't played this weird game yet."
	"Your opponents will be average players. IQ of 100, a small house, and a big dog that drools everywhere: the perfect doomer.^nIf you know how to use your fingers other than for handjobs, you're going to beat them."
	"Your opponents will be chads ready to fight with a good player. They have mastered the game, but not to the point of losing all social life.^nThey can beat you, but you should get by if you have a little experience!"
	"Your opponents will be mainly professional players. They have the mouse set to 50 dpi, the field of view set to max,^nand the last water-cooled graphics card. Expect to be destroyed, because if you have friends and social life, they don't."
]

show_gamesettings = [
	if (= $game_mode 4) [
		uihlist 0 [
			uispace 0.005
			if @GAME_LANG [
				uiwrapcolortext "Face waves of dumb but ruthless monsters! Luckily for you, they will sometimes drop some good stuff. Will you be able to survive 10 minutes? Be prepared for anything because that will escalate quickly." 0.75 0x222222 0.55
			][
				uiwrapcolortext "Affronte des vagues de monstres un peu idiots mais sans pitié ! Heureusement pour toi, ils lâcheront parfois de bon objets. Seras-tu capable de survivre 10 minutes ? Ca risque de vite partir en cacahuète." 0.75 0x222222 0.55
			]
		]
		uialign* -1
	]
	if (= $game_mode 5) [
		uihlist 0 [
			uispace 0.005
			if @GAME_LANG [
				uiwrapcolortext "In tutorial mode, you can chat with characters to get information about the game, test all weapons, items and abilities at will." 0.75 0x222222 0.55
			][
				uiwrapcolortext "Dans le mode tutoriel, tu peux discuter avec des personnages afin d'obtenir des informations sur le jeu, tester toutes les armes, objets et aptitudes à volonté." 0.75 0x222222 0.55
			]
		]
		uialign* -1
	]
	
	if (< $game_mode 4) [
		uispace 0 0.0015
		uitarget 0.6 0.05 [
			uihover [setgradsize = 0.038 ; setgradcol = 0xE5DFDB ; if @GAME_LANG [infotxt = @arg10][infotxt = @arg11]] [setgradsize = 0.035 ; setgradcol = 0xDAD4D1]
			uimodhgradient $setgradcol 0xFFFFFF 0.7 0.06 [
				uihlist 0 [
					uispace 0.005
					uitarget 0.25 0.03 [if @GAME_LANG [uicolortext "Game mutators" 0x222222 0.6][uicolortext "Modes de jeux" 0x222222 0.6] ; uialign* -1]
					uispace 0.13
					if (= $game_mode 3) [
						if @GAME_LANG [UIlistslider muta_mode = [1 "Weapon pickup" 2 "Random weapon" 3 "Full stuff" 4 "Identical weapons" 5 "Regeneration"] 0.25 0.025] [UIlistslider muta_mode = [1 "Collecte d'armes" 2 "Arme aléatoire" 3 "Armes multiples" 4 "Armes identiques" 5 "Régénération"] 0.25 0.025]] [
						if (= $muta_mode 5) [muta_mode = (- $muta_mode 1)]
						if @GAME_LANG [UIlistslider muta_mode = [1 "Weapon pickup" 2 "Random weapon" 3 "Full stuff" 4 "Identical weapons"] 0.25 0.025] [UIlistslider muta_mode = [1 "Collecte d'armes" 2 "Arme aléatoire" 3 "Armes multiples" 4 "Armes identiques"] 0.25 0.025]
					]
				]
			]
		]
		uispace 0 0.0015

		UIsettingslider ["Game length"] ["Durée de la partie"] [gamelength] [$gamelength] [3] [60] [1] [] [] ["Duration of the game in minutes."] ["Durée de la partie en minutes."]

		UIsettingslider ["Bot skill"] ["Niveau des bots"] [botlevel] [(at $botsettings (+ (* $botlevel 2) @GAME_LANG))] [0] [4] [1] [] [1] [(at $botsdesc_en $botlevel)][(at $botsdesc_fr $botlevel)]
			
		UIsettingslider ["Amount of bots"] ["Nombre de bots"] [numbots] [$numbots] [0] [49] [1] [] [] ["A high amount of bots can reduce performance and affect gameplay. It is recommended not to exceed 24 bots."] ["Un nombre élevé de bots peut réduire les performances et nuire à la jouabilité. Il est recommandé de ne pas dépasser 24 bots."]
	]	
	
	if (< $game_mode 5) [
		uispace 0 0.0015
		uitarget 0.6 0.05 [
			uihover [setgradsize = 0.038 ; setgradcol = 0xE5DFDB ; if @GAME_LANG [infotxt = @arg10][infotxt = @arg11]] [setgradsize = 0.035 ; setgradcol = 0xDAD4D1]
			uimodhgradient $setgradcol 0xFFFFFF 0.7 0.06 [
				uihlist 0 [
					uispace 0.005
					uitarget 0.25 0.03 [if @GAME_LANG [uicolortext "Map setup" 0x222222 0.6][uicolortext "Choix de la carte" 0x222222 0.6] ; uialign* -1]
					uispace 0.13
					if @GAME_LANG [UIlistslider map_sel = [1 "Village" 2 "Factory" 3 "Moon" 4 "Castle" 5 "Volcano"] 0.25 0.025] [UIlistslider map_sel = [1 "Village" 2 "Usine" 3 "Lune" 4 "Chateau" 5 "Volcan"] 0.25 0.025]
				]
			]
		]
	]
	
	uispace 0 0.0030
		
	txtcol = 0x777777
	mingradcol = 0x222222
	case $map_sel 1 [txtcol = 0x222222 ; mingradcol = 0x000000]
	case $map_sel 4 [txtcol = 0x222222 ; mingradcol = 0x000000]
	case $game_mode 5 [txtcol = 0x222222 ; mingradcol = 0x000000]
	
	uitarget 0.6 0.05 [
		uihover [setgradsize = 0.038 ; setgradcol = 0xE5DFDB ; if @GAME_LANG [infotxt = @arg10][infotxt = @arg11]] [setgradsize = 0.035 ; setgradcol = 0xDAD4D1]
		setgradcol = (- $setgradcol $mingradcol)
		uimodhgradient $setgradcol 0xFFFFFF 0.7 0.06 [
			uihlist 0 [
				uispace 0.005
				uitarget 0.25 0.03 [if @GAME_LANG [uicolortext "Map atmosphere" $txtcol 0.6][uicolortext "Ambiance de la carte" $txtcol 0.6] ; uialign* -1]
				uispace 0.13
				if (= $mingradcol 0) [if @GAME_LANG [UIlistslider map_atmo = [0 "Random" 1 "Evening" 2 "Night" 3 "Heat wave" 4 "Rain" 5 "Apocalypse" 6 "Morning" 7 "Sunset" 8 "Sleet" 9 "Snow"] 0.25 0.025] [UIlistslider map_atmo = [0 "Aléatoire" 1 "Soirée" 2 "Nuit" 3 "Canicule" 4 "Pluie" 5 "Apocalypse" 6 "Matinée" 7 "Crépuscule" 8 "Giboulées" 9 "Neige"] 0.25 0.025]][uispace 0.1375]
			]
		]
	]
	uispace 0 0.0105
]

mapselect = [
	if (< $game_mode 5) [map $map_sel][map 0]
]

play = [ ////////////////////////////////////////////////////////////////////////////// GAME MODE BTN //////////////////////////////////////////////////////////////////////
	uispace 0 0.009
	uihlist 0 [
        UIbutton "hold1" [
			if @GAME_LANG [uispace 0.0435][uispace 0.02437]
            UItriangle 0xFF5500 0.03 0.03 270
            if @GAME_LANG [uitext "^f7Start game" 0.9][uitext "^f7Lancer la partie" 0.9]
			UItriangle 0xFF5500 0.03 0.03 90
			if @GAME_LANG [uispace 0.0435][uispace 0.02437]
        ] 0 0 [calcmode ; mode $final_mode ; mapselect ; addbots $numbots -1 ; hideallui] -1
    ]
	uispace 0 0.009
]

map_config = [
	if (< $game_mode 3) [
		UItitle [
			uihlist 0 [
				uiimage "media/interface/hud/map.jpg" 0.04 0.04
				uispace 0.003
				if @GAME_LANG [uitext "^fbMAP SETUP" 0.60][uitext "^fbCHOIX DE LA MAP" 0.60]
			]
			uialign* -1 0
		]
		uispace 0 0.003
		uitablerow [
			uitext "^fb Map " 0.6
			if @GAME_LANG [
				UIlistslider map_sel = [1 "Village" 2 "Factory" 3 "Moon" 4 "Castle" 5 "Volcano"] 0.222 $UI_optrowh
			][
				UIlistslider map_sel = [1 "Village" 2 "Usine" 3 "Lune" 4 "Chateau" 5 "Volcan"] 0.222 $UI_optrowh
			]
			uialign* -1 0
		]
	]
	
	uispace 0 0.003
	
    uitablerow [
		case @map_sel 0 [atmo_config]
		case @map_sel 1 [atmo_config]
		case @map_sel 2 [uispace 0 0.016]
		case @map_sel 3 [uispace 0 0.016]
		case @map_sel 4 [atmo_config]
		case @map_sel 5 [uispace 0 0.016]
        uialign* -1 0
    ]
	uispace 0 0.007
]