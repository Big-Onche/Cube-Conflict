hudshadowcolor = 0x151515
hudshadowspace = 0.075

calcratio = [
	if ($fullscreen) [sw = (getdesktopw) ; sh = (getdesktoph)][sw = $screenw ; sh = $screenh] // if fullscreen we use desktop's resolution, window resolution if not
	hudw = (divf $sw (+ 45 $hudscale))    // hud width
	hudh = (divf $sh (+ 45 $hudscale))    // hud height
	imgsize = (divf $sw 1440) 	 		  // images size
	txtsize = (divf $sw 100) 	 		  // text size
]

newui "hud" [
	if (! $hidehud) [
		uiallowinput 0
		if (! $huddead) [
			uitarget $hudw $hudh [	////////////////////////////////////////// left side /////////////////////
				uivlist 0 [
					uivlist 0 [
						loop i 4 [
							showboost $i
						]				
						showability
						
						uihlist 0.2 [
							showhealth
							if (hudarmour) [showshield]
							uialign -1 1
						]
					]
					uialign -1 1
				]
			]
			
			uitarget $hudw $hudh [	////////////////////////////////////////// center /////////////////////
				uivlist 0 [showscores ; uialign 0 -1]
				uivlist 0 [
					if (@(isspectator 0 1)) [uigroup [showspec]]
					if (hudshowabilities) [showabilities]
					if ($hudxpbar) [showxpbar]
					uialign 0 1
				]
			]
			
			uitarget $hudw $hudh [	////////////////////////////////////////// right side /////////////////////
				uigroup [showperfs ; uialign 1 -1]
				uivlist 0 [
					if (hudflag) [showflag]
					showammo
					uialign 1 1
				]
			]
		][	////////////////////////////////////////// dead screen /////////////////////
			uitarget $hudw $hudh [
				uigroup [uiimage "media/interface/hud/dead.png" $imgsize $imgsize ; uialign -1 1]
			]	
		]
	]
] [calcratio]

showperfs = [
	perfsize =  (divf $txtsize 2.7)
	uimodvgradient 0x777777 0x777777 0 0 [
		uihlist 0.25 [
			if (@scoreboardmultiplayer) [if (@showmyping) [uitext (getcurping) $perfsize]]
			if (@showfps) [uitext (getcurfps) $perfsize]
		]
	]
]

showspec = [
	specsize = (divf $txtsize 2)
	specshadowspace = (divf $hudshadowspace 1.5)
	uihlist 0 [
		camerasize = (divf $imgsize 1.5)
		uiimage "media/interface/hud/camera.png" $camerasize $camerasize
		uispace 0.05
		uigroup [uioffset $specshadowspace $specshadowspace [uicolortext (hudspecname) $hudshadowcolor $specsize] ; uitext (hudspecname) $specsize]
	]
]

showscores = [
	uihlist 0 [
		scorew = (*f $imgsize 3.2)
		scoresize =  (divf $txtsize 1.16)
		timersize =  (divf $txtsize 2.4)
		uigroup [
			uiimage "media/interface/hud/scores.png" $scorew $imgsize
			if (hudctf) [uiimage "media/interface/hud/scores_ctf.png" $scorew $imgsize]
			uivlist 0 [
				uigroup [uioffset $hudshadowspace $hudshadowspace [uicolortext (hudscores 1) $hudshadowcolor $scoresize] ; uitext (hudscores) $scoresize]
				uitext (hudtimer) $timersize
			]
		]
	]
]

showabilities = [
	uigroup [
		uihlist -0.05 [
			if (= hudclass $KAMIKAZE) [uiimage (hudabilitylogo 1) $imgsize $imgsize][loop i 3 [uiimage (hudabilitylogo $i) $imgsize $imgsize]]
		]
		uihlist -0.05 [
			if (= hudclass $KAMIKAZE) [uiimage (hudabilitystatus 1) $imgsize $imgsize][loop i 3 [uiimage (hudabilitystatus $i) $imgsize $imgsize]]
		]
	]
]

percentage = 0.5

showxpbar = [
	barscale = (divf 144000 $sw)
	barw = (divf 966 $barscale) ; fillw = (divf 960 $barscale)
	barh = (divf 25 $barscale) ; fillh = (divf 19 $barscale)
	uigroup [
		progress = (-f $fillw (*f $fillw (hudlevelprogress)))
		uihlist 0 [uispace 0.015 ; uimodcolor 0x888888 $fillw $fillh ; uialign -1 1]
		uihlist 0 [uispace 0.015 ; uicolor 0x6000ff $progress $fillh ; uialign -1 1]
		uiimage "media/interface/hud/xp_bar.png" $barw $barh
		xpcountsize = (divf $txtsize 3)
		uigroup [uicolortext (hudxpcount) 0xFFFFFF $xpcountsize ; uialign 0 1]
	]
]

showhealth = [
	uihlist 0.07 [
		uiimage "media/interface/hud/health.png" $imgsize $imgsize
		uigroup [uioffset $hudshadowspace $hudshadowspace [uicolortext (hudhealth) $hudshadowcolor $txtsize] ; uicolortext (hudhealth) 0xFFFFFF $txtsize]
	]
]

armour_ico = ["media/interface/hud/s_wood.png" "media/interface/hud/s_iron.png" "media/interface/hud/s_gold.png" "media/interface/hud/s_magnet.png" "media/interface/hud/s_power.png"]
showshield = [
	uihlist 0.07 [
		uiimage (at $armour_ico (hudarmourtype)) $imgsize $imgsize
		uigroup [uioffset $hudshadowspace $hudshadowspace [uicolortext (hudarmour) $hudshadowcolor $txtsize] ; uicolortext (hudarmour) 0xFFFFFF $txtsize]
	]
]

boost_ico = ["media/interface/hud/b_roids.png" "media/interface/hud/b_shrooms.png" "media/interface/hud/b_epo.png" "media/interface/hud/b_joint.png"]
showboost = [
	if (hudboost $arg1) [
		uihlist 0.07 [
			uiimage (at $boost_ico $arg1) $imgsize $imgsize
			uigroup [uioffset $hudshadowspace $hudshadowspace [uicolortext (hudboost $arg1) $hudshadowcolor $txtsize] ; uicolortext (hudboost $arg1) 0xFFFFFF $txtsize]
			uialign -1
		]
	]
]

ammo_ico = ["media/interface/hud/ammo.png" "media/interface/hud/sword.png"]
showammo = [
	uihlist 0.07 [
		if (hudinfammo) [uiimage "media/interface/hud/inf.png" $imgsize $imgsize] [
			uigroup [uioffset $hudshadowspace $hudshadowspace [uicolortext (hudammo) $hudshadowcolor $txtsize] ; uicolortext (hudammo) 0xFFFFFF $txtsize]
		]
		uiimage (at $ammo_ico (hudmelee)) $imgsize $imgsize
		uialign 1 1
	]
]

showflag = [
	uihlist 0.07 [
		uispace 0.9
		uiimage "media/interface/hud/drapeau_ennemi.png" $imgsize $imgsize
	]
]

showability = [
	if (hudability) [
		uihlist 0.07 [
			if (= (hudclass) $KAMIKAZE) [uiimage "media/interface/hud/timer.png" $imgsize $imgsize][
				if (= (hudclass) $VIKING) [uiimage "media/interface/hud/b_rage.png" $imgsize $imgsize][uiimage "media/interface/hud/mana.png" $imgsize $imgsize]
			]
			uigroup [uioffset $hudshadowspace $hudshadowspace [uicolortext (hudability) $hudshadowcolor $txtsize] ; uicolortext (hudability) 0xFFFFFF $txtsize]
			uialign -1
		]
	]
]