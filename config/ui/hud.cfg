hudshadowcolor = 0x151515
hudshadowspace = 0.075

calcratio = [
	hudw = (divf $screenw (+ 45 $hudscale))   // hud width
	hudh = (divf $screenh (+ 45 $hudscale))   // hud height
	imgsize = (divf $screenw 1440) 	 		  // images size
	txtsize = (divf $screenw 100) 	 		  // text size
]

newui "hud" [
	if (! $hidehud) [
		calcratio
		uiallowinput 0
		if (! $huddead) [
			uitarget $hudw $hudh [	////////////////////////////////////////// left side /////////////////////
				uivlist 0 [
					uivlist 0 [
						loop i 4 [
							showboost $i
						]				
						showability
						
						uihlist 0.2 [showhealth ; showshield ; uialign -1 1]
					]
					uialign -1 1
				]
			]
			
			uitarget $hudw $hudh [	////////////////////////////////////////// center /////////////////////
				uivlist 0 [
					if (hudshowabilities) [showabilities]
					if ($hudxpbar) [showxpbar]
					uialign 0 1
				]
			]
			
			uitarget $hudw $hudh [	////////////////////////////////////////// right side /////////////////////
				showammo
				uialign 0 1
			]
		][
			uitarget $hudw $hudh [
				uigroup [uiimage "media/interface/hud/dead.png" $imgsize $imgsize ; uialign -1 1]
			]	
		]
	]
]

showabilities = [
	uigroup [
		uihlist -0.05 [
			if (= hudclass $KAMIKAZE) [uiimage (hudabilitylogo 1) $imgsize $imgsize][loop i 3 [uiimage (hudabilitylogo $i) $imgsize $imgsize]]
		]
		uihlist -0.05 [
			if (= hudclass $KAMIKAZE) [uiimage (hudabilitystatus 1) $imgsize $imgsize][loop i 3 [uiimage (hudabilitystatus $i) $imgsize $imgsize]]
		]
	]
]

percentage = 0.5

showxpbar = [
	barscale = (divf 144000 $screenw)
	barw = (divf 966 $barscale) ; fillw = (divf 960 $barscale)
	barh = (divf 25 $barscale) ; fillh = (divf 19 $barscale)
	uigroup [
		progress = (*f $fillw (hudlevelprogress))
		uihlist 0 [uispace 0.015 ; uimodcolor 0x777777 $fillw $fillh ; uialign -1 1]
		uihlist 0 [uispace 0.015 ; uicolor 0x6000ff $progress $fillh ; uialign -1 1]
		uiimage "media/interface/hud/xp_bar.png" $barw $barh
		xpcountsize = (divf $txtsize 3)
		uigroup [uicolortext (hudxpcount) 0xFFFFFF $xpcountsize ; uialign 0 1]
	]
]

showhealth = [
	uihlist 0.07 [
		uiimage "media/interface/hud/health.png" $imgsize $imgsize
		uigroup [uioffset $hudshadowspace $hudshadowspace [uicolortext (hudhealth) $hudshadowcolor $txtsize] ; uicolortext (hudhealth) 0xFFFFFF $txtsize]
	]
]

armour_ico = ["media/interface/hud/s_wood.png" "media/interface/hud/s_iron.png" "media/interface/hud/s_gold.png" "media/interface/hud/s_magnet.png" "media/interface/hud/s_power.png"]
showshield = [
	uihlist 0.07 [
		uiimage (at $armour_ico (hudarmourtype)) $imgsize $imgsize
		uigroup [uioffset $hudshadowspace $hudshadowspace [uicolortext (hudarmour) $hudshadowcolor $txtsize] ; uicolortext (hudarmour) 0xFFFFFF $txtsize]
	]
]

boost_ico = ["media/interface/hud/b_roids.png" "media/interface/hud/b_shrooms.png" "media/interface/hud/b_epo.png" "media/interface/hud/b_joint.png"]
showboost = [
	if (hudboost $arg1) [
		uihlist 0.07 [
			uiimage (at $boost_ico $arg1) $imgsize $imgsize
			uigroup [uioffset $hudshadowspace $hudshadowspace [uicolortext (hudboost $arg1) $hudshadowcolor $txtsize] ; uicolortext (hudboost $arg1) 0xFFFFFF $txtsize]
			uialign -1
		]
	]
]

ammo_ico = ["media/interface/hud/ammo.png" "media/interface/hud/sword.png"]
showammo = [
	uihlist 0.07 [
		if (hudinfammo) [uiimage "media/interface/hud/inf.png" $imgsize $imgsize] [
			uigroup [uioffset $hudshadowspace $hudshadowspace [uicolortext (hudammo) $hudshadowcolor $txtsize] ; uicolortext (hudammo) 0xFFFFFF $txtsize]
		]
		uiimage (at $ammo_ico (hudmelee)) $imgsize $imgsize
		uialign 1 1
	]
]

showability = [
	if (hudability) [
		uihlist 0.07 [
			if (= (hudclass) $KAMIKAZE) [uiimage "media/interface/hud/timer.png" $imgsize $imgsize][
				if (= (hudclass) $VIKING) [uiimage "media/interface/hud/b_rage.png" $imgsize $imgsize][uiimage "media/interface/hud/mana.png" $imgsize $imgsize]
			]
			uigroup [uioffset $hudshadowspace $hudshadowspace [uicolortext (hudability) $hudshadowcolor $txtsize] ; uicolortext (hudability) 0xFFFFFF $txtsize]
			uialign -1
		]
	]
]