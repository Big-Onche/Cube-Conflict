newui "scoreboard" [
   if $mainmenu [hideui "scoreboard"]
   uiallowinput 0
   refreshscoreboard
   uimodcolor 0x7A7A7A 0 0 [
      uivlist 0 [
         UI_sbheader
         uifill 0 0.005
         if (> $getmode 5) UI_playertableteam UI_playertablesolo
         if (@hasspectator) [UI_spectatorlist]
      ]
   ]
] [if $mainmenu [hideui "scoreboard"]]

///////////////////////////////////////////////////////////////////////////////
UI_sbwsolo = 0.7
UI_sbwteam = 1.2
UI_sbheader = [
	uifill 0 0.005
	uifill (? (> $getmode 5) $UI_sbwteam $UI_sbwsolo) 0.05 [
		uifill (-f (? (> $getmode 5) $UI_sbwteam $UI_sbwsolo) (? (= $showip 1) 0.18 0)) 0 [
			uiclip (-f (? (> $getmode 5) $UI_sbwteam $UI_sbwsolo) (? (= $showip 1) 0.18 0)) 0 [
				uispace 0.01 0 [
					uivlist 0 [
						if $scoreboardservinfo [uitext $scoreboardservinfo 0.65] 
						
						uispace 0 0.005
						
						uihlist 0.003 [
							uitext "^fs[" 0.75
							if $scoreboardmultiplayer [uitext (concatword (at ["^f0" "^f0" "^f2" "^f3"] $getmastermode) (getmastermodename $getmastermode)) 0.7][uicolortext "Local" 0xCCCCCC 0.75]
							uitext "^fS] " 0.75
							uitext (getmodename $getmode) 0.75
							
						]
						
						uispace 0 0.005
						
						uihlist 0.015 [
							uifill
							uitext (at $Scoreboard_Map) 0.62
							uitext (getmaptitle) 0.62
							if (m_timed $getmode) [
								uitext "|" 0.62
								uitext (at $Scoreboard_TimeRemaining) 0.62
								uitext (concatword (? (|| $intermission $paused) "^fe" "^f8") $scoreboardtime) 0.62
							]
						]
					]
				]
			]
		]
		if $scoreboardmultiplayer [
			if $showip [
				uifill 0.18 0 [
					uialign 1
					UIbar 0 1; uialign- -1
					uiclip 0.18 0 [
						uialign 1
						uispace 0.01 0 [
							uivlist 0.004 [
								uicolortext $connectedip 0xA0A0A0 0.62
								uicolortext $connectedport 0xA0A0A0 0.62
								uialign* 1
							]
						]
						uialign- 1
					]
				]
			]
		]
	]
]
//-------------------------------------------------------------------------------------------------
UI_cw_n  = [-f (? (> $getmode 5) (*f $UI_sbwteam 0.54) $UI_sbwsolo) (? (> $getmode 9) (? $showscore 0.055)) (? $showkills 0.055) (? $showdeaths 0.055) (? $showlvl 0.055) (? $scoreboardmultiplayer (? $showpj 0.055)) (? $scoreboardmultiplayer (? $showping 0.055)) (? $scoreboardmultiplayer (? $showclientnum 0.055)) 0.03]
// ^ blarg

UI_playertablesolo = [
   uifill 0 0 [
      uitable 0 0 [
         uitableheader [
            uifill 0.01
			uicolortext (at $Scoreboard_Name) 0xDDDDDD 0.45 ; uialign- -1 0
			if $showkills        [uifill 0.055 0.05 [uiimage "media/interface/hud/flingue.jpg" 0.03 0.03] ; uialign-  1 1]
			if $showdeaths       [uifill 0.055 0.05 [uiimage "media/interface/hud/dead.png"    0.03 0.03] ; uialign-  1 1]
			if $showlvl       	 [uifill 0.055 0.05 [uiimage "media/interface/hud/stats.png"   0.03 0.03] ; uialign-  1 1]
            if $scoreboardmultiplayer [
				if $showclientnum [uifill 0.055 0.05 [uicolortext "#"     	0xA0A0A0 0.45 ; uialign- -1 1]]
                if $showping      [uifill 0.055 0.05 [uiimage "media/interface/hud/ping.png"    0.03 0.03] ; uialign- -1 1]
                if $showpj        [uifill 0.055 0.05 [uicolortext "PJ"    	0x77A1D9 0.45 ; uialign- -1 1]]
            ]
            uifill 0.01
         ] [uimodcolor 0x999999 0 0 [uiclamp 1 1 1 1]]
		 
         UI_sbtc = 0
		 
         loopscoreboard cn 0 [
            UI_sbtc = (! $UI_sbtc)
            uitablerow [
               uifill 0.01
               uifill (UI_cw_n) 0.026 [uihlist 0.005 [uiimage (getclasslogo $cn -1) 0.03 0.03 ; uialign- -1 ; uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.62 ; uispace 0.017] ; uialign* -1]
               if $showkills        [uifill 0.055 0.04 [uicolortext (getclientfrags  $cn)  0xFFD711 0.62 ; uialign-  0]]
               if $showdeaths       [uifill 0.055 0.04 [uicolortext (getclientdeaths $cn)  0xFF3333 0.62 ; uialign-  0]]
			   if $showlvl       	[uifill 0.055 0.04 [uicolortext (getclientlevel $cn)   0xCCCCCC 0.62 ; uialign-  0]]
               if $scoreboardmultiplayer [
                  if $showpj        [uifill 0.055 0.04 [uicolortext (? (isai $cn) "^f4-" (scoreboardpj $cn))   0x77A1D9 0.62 ; uialign-  0]]
                  if $showping      [uifill 0.055 0.04 [uicolortext (? (isai $cn) "^f4-" (scoreboardping $cn)) 0x77A1D9 0.62 ; uialign-  0]]
                  if $showclientnum [uifill 0.055 0.04 [uicolortext (? (isai $cn) "^f4-" $cn)                  0xA0A0A0 0.62 ; uialign-  0]]
               ]
               uifill 0.01
            ] [
               uicolor (? $UI_sbtc 0x99323232 0x99262626) 0 0 [uiclamp 1 1 1 1]
               pushif highlight (scoreboardhighlight $cn) [uioutline 0xFFFFFF (-f $UI_sbwsolo 0.00) 0.038]
            ]
         ]
      ]
   ]
]

UI_playertableteam = [
   getsbcolor
   uifill 0 0 [
      uihlist 0 [
         uitable 0 0 [
            uialign 0 -1
            uitableheader [
               uifill 0.01
               if $scoreboardmultiplayer [
                  if $showclientnum [uifill 0.055 0.05 [uicolortext "#"    0xA0A0A0 0.45 ; uialign- -1 1]]
                  if $showping      [uifill 0.055 0.05 [uiimage "media/interface/hud/ping.png"    0.03 0.03] ; uialign- -1 1]
                  if $showpj        [uifill 0.055 0.05 [uicolortext "PJ"   0x77A1D9 0.45 ; uialign- -1 1]]
               ]
			   if $showlvl       	[uifill 0.055 0.05 [uiimage "media/interface/hud/stats.png"   		0.03 0.03] ; uialign- -1 1]
			   if $showdeaths       [uifill 0.055 0.05 [uiimage "media/interface/hud/dead.png"    		0.03 0.03] ; uialign- -1 1]
			   if (> $getmode 9)    [if $showscore [uifill 0.055 0.05 [if (> $getmode 13) [uiimage "media/interface/hud/domination.png" 0.03 0.03][uiimage "media/interface/hud/drapeau_allie.png" 0.03 0.03]] ; uialign- -1 1]]
			   if $showkills        [uifill 0.055 0.05 [uiimage "media/interface/hud/flingue.jpg"	 	0.03 0.03] ; uialign- -1 1]
               uifill (UI_cw_n) 0.05 [uicolortext (getteamscore 1) 0xFFFFFF 1.3 ; uialign- 1]
               uifill 0.01
            ] [if $teamscoreboardcolor [uicolor 0xAA857000 0 0 [uiclamp 1 1 1 1]] [uicolor 0xBB700000 0 0 [uiclamp 1 1 1 1]]]
            UI_sbtc = 0
            loopscoreboard cn 1 [
               UI_sbtc = (! $UI_sbtc)
               uitablerow [
                  uifill 0.01
                  if $scoreboardmultiplayer [
                     if $showclientnum [uifill 0.055 0.04 [uicolortext (? (isai $cn) "^f4-" $cn)                  0xA0A0A0 0.62 ; uialign- 0]]
                     if $showping      [uifill 0.055 0.04 [uicolortext (? (isai $cn) "^f4-" (scoreboardping $cn)) 0x77A1D9 0.62 ; uialign- 0]]
                     if $showpj        [uifill 0.055 0.04 [uicolortext (? (isai $cn) "^f4-" (scoreboardpj $cn))   0x77A1D9 0.62 ; uialign- 0]]
                  ]
				  if $showlvl          [uifill 0.055 0.04 [uicolortext (getclientlevel $cn)                       0xCCCCCC 0.62 ; uialign- 0]]
                  if $showdeaths       [uifill 0.055 0.04 [uicolortext (getclientdeaths $cn)                      0xFF3333 0.62 ; uialign- 0]]
                  if (> $getmode 9)    [if $showscore [uifill 0.055 0.04 [uicolortext (getclientflags  $cn) 	  0x62CF70 0.62 ; uialign- 0]]]
				  if $showkills        [uifill 0.055 0.04 [uicolortext (getclientfrags  $cn)                      0xFFD711 0.62 ; uialign- 0]]
                  uifill (UI_cw_n) 0.026 [uihlist 0.005 [uispace 0.017 ; uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.57 ; uiimage (getclasslogo $cn -1) 0.03 0.03] ; uialign* 1]
				  
                  uifill 0.01
               ] [
                  uicolor (? $UI_sbtc 0x99333b40 0x99262b33) 0 0 [uiclamp 1 1 1 1]
                  pushif highlight (scoreboardhighlight $cn) [uioutline 0xFFFFFF (-f (*f $UI_sbwteam 0.54) 0.0) 0.038]
               ]
            ]
         ]
         uitable 0 0 [
            uialign 0 -1
            uitableheader [
               uifill 0.01
               uifill (UI_cw_n) 0.05 [uicolortext (getteamscore 2) 0xFFFFFF 1.3 ; uialign- -1]
			   if $showkills        [uifill 0.055 0.05 [uiimage "media/interface/hud/flingue.jpg"	 	0.03 0.03] ; uialign- 1 1]
			   if (> $getmode 9)    [if $showscore [uifill 0.055 0.05 [if (> $getmode 13) [uiimage "media/interface/hud/domination.png" 0.03 0.03][uiimage "media/interface/hud/drapeau_allie.png" 0.03 0.03]] ; uialign- 1 1]]
			   if $showdeaths       [uifill 0.055 0.05 [uiimage "media/interface/hud/dead.png"    		0.03 0.03] ; uialign- 1 1]
			   if $showlvl       	[uifill 0.055 0.05 [uiimage "media/interface/hud/stats.png"   		0.03 0.03] ; uialign- 1 1]
               if $scoreboardmultiplayer [
                  if $showpj        [uifill 0.055 0.05 [uicolortext "PJ"    	0x77A1D9 0.45 ; uialign- 1 1]]
                  if $showping      [uifill 0.055 0.05 [uiimage "media/interface/hud/ping.png"    0.03 0.03] ; uialign- -1 1]
                  if $showclientnum [uifill 0.055 0.05 [uicolortext "#"     	0xA0A0A0 0.45 ; uialign- 1 1]]
               ]
               uifill 0.01
            ] [if $teamscoreboardcolor [uicolor 0x99700000 0 0 [uiclamp 1 1 1 1]] [uicolor 0x99707000 0 0 [uiclamp 1 1 1 1]]]
            UI_sbtc = 0
            loopscoreboard cn 2 [
               UI_sbtc = (! $UI_sbtc)
               uitablerow [
                  uifill 0.01
                  uifill (UI_cw_n) 0.026 [uihlist 0.005 [uiimage (getclasslogo $cn -1) 0.03 0.03 ; uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.62 ; uispace 0.017] ; uialign* -1]
				  if $showkills        [uifill 0.055 0.04 [uicolortext (getclientfrags  $cn)                      0xFFD711 0.62 ; uialign-  0]]
                  if (> $getmode 9)    [if $showscore [uifill 0.055 0.04 [uicolortext (getclientflags  $cn)       0x62CF70 0.62 ; uialign-  0]]]
                  if $showdeaths       [uifill 0.055 0.04 [uicolortext (getclientdeaths $cn)                      0xFF3333 0.62 ; uialign-  0]]
				  if $showlvl          [uifill 0.055 0.04 [uicolortext (getclientlevel $cn)                       0xCCCCCC 0.62 ; uialign-  0]]
                  if $scoreboardmultiplayer [
                     if $showpj        [uifill 0.055 0.04 [uicolortext (? (isai $cn) "^f4-" (scoreboardpj $cn))   0x77A1D9 0.62 ; uialign-  0]]
                     if $showping      [uifill 0.055 0.04 [uicolortext (? (isai $cn) "^f4-" (scoreboardping $cn)) 0x77A1D9 0.62 ; uialign-  0]]
                     if $showclientnum [uifill 0.055 0.04 [uicolortext (? (isai $cn) "^f4-" $cn)                  0xA0A0A0 0.62 ; uialign-  0]]
                  ]
                  uifill 0.01
               ] [
                  uicolor (? $UI_sbtc 0x99403333 0x99332626) 0 0 [uiclamp 1 1 1 1]
                  pushif highlight (scoreboardhighlight $cn) [uioutline 0xFFFFFF (-f (*f $UI_sbwteam 0.54) 0.002) 0.03]
               ]
            ]
         ]
      ]
   ]
]
//-------------------------------------------------------------------------------------------------
UI_spectatorlist = [
    uiclamp 1 1 1 1
		if $showspectators [
			uispace 0 0.005
			uitext (at $Scoreboard_Spectators) 0.75
			
			uigrid (? (> $getmode 4) 3 2) 0 0 [
				loopscoreboard cn -1 [
					uispace 0.01 0.01 [
						pushif sbhigh (scoreboardhighlight $cn) [
						uioutline 0xA0A0A0 ; uiclamp- 1 1 1 1
					]
					uihlist 0.005 [
						uiimage "media/interface/hud/camera.png" 0.03 0.03
						uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.62
						if $scoreboardmultiplayer [if $showclientnum [uitext $cn 0.62]]
					]
				]
			]
		]
	]
]
