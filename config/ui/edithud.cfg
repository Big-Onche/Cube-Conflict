///////////////////////////////////////////////////////////////////////////////
//  Map editor menu                                                          //
///////////////////////////////////////////////////////////////////////////////

numPartsEffects = 8
numSoundEffects = 17

UI_window "mapeditor" "media/interface/hud/mapeditor.jpg" @Map_Editor_Menu_Title [
	uivgradient 0xEEEEEE 0xBBBBBB 0.9 0.5 [
		uivlist 0 [
			uivgradient 0x96B9D0 0x76A0CA 0.9 0.07 [
				uihlist 0.01 [
					uispace 0
					uiimage "media/interface/hud/info.jpg" 0.05 0.05
					uicolortext (at $Map_Editor_Menu_Subtitle) 0x2A2A2A 0.8
				]
				uialign* -1
				uiclamp 1 1 1 1
				uioutline 0x658FB9 ; uiclamp- 1 1 1 1
				
			]
			
			uihlist 0 [
				uiscroll 0.91 0.41 [
					uivlist 0.005 [
						loopfiles filename media/map ogz [
							if (!=s "base" $filename) [
								officialMap = (if ( || (=s $filename "village") (=s $filename "factory") (=s $filename "moon") (=s $filename "castle") (=s $filename "volcano")) [1][0])
								if (officialMap) [mapLogo = "media/interface/hud/serverfull.jpg"] [mapLogo = "media/interface/hud/server.jpg"]
								UI_Simple_Button (concatword (at $Map_Editor_Menu_Load) " " $filename ".ogz") $mapLogo [
									if (officialMap) [selectedMap = $filename ; showui "editwarning"][mode 1 ; map $filename ; hideallui ; edittoggle]
								] 
							]
						]	
					]	
				]
				UIvscroll 0.025 0.4 0.4
			]
			
			UIbar 1
			
			uispace 0 0.005
			UI_Pretty_Button $Map_Editor_Menu_NewMap [mode 1 ; map base ; hideallui ; edittoggle]
			uispace 0 0.005
		]
	]
] [hideui "main"] [if (! uivisible "editwarning") [showui "main"]]

UI_window "editwarning" "media/interface/hud/blip_red_dead.png" @Warning_Title [
	uivgradient 0xCCCCCC 0x999999 0.1 0.02 [
	   uivlist 0.02 [
			uispace 0.02
			uiwrapcolortext (at $Map_Editor_Menu_Confirmation) 0.8 0x000000 0.6
			uihlist 0 [
				UIbutton "hold5" [uiimage "media/interface/hud/checkbox_on.jpg" 0.03 0.03 ; uitext (at $Map_Editor_Menu_Confirmation_Yes) 0.6] 0.4 0.035 [mode 1 ; map $selectedMap ; hideallui ; edittoggle]
				UIbutton "hold5" [uiimage "media/interface/hud/checkbox_off.jpg" 0.03 0.03 ; uitext (at $Map_Editor_Menu_Confirmation_No) 0.6] 0.4 0.035 [hideui "editwarning" ; showui "mapeditor"]
			]
	    ]
	]
] [hideui "mapeditor"] [showui "mapeditor"]

///////////////////////////////////////////////////////////////////////////////
//  Edit HUD Elements                                                        //
///////////////////////////////////////////////////////////////////////////////
edittoggled = [
   if $editing [
      showui "editstats"
      showui "edithelp"
   ] [
      hideallui
   ]
]

///////////////////////////////////////////////////////////////////////////////
//  editstats				                                                 //
///////////////////////////////////////////////////////////////////////////////
newui "editstats" [
   uiallowinput 0
   if (! $hidehud) [
      uialign -1 1
      uiabovehud
      uicolor $c_menu_a 0 0 [
         uispace $UIwindow_padding $UIwindow_padding [
            uivlist 0 [
               uicontext (
                  format "%4^f7%5^f7gridsize :^f8 %1 ^f7| worldsize :^f8 %2 ^f7| floatspeed :^f8 %3" $gridpower $mapscale $floatspeed (
                     if $hmapedit [format "heightmap brush^f8 %1 ^f7:^f8 %2^n" $hbrushindex $hbrushname] [
                        if $blendpaintmode [
                           format "blendpaint mode : ^f8%1^f7^nblend brush : ^f8%2^n" (at $blendpaintmodes $blendpaintmode) (getblendbrushname (curblendbrush))
                        ] [if (! (enthavesel)) [
                           format "index^f8 %1 ^f7:^f8 %2^n" (max 0 (getseltex)) (gettexname (getseltex))
                        ]]
                     ]
                  ) (if (enthavesel) [concat (entget) ":^f8" (enthavesel) "^f7selected^n"])
               )
               uicontext (format "cube %1%2" $selchildcount (if $showmat [selchildmat ": "]))
               uihlist 0 [
                  uicontextfill 12 0 [uicontext (format "wtr:%1k(%2%%)" $editstatwtr $editstatvtr)    ; uialign- -1 0]
                  uicontextfill 12 0 [uicontext (format "wvt:%1k(%2%%)" $editstatwvt $editstatvvt)    ; uialign- -1 0]
                  uicontextfill 11 0 [uicontext (format "evt:%1k" $editstatevt)                       ; uialign- -1 0]
                  uicontextfill  7 0 [uicontext (format "eva:%1k" $editstateva)                       ; uialign- -1 0]
               ]
               uihlist 0 [
                  uicontextfill 12 0 [uicontext (concatword "ond:" $editstatocta)                     ; uialign- -1 0]
                  uicontextfill 12 0 [uicontext (concatword "va:" $editstatva)                        ; uialign- -1 0]
                  uicontextfill 11 0 [uicontext (format "gl:%1(%2)" $editstatglde $editstatgeombatch) ; uialign- -1 0]
                  uicontextfill  7 0 [uicontext (concatword "oq:" $editstatoq)                        ; uialign- -1 0]
                  uicontextfill  7 0 [uicontext (concatword "pvs:" $editstatpvs)                      ; uialign- -1 0]
               ]
               uialign* -1 0
            ]
         ]
         uioutline $c_line ; uiclamp- 1 1 1 1
      ]
   ]
]

///////////////////////////////////////////////////////////////////////////////
//  Help				                                                     //
///////////////////////////////////////////////////////////////////////////////
newui "edithelp" [
	uiallowinput 0
	if (! $hidehud) [
		uialign 1 0
		uicolor $c_menu_a 0 0 [
			uivlist 0.005 [
				uispace 0.0025
				UI_Edit_Help $Map_Editor_Help_OpenMenu 		"F1"	2
				UI_Edit_Help $Map_Editor_Help_AddEntity 	"F2"	2
				UI_Edit_Help $Map_Editor_Help_AddTexture 	"F3"	2
				UI_Edit_Help $Map_Editor_Help_AddModel 		"F4"	2
				UI_Edit_Help $Map_Editor_Help_EditMaterials "F5"	2
				UI_Edit_Help $Map_Editor_Help_SaveMap 		"F6"	2
				UI_Edit_Help $Map_Editor_Help_MapAtmosphere "F7"	2
				UI_Edit_Help $Map_Editor_Help_ViewKeyMap 	"F10"	2
				
				uicolor 0x777777 0.22 0 [
					uihlist	0 [
						uispace 0.005
						uitext (at $Map_Editor_Help_EditModes) 0.55
					]
				]
				
				UI_Edit_Help $Map_Editor_Help_AllFaces 		"6"		$allfaces
				UI_Edit_Help $Map_Editor_Help_EntSelSnap 	"7"		$entselsnap
				UI_Edit_Help $Map_Editor_Help_EntEditing 	"8"		$entediting
				UI_Edit_Help $Map_Editor_Help_FullBright 	"L"		$fullbright
				UI_Edit_Help $Map_Editor_Help_ShowMaterials "M"		$showmat
				uispace 0.0025
			]
		]
	]
]

///////////////////////////////////////////////////////////////////////////////
//  F1 Main edit menu        			                                     //
///////////////////////////////////////////////////////////////////////////////

menu_savemap = [if (! $mapname) [toggleui "savemap"] [savemap]]
UI_mapname = "untitled"

UImenu "savemap" [
   uispace 0.008 0.015 [

   ]
] [] [] [] 0 "SAVE MAP" "SAUVEGARDER LA MAP" "media/interface/hud/sauer.jpg"


newui "editmain" [
	uialign -1 -1
	uicolor $c_menu_a 0.522 1 [
		uihlist 0 [
			uispace 0.005
			uivlist 0.005 [
				uitext (at $Map_Editor_Menu_MapEditor) 0.8
				UIbar 1
				UIbutton "" [uitext (at $Map_Editor_Menu_Edittoggle) 0.55] 0.22 0.03 [edittoggle ; hideallui]
				
				uispace 0 0.02
				
				uitext (at $Map_Editor_Menu_MapManagement) 0.8
				UIbar 1
				
				uihlist 0 [
					uitext (at $Map_Editor_Menu_MapName) 0.6
					uispace 0.01
					uifield UI_mapname 20 [] 0.7 style_generic_focus
				]
				UIbutton "" [uitext (at $Map_Editor_Menu_SaveMap) 0.55] 0.22 0.03 [savemap $UI_mapname]
				UIbutton "" [uitext (at $Map_Editor_Menu_NewMap) 0.55] 0.22 0.03 [savemap $UI_mapname ; map base ; hideallui ; edittoggle]

				uispace 0 0.02
			
				uitext (at $Map_Editor_Menu_EditMenus) 0.8
				UIbar 1
			
				UIbutton "" [uitext (at $Map_Editor_Help_AddEntity) 0.55] 0.22 0.03 [showui "addentity"]
				UIbutton "" [uitext (at $Map_Editor_Help_AddTexture) 0.55] 0.22 0.03 [showui "texture_browser"]
				UIbutton "" [uitext (at $Map_Editor_Help_AddModel) 0.55] 0.22 0.03 [showui "mapmodel"]
				uialign* -1 -1
			]
		]
		uialign* -1
	]
] [
   hidetopui
]

///////////////////////////////////////////////////////////////////////////////
//  F2 Entities          			                                         //
///////////////////////////////////////////////////////////////////////////////
showEnvEnts = [
	uihlist 0 [
		uivlist 0.005 [
			uitext (at $Map_Editor_Menu_Lighting) 0.6
			UIbutton "" [uitext (at $Ent_Light) 0.6]    	0.3 0.03 [hideui "addentity" ; newent light 64 128 128 128 0 ; toggleeditent]	-1 [uihover [UI_enttype = "light ^f4*"]]
			UIbutton "" [uitext (at $Ent_Spotlight) 0.6] 	0.3 0.03 [hideui "addentity" ; newent spotlight 32 ; toggleeditent] 			-1 [uihover [UI_enttype = "spotlight ^f4*"]]
			uitext (at $Map_Editor_Menu_Reflections) 0.6
			UIbutton "" [uitext (at $Ent_Envmap) 0.6]    	0.3 0.03 [hideui "addentity" ; newent envmap ; toggleeditent] 					-1 [uihover [UI_enttype = "envmap"]]
			uitext (at $Map_Editor_Menu_Decorations) 0.6
			UIbutton "" [uitext (at $Ent_Mapmodel) 0.6]  	0.3 0.03 [hideui "addentity" ; showui "mapmodel"]     							-1 [uihover [UI_enttype = "mapmodel ^f4*"]]
			UIbutton "" [uitext (at $Ent_Particles) 0.6] 	0.3 0.03 [hideui "addentity" ; newent particles ; toggleeditent] 		    	-1 [uihover [UI_enttype = "particles ^f4*"]]
			UIbutton "" [uitext (at $Ent_Decal) 0.6] 		0.3 0.03 [hideui "addentity" ; newent decal ; toggleeditent] 					-1 [uihover [UI_enttype = "decal ^f4*"]]
			uitext (at $Map_Editor_Menu_Audio) 0.6
			UIbutton "" [uitext (at $Ent_Sound) 0.6] 		0.3 0.03 [hideui "addentity" ; newent sound 0 128 64 ; toggleeditent] 			-1 [uihover [UI_enttype = "sound ^f4*"]]
		]
	]	
]

showItemEnts = [
	uihlist 0 [
		uivlist 0.005 [
			uitext (at $Map_Editor_Menu_Items)  0.6
			UIbutton "" [uitext (at $Item_Health) 0.55]    		0.22 0.03 [hideui "addentity" ; newent health] 			-1 [uihover [UI_enttype = "health"]]
			UIbutton "" [uitext (at $Item_GrilledPig) 0.55]    	0.22 0.03 [hideui "addentity" ; newent healthboost] 	-1 [uihover [UI_enttype = "healthboost"]]
			UIbutton "" [uitext (at $Item_Mana) 0.55] 			0.22 0.03 [hideui "addentity" ; newent mana] 			-1 [uihover [UI_enttype = "mana"]]
			uitext (at $Map_Editor_Menu_Shields)  0.6
			UIbutton "" [uitext (at $Item_WoodShield) 0.55]    	0.22 0.03 [hideui "addentity" ; newent woodshield] 		-1 [uihover [UI_enttype = "woodshield"]]
			UIbutton "" [uitext (at $Item_IronShield) 0.55]   	0.22 0.03 [hideui "addentity" ; newent ironshield] 		-1 [uihover [UI_enttype = "ironshield"]]
			UIbutton "" [uitext (at $Item_GoldShield) 0.55] 	0.22 0.03 [hideui "addentity" ; newent goldshield] 		-1 [uihover [UI_enttype = "goldshield"]]
			UIbutton "" [uitext (at $Item_MagnetShield) 0.55]	0.22 0.03 [hideui "addentity" ; newent magnetshield] 	-1 [uihover [UI_enttype = "magnetshield"]]
			UIbutton "" [uitext (at $Item_PowerArmor) 0.55]    	0.22 0.03 [hideui "addentity" ; newent powerarmor] 		-1 [uihover [UI_enttype = "powerarmor"]]
			uitext (at $Map_Editor_Menu_Boosts)  0.6
			UIbutton "" [uitext (at $Item_Roids) 0.55]    		0.22 0.03 [hideui "addentity" ; newent roids] 			-1 [uihover [UI_enttype = "roids"]]
			UIbutton "" [uitext (at $Item_Shrooms) 0.55]   		0.22 0.03 [hideui "addentity" ; newent shrooms] 		-1 [uihover [UI_enttype = "shrooms"]]
			UIbutton "" [uitext (at $Item_Epo) 0.55] 			0.22 0.03 [hideui "addentity" ; newent epo] 			-1 [uihover [UI_enttype = "epo"]]
			UIbutton "" [uitext (at $Item_Joint) 0.55] 			0.22 0.03 [hideui "addentity" ; newent joint] 			-1 [uihover [UI_enttype = "joint"]]
		]
				
		uispace 0.01
				
		uivlist 0.005 [
			uitext (at $Map_Editor_Menu_Weapons)  0.6
			UIbutton "" [uitext (at $Weapon_ElectricRifle) 0.55]  	0.22 0.03 [hideui "addentity" ; newent electricrifle] 	-1 [uihover [UI_enttype = "electricrifle"]]
			UIbutton "" [uitext (at $Weapon_PlasmaRifle) 0.55] 		0.22 0.03 [hideui "addentity" ; newent plasmarifle] 	-1 [uihover [UI_enttype = "plasmarifle"]]
			UIbutton "" [uitext (at $Weapon_Smaw) 0.55]    			0.22 0.03 [hideui "addentity" ; newent smaw] 			-1 [uihover [UI_enttype = "smaw"]]
			UIbutton "" [uitext (at $Weapon_Minigun) 0.55]   		0.22 0.03 [hideui "addentity" ; newent minigun] 		-1 [uihover [UI_enttype = "minigun"]]
			UIbutton "" [uitext (at $Weapon_Spockgun) 0.55] 		0.22 0.03 [hideui "addentity" ; newent spockgun] 		-1 [uihover [UI_enttype = "spockgun"]]
			UIbutton "" [uitext (at $Weapon_M32) 0.55]				0.22 0.03 [hideui "addentity" ; newent m32] 			-1 [uihover [UI_enttype = "m32"]]
			UIbutton "" [uitext (at $Weapon_Flamethrower) 0.55]		0.22 0.03 [hideui "addentity" ; newent flamethrower] 	-1 [uihover [UI_enttype = "flamethrower"]]
			UIbutton "" [uitext (at $Weapon_Uzi) 0.55]    			0.22 0.03 [hideui "addentity" ; newent uzi] 			-1 [uihover [UI_enttype = "uzi"]]
			UIbutton "" [uitext (at $Weapon_Famas) 0.55]   			0.22 0.03 [hideui "addentity" ; newent famas] 			-1 [uihover [UI_enttype = "famas"]]
			UIbutton "" [uitext (at $Weapon_Mossberg500) 0.55] 		0.22 0.03 [hideui "addentity" ; newent mossberg500] 	-1 [uihover [UI_enttype = "mossberg500"]]
			UIbutton "" [uitext (at $Weapon_Hydra) 0.55] 			0.22 0.03 [hideui "addentity" ; newent hydra] 			-1 [uihover [UI_enttype = "hydra"]]
			UIbutton "" [uitext (at $Weapon_Sv98) 0.55]   			0.22 0.03 [hideui "addentity" ; newent sv98] 			-1 [uihover [UI_enttype = "sv98"]]
			UIbutton "" [uitext (at $Weapon_Sks) 0.55]    			0.22 0.03 [hideui "addentity" ; newent sks] 			-1 [uihover [UI_enttype = "sks"]]
			UIbutton "" [uitext (at $Weapon_Crossbow) 0.55]   		0.22 0.03 [hideui "addentity" ; newent crossbow] 		-1 [uihover [UI_enttype = "crossbow"]]
			UIbutton "" [uitext (at $Weapon_Ak47) 0.55] 			0.22 0.03 [hideui "addentity" ; newent ak47] 			-1 [uihover [UI_enttype = "ak47"]]
			UIbutton "" [uitext (at $Weapon_Gapb1) 0.55] 			0.22 0.03 [hideui "addentity" ; newent gapb1] 			-1 [uihover [UI_enttype = "gapb1"]]
			UIbutton "" [uitext (at $Weapon_Fireworks) 0.55] 		0.22 0.03 [hideui "addentity" ; newent fireworks] 		-1 [uihover [UI_enttype = "fireworks"]]
			UIbutton "" [uitext (at $Weapon_Glock) 0.55] 			0.22 0.03 [hideui "addentity" ; newent glock] 			-1 [uihover [UI_enttype = "glock"]]
			uitext (at $Map_Editor_Menu_Ent_Special)  0.6
			UIbutton "" [uitext (at $Weapon_Superweapon) 0.55] 		0.22 0.03 [hideui "addentity" ; newent superweapon] 	-1 [uihover [UI_enttype = "superweapon"]]
		]
	]	
]

showGameEnts = [
	uihlist 0 [
		uivlist 0.005 [
			uitext (at $Map_Editor_Menu_SinglePlayer) 0.6
			UIbutton "" [uitext (at $Ent_SpRespawnPoint) 0.6]	0.22 0.03 [hideui "addentity" ; newent respawnpoint]   			-1 [uihover [UI_enttype = "respawnpoint"]]
			UIbutton "" [uitext (at $Ent_Npc) 0.6]				0.22 0.03 [hideui "addentity" ; newent npc]   					-1 [uihover [UI_enttype = "npc"]]
			UIbutton "" [uitext (at $Ent_TriggerZone) 0.6]		0.22 0.03 [hideui "addentity" ; newent trigger]   				-1 [uihover [UI_enttype = "trigger"]]
		]
		
		uispace 0.01
		
		uivlist 0.005 [
			uitext (at $Map_Editor_Menu_RespawnPoints) 0.6
			UIbutton "" [uitext (at $Ent_RespawnNeutral) 0.6]	0.22 0.03 [hideui "addentity" ; newent playerstart]   			-1 [uihover [UI_enttype = "playerstart"]]
			UIbutton "" [uitext (at $Ent_RespawnTeam1) 0.6]	0.22 0.03 [hideui "addentity" ; newent playerstart 1] 			-1 [uihover [UI_enttype = "playerstart 1"]]
			UIbutton "" [uitext (at $Ent_RespawnTeam2) 0.6]	0.22 0.03 [hideui "addentity" ; newent playerstart 2] 			-1 [uihover [UI_enttype = "playerstart 2"]]
			uitext (at $Map_Editor_Menu_Movement) 0.6
			UIbutton "" [uitext (at $Ent_JumpPad) 0.6]			0.22 0.03 [hideui "addentity" ; showui "ent_jumppad"]   		-1 [uihover [UI_enttype = "jumppad ^f4*"]]
			UIbutton "" [uitext (at $Ent_Teleport) 0.6]		0.22 0.03 [hideui "addentity" ; showui "ent_teleport"]  		-1 [uihover [UI_enttype = "teleport ^f4*"]]
			UIbutton "" [uitext (at $Ent_Teledest) 0.6]		0.22 0.03 [hideui "addentity" ; showui "ent_teledest"]  		-1 [uihover [UI_enttype = "teledest ^f4*"]]
			uitext (at $Map_Editor_Menu_CaptureTheFlag) 0.6
			UIbutton "" [uitext (at $Ent_FlagTeam1) 0.6]		0.22 0.03 [hideui "addentity" ; newent flag 1]        			-1 [uihover [UI_enttype = "flag 1"]]
			UIbutton "" [uitext (at $Ent_FlagTeam2) 0.6]		0.22 0.03 [hideui "addentity" ; newent flag 2]        			-1 [uihover [UI_enttype = "flag 2"]]
			uitext (at $Map_Editor_Menu_Domination) 0.6
			UIbutton "" [uitext (at $Ent_Base) 0.6]			0.22 0.03 [hideui "addentity" ; newent base]        			-1 [uihover [UI_enttype = "base"]]
			uitext (at $Map_Editor_Menu_Miscellaneous) 0.6
			UIbutton "" [uitext (at $Ent_Camera) 0.6]			0.22 0.03 [hideui "addentity" ; newent camera]        			-1 [uihover [UI_enttype = "camera"]]
		]
	]
]

edit_ent_tab = 0
UI_enttype = ""
editTitle = ""

newui "addentity" [
	uialign -1 -1
	uicolor $c_menu_a 0.522 1 [
		uivlist 0 [
			uifill 0.5 0.032 [
				uitext (concatword "^f7/newent " $UI_enttype) 0.7
				uialign* -1 0
				uioutline $c_line ; uiclamp- 1 1 1 1
			]
			
			uispace 0 0.01
			
			UI_h_tab 0.5 0.05 edit_ent_tab [
				0 @(at $Map_Editor_EntTab_Environement) []
				1 @(at $Map_Editor_EntTab_GameItems) []
				2 @(at $Map_Editor_EntTab_GameMisc) []
			] 1 [["media/interface/hud/pixel.png" "media/interface/hud/flingue.jpg" "media/interface/hud/souris.png"]]
			
			uispace 0 0.01
			
			uifill 0.5 0.75 [
				case $edit_ent_tab 0 [showEnvEnts]
				case $edit_ent_tab 1 [showItemEnts]
				case $edit_ent_tab 2 [showGameEnts]
			]

		]
	]
] [
   hidetopui
]

showEditMapModel = [
	editTitle = (at $Map_Editor_EditMapModel)
	uihlist 0 [
		uispace 0.005
		uivlist 0.02 [
			uitext (at $Map_Editor_EditId) 0.62
			uitext (at $Map_Editor_EditYaw) 0.62
			uitext (at $Map_Editor_EditPitch) 0.62
			uitext (at $Map_Editor_EditRoll) 0.62
			uitext (at $Map_Editor_EditSize) 0.62
			uialign* -1
		]
		uispace 0.005
		uivlist 0.02 [
			UIhslider eattr0 0 $nummapmodels 1 0.65 0.02 [uitext ($eattr0) 0.6] [updateEntAttrs]
			UIhslider eattr1 -180 180 1 0.65 0.02 [uitext ($eattr1) 0.6] [updateEntAttrs]
			UIhslider eattr2 -180 180 1 0.65 0.02 [uitext ($eattr2) 0.6] [updateEntAttrs]
			UIhslider eattr3 -180 180 1 0.65 0.02 [uitext ($eattr3) 0.6] [updateEntAttrs]
			UIhslider eattr4 1 1000 1 0.65 0.02 [uitext ($eattr4) 0.6] [updateEntAttrs]
			uialign 1
		]
		uispace 0.005
	]
]

showEditLight = [
	editTitle = (at $Map_Editor_EditLight)
	uivlist 0 [
		uihlist 0 [
			uispace 0.005
			uivlist 0.02 [
				uitext (at $Map_Editor_EditRadius) 0.62
				uitext (at $Map_Editor_EditRed) 0.62
				uitext (at $Map_Editor_EditGreen) 0.62
				uitext (at $Map_Editor_EditBlue) 0.62
				uitext (at $Map_Editor_EditType) 0.62
				uitext (at $Map_Editor_EditWhen) 0.62
				uitext (at $Map_Editor_EditBlink) 0.62
				uialign* -1
			]
			
			uispace 0.005
			
			uivlist 0.02 [
				UIhslider eattr0 0 1024 1 0.65 0.02 [uitext ($eattr0) 0.6] [updateEntAttrs]
				UIhslider eattr1 0 512 1 0.65 0.02 [uitext ($eattr1) 0.6] [updateEntAttrs]
				UIhslider eattr2 0 512 1 0.65 0.02 [uitext ($eattr2) 0.6] [updateEntAttrs]
				UIhslider eattr3 0 512 1 0.65 0.02 [uitext ($eattr3) 0.6] [updateEntAttrs]
				UIhslider eattr4 0 7 1 0.65 0.02 [uitext (at $Map_Editor_LightTypesList $eattr4) 0.6] [updateEntAttrs]
				UIhslider eattr5 0 1 1 0.65 0.02 [uitext (at $Map_Editor_LightWhen $eattr5) 0.6] [updateEntAttrs]
				UIhslider eattr6 0 5000 1 0.65 0.02 [uitext $eattr6 0.6] [updateEntAttrs]
				uialign 1
			]
			uispace 0.005
		]
	]
]

showEditSpotlight = [
	editTitle = (at $Map_Editor_EditSpotlight)
	uihlist 0 [
		uispace 0.005
		uivlist 0.02 [
			uitext (at $Map_Editor_EditAperture) 0.62
			uialign* -1
		]
		uispace 0.005
		uivlist 0.02 [
			UIhslider eattr0 0 90 1 0.65 0.02 [uitext (* $eattr0 2) 0.6] [updateEntAttrs]
			uialign 1
		]
		uispace 0.005
	]
]

showEditEnvMap = [
	editTitle = (at $Map_Editor_EditEnvMap)
	uihlist 0 [
		uispace 0.005
		uivlist 0.02 [
			uitext (at $Map_Editor_EditRadius) 0.62
			uialign* -1
		]
		uispace 0.005
		uivlist 0.02 [
			UIhslider eattr0 1 512 1 0.65 0.02 [uitext (* $eattr0 2) 0.6] [updateEntAttrs]
			uialign 1
		]
		uispace 0.005
	]
]


showEditParticles = [
	editTitle = (at $Map_Editor_EditParticles)
	getParticlesAttrs
	uihlist 0 [
		uispace 0.005
		uivlist 0.01 [
			uitext (at $Map_Editor_EditType) 0.55 // particle type (always the same)
			
			uitext $attr1text 0.55
			if (!=s $attr2text "") [uitext $attr2text 0.55]

			if (!= $eattr0 7) [
				uitext (at $Map_Editor_EditRed) 0.55 // r g b (always the same)
				uitext (at $Map_Editor_EditGreen) 0.55
				uitext (at $Map_Editor_EditBlue) 0.55
			]
			
			if (!=s $attr6text "") [uitext $attr6text 0.55]
			if (!=s $attr7text "") [uitext $attr7text 0.55]
			if (!=s $attr8text "") [uitext $attr8text 0.55]
			uialign* 1
		]
		uispace 0.005
		uivlist 0.009 [
			UIhslider eattr0 0 $numPartsEffects 1 0.65 0.02 [uitext (at $Map_Editor_PartsTypesList $eattr0) 0.5] [updateEntAttrs]  // particle type (always the same)
			
			UIhslider eattr1 $attr1min $attr1max 1 0.65 0.02 [
				if (textval) [
					case $eattr0 0 [uitext (at $Map_Editor_WeatherTypesList $eattr1) 0.5]
					case $eattr0 4 [uitext (at $Map_Editor_PartDirList $eattr1) 0.5]
				][uitext ($eattr1) 0.5]
			][updateEntAttrs]
				
			if (!=s $attr2text "") [UIhslider eattr2 $attr2min $attr2max 1 0.65 0.02 	[uitext ($eattr2) 0.5]	[updateEntAttrs]]

			if (!= $eattr0 7) [
				UIhslider eattr3 0 255 1 0.65 0.02 										[uitext ($eattr3) 0.5]	[updateEntAttrs]  // r g b (always the same)
				UIhslider eattr4 0 255 1 0.65 0.02 										[uitext ($eattr4) 0.5]	[updateEntAttrs]
				UIhslider eattr5 0 255 1 0.65 0.02 										[uitext ($eattr5) 0.5]	[updateEntAttrs]
			]
			
			if (!=s $attr6text "") [UIhslider eattr6 $attr6min $attr6max 1 0.65 0.02 	[uitext ($eattr6) 0.5]	[updateEntAttrs]]
			if (!=s $attr7text "") [UIhslider eattr7 $attr7min $attr7max 1 0.65 0.02 	[uitext ($eattr7) 0.5] 	[updateEntAttrs]]
			if (!=s $attr8text "") [UIhslider eattr8 $attr8min $attr8max 1 0.65 0.02	[uitext ($eattr8) 0.5] 	[updateEntAttrs]]
			uialign 1
		]
		uispace 0.005
	]
]

decalTypes = ["Puddle 1" "Puddle 2" "Puddle 3" "Radioactivity sign" "Overhead load sign" "Rotating parts sign" "Hot temperature sign" "I want you" "Menus 1"]

showEditDecal = [
	editTitle = (at $Map_Editor_EditDecal)
	uihlist 0 [
		uispace 0.005
		uivlist 0.02 [
			uitext (at $Map_Editor_EditType) 0.62
			uitext (at $Map_Editor_EditYaw) 0.62
			uitext (at $Map_Editor_EditPitch) 0.62
			uitext (at $Map_Editor_EditRoll) 0.62
			uitext (at $Map_Editor_EditSize) 0.62
			uitext (at $Map_Editor_EditWhen) 0.62
			uialign* -1
		]
		uispace 0.005
		uivlist 0.02 [
			UIhslider eattr0 0 8 1 0.65 0.02 [uitext (at $decalTypes $eattr0) 0.6] [updateEntAttrs]
			UIhslider eattr1 -180 180 1 0.65 0.02 [uitext ($eattr1) 0.6] [updateEntAttrs]
			UIhslider eattr2 -180 180 1 0.65 0.02 [uitext ($eattr2) 0.6] [updateEntAttrs]
			UIhslider eattr3 -180 180 1 0.65 0.02 [uitext ($eattr3) 0.6] [updateEntAttrs]
			UIhslider eattr4 1 1000 1 0.65 0.02 [uitext ($eattr4) 0.6] [updateEntAttrs]
			UIhslider eattr5 0 1 1 0.65 0.02 [uitext (at $Map_Editor_EditWeatherDecals $eattr1) 0.6] [updateEntAttrs]
			uialign 1
		]
		uispace 0.005
	]
]

oldradius = 0

showEditSound = [
	editTitle = (at $Map_Editor_EditSound)
	uihlist 0 [
		uispace 0.005
		uivlist 0.02 [
			uitext (at $Map_Editor_EditType) 0.62
			uitext (at $Map_Editor_EditRadius) 0.62
			uitext (at $Map_Editor_EditMaxVolRadius) 0.62
			uialign* -1
		]
		uispace 0.005
		uivlist 0.02 [
			UIhslider eattr0 0 $numSoundEffects 1 0.65 0.02 [uitext (at $Map_Editor_SoundTypesList $eattr0) 0.6] [updateEntAttrs]
			UIhslider eattr1 1 1024 1 0.65 0.02 [uitext ($eattr1) 0.6] [updateEntAttrs]
			UIhslider eattr2 1 1024 1 0.65 0.02 [uitext ($eattr2) 0.6] [updateEntAttrs]
			UIbutton "" [uitext (at $Map_Editor_EditUpdateSound) 0.55] 0.22 0.03 [stopmapsounds] 
			uialign 1
		]
		uispace 0.005
	]
]

newui "editent" [
	uiallowinput 1
	uialign 0 1
	uicolor $c_menu_a 0 0 [
		uivlist 0.005 [
			uispace 0.005
			uihgradient 0x6F6F6F $c_menu_a 0.85 0 [
				uihlist 0 [
					uispace 0.005
					uiimage "media/interface/hud/options.jpg" 0.04 0.04
					uispace 0.005
					uitext $editTitle 0.7
				]
				uialign* -1
				
				uihlist 0 [
					uitarget 0.05 0.05 [
						uihover [uicolor 0x999977 0.05 0.035] [uicolor 0xBBBB99 0.05 0.035]
						uiimage "media/interface/hud/checkbox_on.jpg" 0.026 0.026
						uihold [uiimage "media/interface/hud/checkbox_on.jpg" 0.032 0.032] ; uirelease [hideui "editent"]
					]
					uispace 0.005
				]
				uialign- 1
			]
			
			UIbar 1
			if (=s $etype "mapmodel") 	[showEditMapModel]
			if (=s $etype "light") 	  	[showEditLight]
			if (=s $etype "spotlight")  [showEditSpotlight]
			if (=s $etype "envmap")  	[showEditEnvMap]
			if (=s $etype "particles")  [showEditParticles]
			if (=s $etype "decal")  	[showEditDecal]
			if (=s $etype "sound")  	[showEditSound]
			uispace 0.005
		]
	]
]

///////////////////////////////////////////////////////////////////////////////
//  F3 Texture Browser                                                       //
///////////////////////////////////////////////////////////////////////////////
UI_textabs = 0

newui "texture_browser" [
	uialign -1 -1
	uieschide 0
	uicolor $c_menu_a 0 0 [
		uivlist 0 [
			uihlist 0 [
				UIvscroll 0.02 1 3.0225
				uispace 0.002
				uiscroll 0.5 1 [
					uifill 0.5 1 [
						uigrid 6 0.0038 0.0038 [
							loop slot (numslots) [
								uislotview $slot 0.08 0.08 [
									uihold [uioutline 0xC0C0C0; uiclamp- 1 1 1 1] [uihover [uioutline 0xFFFFFF; uiclamp- 1 1 1 1]]
									uirelease [settex (getslottex $slot)]
								]
							]
						]
						uialign* -1 -1
					]
				]
				uispace 0.002
			]
			//UI_Check_Button $Map_Editor_Button_AllFaces [allfaces (! $allfaces)] $allfaces
		]
	]
] [
   hidetopui
]

///////////////////////////////////////////////////////////////////////////////
//  F4 Mapmodel                                                              //
///////////////////////////////////////////////////////////////////////////////
newui "mapmodel" [
   uialign -1 -1
   uieschide 0
   uicolor $c_menu_a 0 0 [
      uispace $UIwindow_padding $UIwindow_padding [
         uiclamp 1 1 1 1
         uihlist 0.01 [
            if 0 [
               UIvscroll 0.02 1 (*f (min (+ 1 (div $nummapmodels 100)) 10) 5.4)
            ] [
               UIvscroll 0.02 1 (*f (min (+ 1 (div $nummapmodels 50)) 5) 2.7)
            ]
            uiscroll 0.5 1 [
               uivlist 0 [
                  uigrid 4 0 0 [// (+ $_res_wide 1)
                     loop i $nummapmodels [
                        uigroup [
                           uimodelpreview (mapmodelname $i 1) "mapmodel" 0.122 0.122 [
                              uihover [
                                 uihold [uioutline 0xFFFFFF] [
                                    uieschold [uioutline 0xFFFFFF] [
                                       uioutline 0x80FFFFFF
                                    ]
                                 ]
                                 uiclamp* 1 1 1 1
                                 UI_selmdl = $i
                                 uirelease [
                                    newent mapmodel $i
                                    _io_mapmodel = 0
									hideui "mapmodel"
									hideui "addentity"
									toggleeditent
                                 ]
                                 uiescrelease [
                                    if (enthavesel) [
                                       entset "mapmodel" $i
                                       _io_mapmodel = 0
                                    ]
                                 ]
                              ] [
                                 if (= $UI_selmdl $i) [
                                    uioutline 0x40FFFFFF
                                    uiclamp- 1 1 1 1
                                 ]
                              ]
                           ]
                           uispace 0.01 0.006 [
                              uialign -1 -1
                              uicolortext $i 0x80FFFFFF 0.6
                           ]
                        ]
                     ]
                  ]
               ]
            ]
         ]
      ]
      uioutline $c_line ; uiclamp- 1 1 1 1
   ]
   uiclamp* 1 1 1 1
] [
   UI_selmdl = -1
   hidetopui
]

scatterType = 124
scatterSize = 100

scatter = [
	newent mapmodel 124 (rand 360) (rand 360) (rand 360) (+ 150 (rand 250))
]

///////////////////////////////////////////////////////////////////////////////
//  F5 Material Volume                                                       //
///////////////////////////////////////////////////////////////////////////////
newui "material" [
	uialign -1 -1
	uicolor $c_menu_a 0.522 1 [
		uispace 0.008 0.015 [
			uivlist 0 [
				uifill 0.5 0.032 [
					uitext (concatword "^f8/" $UI_mattype) 0.7
					uialign* -1 0
					uioutline $c_line ; uiclamp- 1 1 1 1
				]
				
				uispace 0 0.02
			 
				uitext (at $Map_Editor_Menu_Surfaces) 0.8
				UIbar 1
				uispace 0 0.005
				
				uihlist 0.005 [
					uivlist 0.005 [
						UIbutton "" [uitext (at $Map_Editor_Menu_Air) 0.6]		0.2 0.04 [editmat air]      -1 [uihover [UI_mattype = "air"]]
						UIbutton "" [uitext (at $Map_Editor_Menu_Clip) 0.6]		0.2 0.04 [editmat clip]     -1 [uihover [UI_mattype = "clip"]]
						UIbutton "" [uitext (at $Map_Editor_Menu_NoClip) 0.6]	0.2 0.04 [editmat noclip]   -1 [uihover [UI_mattype = "noclip"]]
						UIbutton "" [uitext (at $Map_Editor_Menu_GameClip) 0.6]	0.2 0.04 [editmat gameclip] -1 [uihover [UI_mattype = "gameclip"]]
						UIbutton "" [uitext (at $Map_Editor_Menu_Death) 0.6]	0.2 0.04 [editmat death]    -1 [uihover [UI_mattype = "death"]]
						UIbutton "" [uitext (at $Map_Editor_Menu_Alpha) 0.6]	0.2 0.04 [editmat alpha]    -1 [uihover [UI_mattype = "alpha"]]
					]
					uivlist 0.005 [
						UIbutton "" [uitext (concatword (at $Map_Editor_Menu_Glass) " 1") 0.6]     0.2 0.04 [editmat glass]    -1 [uihover [UI_mattype = "glass"]]
						UIbutton "" [uitext (concatword (at $Map_Editor_Menu_Glass) " 2") 0.6]     0.2 0.04 [editmat glass2]   -1 [uihover [UI_mattype = "glass2"]]
						UIbutton "" [uitext (concatword (at $Map_Editor_Menu_Glass) " 3") 0.6]     0.2 0.04 [editmat glass3]   -1 [uihover [UI_mattype = "glass3"]]
						UIbutton "" [uitext (concatword (at $Map_Editor_Menu_Glass) " 4") 0.6]     0.2 0.04 [editmat glass4]   -1 [uihover [UI_mattype = "glass4"]]
					]
				]
			
				uispace 0 0.02
			
				uitext (at $Map_Editor_Menu_Liquids) 0.8
				UIbar 1
				uispace 0 0.005
			
				uihlist 0.005 [
					uivlist 0.005 [
						UIbutton "" [uitext (concatword (at $Map_Editor_Menu_Lava) " 1") 0.6]      0.2 0.04 [editmat lava]     -1 [uihover [UI_mattype = "lava"]]
						UIbutton "" [uitext (concatword (at $Map_Editor_Menu_Lava) " 2") 0.6]      0.2 0.04 [editmat lava2]    -1 [uihover [UI_mattype = "lava2"]]
						UIbutton "" [uitext (concatword (at $Map_Editor_Menu_Lava) " 3") 0.6]      0.2 0.04 [editmat lava3]    -1 [uihover [UI_mattype = "lava3"]]
						UIbutton "" [uitext (concatword (at $Map_Editor_Menu_Lava) " 4") 0.6]      0.2 0.04 [editmat lava4]    -1 [uihover [UI_mattype = "lava4"]]
					]
					uivlist 0.005 [
						UIbutton "" [uitext (concatword (at $Map_Editor_Menu_Water) " 1") 0.6]     0.2 0.04 [editmat water]    -1 [uihover [UI_mattype = "water"]]
						UIbutton "" [uitext (concatword (at $Map_Editor_Menu_Water) " 2") 0.6]     0.2 0.04 [editmat water2]   -1 [uihover [UI_mattype = "water2"]]
						UIbutton "" [uitext (concatword (at $Map_Editor_Menu_Water) " 3") 0.6]     0.2 0.04 [editmat water3]   -1 [uihover [UI_mattype = "water3"]]
						UIbutton "" [uitext (concatword (at $Map_Editor_Menu_Water) " 4") 0.6]     0.2 0.04 [editmat water4]   -1 [uihover [UI_mattype = "water4"]]
					]
				]
				
				uispace 0 0.02
				
				UIbutton "hold5" [
					UIcheckbox $showmat 0.016
					uitext (at $Map_Editor_Menu_ShowMatVol) 0.6
					UI_keyref 2 0.027 0.027 "M"
				] 0.33 0.04 [showmat (! $showmat)]
			]
		]
	]
] [
   hidetopui
]