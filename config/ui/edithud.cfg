///////////////////////////////////////////////////////////////////////////////
//  Edit HUD Elements                                                        //
///////////////////////////////////////////////////////////////////////////////
edittoggled = [
   if $editing [
      showui "editstats"
      showui "edithelp"
   ] [
      hideallui
   ]
]

///////////////////////////////////////////////////////////////////////////////
//  editstats				                                                 //
///////////////////////////////////////////////////////////////////////////////
newui "editstats" [
   uiallowinput 0
   if $hidehud [] [
      uialign -1 1
      uiabovehud
      uicolor $c_menu_a 0 0 [
         uispace $UIwindow_padding $UIwindow_padding [
            uivlist 0 [
               uicontext (
                  format "%4^f7%5^f7gridsize :^f8 %1 ^f7| worldsize :^f8 %2 ^f7| floatspeed :^f8 %3" $gridpower $mapscale $floatspeed (
                     if $hmapedit [format "heightmap brush^f8 %1 ^f7:^f8 %2^n" $hbrushindex $hbrushname] [
                        if $blendpaintmode [
                           format "blendpaint mode : ^f8%1^f7^nblend brush : ^f8%2^n" (at $blendpaintmodes $blendpaintmode) (getblendbrushname (curblendbrush))
                        ] [if (! (enthavesel)) [
                           format "index^f8 %1 ^f7:^f8 %2^n" (max 0 (getseltex)) (gettexname (getseltex))
                        ]]
                     ]
                  ) (if (enthavesel) [concat (entget) ":^f8" (enthavesel) "^f7selected^n"])
               )
               uicontext (format "cube %1%2" $selchildcount (if $showmat [selchildmat ": "]))
               uihlist 0 [
                  uicontextfill 12 0 [uicontext (format "wtr:%1k(%2%%)" $editstatwtr $editstatvtr)    ; uialign- -1 0]
                  uicontextfill 12 0 [uicontext (format "wvt:%1k(%2%%)" $editstatwvt $editstatvvt)    ; uialign- -1 0]
                  uicontextfill 11 0 [uicontext (format "evt:%1k" $editstatevt)                       ; uialign- -1 0]
                  uicontextfill  7 0 [uicontext (format "eva:%1k" $editstateva)                       ; uialign- -1 0]
               ]
               uihlist 0 [
                  uicontextfill 12 0 [uicontext (concatword "ond:" $editstatocta)                     ; uialign- -1 0]
                  uicontextfill 12 0 [uicontext (concatword "va:" $editstatva)                        ; uialign- -1 0]
                  uicontextfill 11 0 [uicontext (format "gl:%1(%2)" $editstatglde $editstatgeombatch) ; uialign- -1 0]
                  uicontextfill  7 0 [uicontext (concatword "oq:" $editstatoq)                        ; uialign- -1 0]
                  uicontextfill  7 0 [uicontext (concatword "pvs:" $editstatpvs)                      ; uialign- -1 0]
               ]
               uialign* -1 0
            ]
         ]
         uioutline $c_line ; uiclamp- 1 1 1 1
      ]
   ]
]

///////////////////////////////////////////////////////////////////////////////
//  Help				                                                     //
///////////////////////////////////////////////////////////////////////////////

newui "edithelp" [
	uiallowinput 0
	if $hidehud [] [
		uialign 1 0
		uicolor $c_menu_a 0 0 [
			uivlist 0.005 [
				uispace 0.0025
				UI_Edit_Help $Map_Editor_Help_Exit 			"F1"	2
				UI_Edit_Help $Map_Editor_Help_AddEntity 	"F2"	2
				UI_Edit_Help $Map_Editor_Help_AddTexture 	"F3"	2
				UI_Edit_Help $Map_Editor_Help_AddModel 		"F4"	2
				UI_Edit_Help $Map_Editor_Help_EditMaterials "F5"	2
				UI_Edit_Help $Map_Editor_Help_SaveMap 		"F6"	2
				UI_Edit_Help $Map_Editor_Help_MapAtmosphere "F7"	2
				UI_Edit_Help $Map_Editor_Help_ViewKeyMap 	"F10"	2
				
				uicolor 0x777777 0.22 0 [
					uihlist	0 [
						uispace 0.005
						uitext (at $Map_Editor_Help_EditModes $GAME_LANG) 0.55
					]
				]
				
				UI_Edit_Help $Map_Editor_Help_AllFaces 		"6"		$allfaces
				UI_Edit_Help $Map_Editor_Help_EntSelSnap 	"7"		$entselsnap
				UI_Edit_Help $Map_Editor_Help_EntEditing 	"8"		$entediting
				UI_Edit_Help $Map_Editor_Help_FullBright 	"L"		$fullbright
				UI_Edit_Help $Map_Editor_Help_ShowMaterials "M"		$showmat
				uispace 0.0025
			]
		]
	]
]

///////////////////////////////////////////////////////////////////////////////
//  F3 Texture Browser                                                       //
///////////////////////////////////////////////////////////////////////////////
UI_textabs = 0

newui "texture_browser" [
	uialign -1 -1
	uieschide 0
	uicolor $c_menu_a 0 0 [
		uivlist 0 [
			uihlist 0 [
				UIvscroll 0.02 1 3.0225
				uispace 0.002
				uiscroll 0.5 1 [
					uifill 0.5 1 [
						uigrid 6 0.0038 0.0038 [
							loop slot (numslots) [
								uislotview $slot 0.08 0.08 [
									uihold [uioutline 0xC0C0C0; uiclamp- 1 1 1 1] [uihover [uioutline 0xFFFFFF; uiclamp- 1 1 1 1]]
									uirelease [settex (getslottex $slot)]
								]
							]
						]
						uialign* -1 -1
					]
				]
				uispace 0.002
			]
			//UI_Check_Button $Map_Editor_Button_AllFaces [allfaces (! $allfaces)] $allfaces
		]
	]
] [
   hideui "mapmodel"
   hideui "addentity"
]

///////////////////////////////////////////////////////////////////////////////
//  F4 Mapmodel                                                              //
///////////////////////////////////////////////////////////////////////////////
newui "mapmodel" [
   uialign -1 -1
   uieschide 0
   uicolor $c_menu_a 0 0 [
      uispace $UIwindow_padding $UIwindow_padding [
         uiclamp 1 1 1 1
         uihlist 0.01 [
            if 0 [
               UIvscroll 0.02 1 (*f (min (+ 1 (div $nummapmodels 100)) 10) 5.4)
            ] [
               UIvscroll 0.02 1 (*f (min (+ 1 (div $nummapmodels 50)) 5) 2.7)
            ]
            uiscroll 0.5 1 [
               uivlist 0 [
                  uigrid 4 0 0 [// (+ $_res_wide 1)
                     loop i $nummapmodels [
                        uigroup [
                           uimodelpreview (mapmodelname $i 1) "mapmodel" 0.122 0.122 [
                              uihover [
                                 uihold [uioutline 0xFFFFFF] [
                                    uieschold [uioutline 0xFFFFFF] [
                                       uioutline 0x80FFFFFF
                                    ]
                                 ]
                                 uiclamp* 1 1 1 1
                                 UI_selmdl = $i
                                 uirelease [
                                    newent mapmodel $i
                                    _io_mapmodel = 0
                                 ]
                                 uiescrelease [
                                    if (enthavesel) [
                                       entset "mapmodel" $i
                                       _io_mapmodel = 0
                                    ]
                                 ]
                              ] [
                                 if (= $UI_selmdl $i) [
                                    uioutline 0x40FFFFFF
                                    uiclamp- 1 1 1 1
                                 ]
                              ]
                           ]
                           uispace 0.01 0.006 [
                              uialign -1 -1
                              uicolortext $i 0x80FFFFFF 0.6
                           ]
                        ]
                     ]
                  ]
               ]
            ]
         ]
      ]
      uioutline $c_line ; uiclamp- 1 1 1 1
   ]
   uiclamp* 1 1 1 1
] [
   UI_selmdl = -1
   hideui "texture_browser"
   hideui "addentity"
]


UImenu "addentity" [
   uispace 0.008 0.015 [
      uivlist 0.005 [
         uifill 0.5 0.032 [
            uitext (concatword "^f8/newent " $UI_enttype) 0.7
            uialign* -1 0
            uioutline $c_line ; uiclamp- 1 1 1 1
         ]
         uifill 0 0.025
         uihlist 0.005 [
            uivlist 0.005 [
               UIbutton "" [uitext "Envmap" 0.6]    			0.22 0.03 [hideui "addentity" ; newent envmap] 					-1 [uihover [UI_enttype = "envmap"]]
               UIbutton "" [uitext "Light" 0.6]    			 	0.22 0.03 [hideui "addentity" ; showui "ent_light"]     		-1 [uihover [UI_enttype = "light ^f4*"]]
               UIbutton "" [uitext "Spotlight" 0.6] 			0.22 0.03 [hideui "addentity" ; showui "ent_spotlight"] 		-1 [uihover [UI_enttype = "spotlight ^f4*"]]
            ]
            uivlist 0.005 [
               UIbutton "" [uitext "Jumppad" 0.6]   			0.22 0.03 [hideui "addentity" ; showui "ent_jumppad"]   		-1 [uihover [UI_enttype = "jumppad ^f4*"]]
               UIbutton "" [uitext "Mapmodel [F3]" 0.6]  		0.22 0.03 [hideui "addentity" ; showui "mapmodel"]     			-1 [uihover [UI_enttype = "mapmodel ^f4*"]]
               UIbutton "" [uitext "Particles" 0.6] 			0.22 0.03 [hideui "addentity" ; showui "ent_particles"] 		-1 [uihover [UI_enttype = "particles ^f4*"]]
               UIbutton "" [uitext "Teleport" 0.6]  			0.22 0.03 [hideui "addentity" ; showui "ent_teleport"]  		-1 [uihover [UI_enttype = "teleport ^f4*"]]
               UIbutton "" [uitext "Teledest" 0.6]  			0.22 0.03 [hideui "addentity" ; showui "ent_teledest"]  		-1 [uihover [UI_enttype = "teledest ^f4*"]]
            ]
            uivlist 0.005 [
               UIbutton "" [uitext "Playerstart" 0.6]        	0.22 0.03 [hideui "addentity" ; newent playerstart]   			-1 [uihover [UI_enttype = "playerstart"]]
               UIbutton "" [uitext "Playerstart ^fdYRR" 0.6] 	0.22 0.03 [hideui "addentity" ; newent playerstart 1] 			-1 [uihover [UI_enttype = "playerstart 1"]]
               UIbutton "" [uitext "Playerstart ^fcYKK" 0.6] 	0.22 0.03 [hideui "addentity" ; newent playerstart 2] 			-1 [uihover [UI_enttype = "playerstart 2"]]
               UIbutton "" [uitext "Flag ^fdYRR" 0.6]        	0.22 0.03 [hideui "addentity" ; newent flag 1]        			-1 [uihover [UI_enttype = "flag 1"]]
               UIbutton "" [uitext "Flag ^fcYKK" 0.6]        	0.22 0.03 [hideui "addentity" ; newent flag 2]        			-1 [uihover [UI_enttype = "flag 2"]]
            ]
            uialign* -1 -1
         ]

		 UIbar 1 0 //---------------------------------------------------------------------------------------------------------------------------------------------
		 
         uihlist 0.005 [
            uivlist 0.005 [
			   uitext "Items:" 0.6
               UIbutton "" [uitext "Health [25]" 0.6]    		0.22 0.03 [hideui "addentity" ; newent health] 					-1 [uihover [UI_enttype = "health"]]
               UIbutton "" [uitext "Health [50]" 0.6]    		0.22 0.03 [hideui "addentity" ; newent healthboost] 			-1 [uihover [UI_enttype = "healthboost"]]
               UIbutton "" [uitext "Mana [25]" 0.6] 			0.22 0.03 [hideui "addentity" ; newent mana] 					-1 [uihover [UI_enttype = "mana"]]
			   uitext "Shields:" 0.6
               UIbutton "" [uitext "Wood_shield[75]" 0.6]    	0.22 0.03 [hideui "addentity" ; newent woodshield] 				-1 [uihover [UI_enttype = "woodshield"]]
               UIbutton "" [uitext "Iron_shield[125]" 0.6]    	0.22 0.03 [hideui "addentity" ; newent ironshield] 				-1 [uihover [UI_enttype = "ironshield"]]
               UIbutton "" [uitext "Gold_shield[200]" 0.6] 		0.22 0.03 [hideui "addentity" ; newent goldshield] 				-1 [uihover [UI_enttype = "goldshield"]]
               UIbutton "" [uitext "Magnet_shield[150]" 0.6]    0.22 0.03 [hideui "addentity" ; newent magnetshield] 			-1 [uihover [UI_enttype = "magnetshield"]]
               UIbutton "" [uitext "Power_armor[300]" 0.6]    	0.22 0.03 [hideui "addentity" ; newent powerarmor] 				-1 [uihover [UI_enttype = "powerarmor"]]
			   uitext "Boosts:" 0.6
			   UIbutton "" [uitext "Roids[Damage]" 0.6]    		0.22 0.03 [hideui "addentity" ; newent roids] 					-1 [uihover [UI_enttype = "roids"]]
               UIbutton "" [uitext "Shrooms[Cadency]" 0.6]    	0.22 0.03 [hideui "addentity" ; newent shrooms] 				-1 [uihover [UI_enttype = "shrooms"]]
               UIbutton "" [uitext "Epo[Speed]" 0.6] 			0.22 0.03 [hideui "addentity" ; newent epo] 					-1 [uihover [UI_enttype = "epo"]]
               UIbutton "" [uitext "Joint[Resistance]" 0.6]    	0.22 0.03 [hideui "addentity" ; newent joint] 					-1 [uihover [UI_enttype = "joint"]]
            ]
            uivlist 0.005 [

            ]
            uivlist 0.005 [

            ]
            uialign* -1 -1
         ]
         uifill 0 0.01
         UIbutton "hold2" [
            UIcheckbox $entselsnap 0.016
            uitext "Entity Snapping" 0.6
            UI_keyref 2 0.027 0.027 "7"
         ] 0.25 0.04 [entselsnap (! $entselsnap)]
      ]
   ]
   uialign* -1 -1
] [
   UI_enttype = " "
   looplist a ["editentity" "ent_light" "ent_spotlight" "ent_jumppad" "mapmodel" "ent_particles" "ent_teleport" "ent_teledest"] [hideui $a]
] [] [] 0 "ADD ENTITY" "AJOUTER UNE ENTITÃ‰" "media/interface/hud/sauer.jpg"