// ui*   hardcoded ui functions
// UI_*  aliased ui functions

// UI_window 1:"NAME" 2:"ICO" 3 @TITLE 4:[content] 5:[on-show] 6:[on-hide] 7:[is-popup?]
UI_window = [
	newui $arg1 [
		uivlist 0 [
			uiclamp 1 1 1 1
			uivgradient 0x605A5A 0x484444 0 0.04 [  // window title and close button
				uiclamp 1 1 1 1
				uispace 0.003 0.003 [
					uiclamp 1 1
					uihlist 0 [
						uiimage @@@@@arg2 0.045 0.045
						uispace 0.007
						uicolortext @@@@@arg3 0xFFFFFF 0.7
					]
					uialign* -1 0
					if (= @@@@arg7 0) [
						uitarget 0.05 0.05 [
							uihover [uicolor 0xFF0000 0.04 0.03] [uicolor 0xCC0000 0.04 0.03]
							uiimage "media/interface/hud/exit.png" 0.026 0.026
							uigroup [uiimage "media/interface/hud/exit.png" 0.028 0.028] ; uirelease [hidetopui]
						]
						uialign- 1
					]
				]
				uioutline $c_line ; uiclamp- 1 1 1 1
			]
			uicolor $c_menu 0 0 [
				uiclamp 1 1 1 1
				uispace $UIwindow_padding $UIwindow_padding [	// content of the window
					uiclamp 1 1 1 1
					infotxt = 0
					@@@@arg4
					if $infotxt [showui "infobox"][hideui "infobox"]
				]
				uioutline $c_line ; uiclamp- 1 1 1 1
			]
		]
	] $arg5 [@arg6] // on-show (arg5) on-hide (arg6)
]

// UI_settingslider 1:TXT 2:SETTING 3:VAL 4:MIN 5:MAX 6:INCR 7:[on-change] 8:INFO 9:GYR GRADIENT 10:HSIZE
UI_settingslider = [
	if (! $arg10) [arg10 = 0.4]
	uispace 0 0.0015
	uitarget 0.6 0.05 [
		uihover [setgradsize = 0.038 ; setgradcol = 0xE5DFDB ; infotxt = @arg8] [setgradsize = 0.035 ; setgradcol = 0xDAD4D1]
		uimodhgradient $setgradcol 0xFFFFFF 0.7 0.06 [
			uihlist 0 [
				uispace 0.005
				uitarget 0.36 0.03 [uicolortext @arg1 0x222222 0.6 ; uialign* -1]
				UIhslider @arg2 @arg4 @arg5 @arg6 $arg10 0.025 [uitext @arg3 0.56] [@arg7 ; playUiSound $S_SCROLLUP] [] @arg9
			]
		]
	]
	uispace 0 0.0015
]

//UI_listslider 1:VAR 2:COMPARISON 3:[values] 4:X 5:Y 6:[on-change] 7:(ARROW-%-SIZE) 8:[children]
UI_listslider = [
   if $arg7 [] [arg7 = 0.5]
   uihlist 0 [
      UIarrowbutton (*f $arg5 1.5) $arg5 90 [
         n = ([listfind@arg2] $arg3 $$arg1 1)
         if (< $n 0) [n = 0] [
            n = (- $n 2)
            if (< $n 0) [n = (+ $n (listlen $arg3))]
         ]
         $arg1 = (at $arg3 $n)
         arg6 ; playUiSound $S_CLICK
      ] $arg7
      uivgradient $c_tab_up $c_tab_dn (-f $arg4 (*f $arg5 2)) $arg5 [ 
         uiclamp* 1 1 1 1
         if (=s $arg8 "") [
			uitext (|| [at ([listassoc@arg2] $arg3 $$arg1)] [result "custom"]) 0.56
         ] arg8
      ]
      UIarrowbutton (*f $arg5 1.5) $arg5 270 [
         n = ([listfind@arg2] $arg3 $$arg1 1)
         n = (if (>= $n 0) [mod (+ $n 2) (listlen $arg3)] [- (listlen $arg3) 2])
         $arg1 = (at $arg3 $n)
         arg6 ; playUiSound $S_CLICK
      ] $arg7
   ]
]

// UI_settingcheckbox 1:TXT 2:[on-change] 3:VAL 4:INFO
UI_settingcheckbox = [
	uispace 0 0.0015
	uitarget 0.625 0.05 [
		uihover [setgradsize = 0.038 ; setgradcol = 0xE5DFDB ; infotxt = @arg4] [setgradsize = 0.035 ; setgradcol = 0xDAD4D1]
		uimodhgradient $setgradcol 0xFFFFFF 0.7 0.05 [
			uihlist 0 [
				uispace 0.005
				UIcheckbox @arg3 0.02
				uispace 0.005
				uicolortext @arg1 0x222222 0.6
			]
			uialign* -1
			uirelease [doargs $arg2 ; playUiSound $S_CLICK]
		]
	]
	uialign* -1
	uispace 0 0.0015
]

// UI_settingbutton 1:TXT 2:BUTTONTEXT 3:[on-change] 4:INFO
UI_settingbutton = [
	uispace 0 0.0015
	uitarget 0.625 0.05 [
		uihover [setgradsize = 0.038 ; setgradcol = 0xE5DFDB ; infotxt = @arg4] [setgradsize = 0.035 ; setgradcol = 0xDAD4D1]
		uimodhgradient $setgradcol 0xFFFFFF 0.7 0.05 [
			uihlist 0 [
				uispace 0.005
				uitarget 0.39 0.02 [uicolortext @arg1 0x222222 0.6 ; uialign* -1]
				uihlist 0 [
					UIbutton "hold5" [
						uicolortext @arg2 0x000000 0.6
					] 0.39 0.03 [doargs $arg3]
				]
			]
			uialign* -1
		]
	]
	uialign* -1
	uispace 0 0.0015
]

// UImenu 1:"NAME" 2:[children] 3:[on-show] 4:[on-hide] 5:[ui-children] 6:"TITLE"
UImenu = [
   newui $arg1 [
      @arg5
      uivlist 0 [
         uiclamp 1 1 1 1
		 @@(? (>= $numargs 6) [uifill 0 @arg6][uifill 0 0.23])
         @@(? (>= $numargs 7) [
            uivgradient 0x666666 0x555555 0 0.04 [
               uiclamp 1 1
               uispace 0.003 0.003 [
                  uiclamp 1 1
				  uihlist 0 [
					uiimage [@@@@@arg9] 0.045 0.045
					uispace 0.007
					if @GAME_LANG [uicolortext [@@@@@@arg7] 0xFFFFFF 0.7][uicolortext [@@@@@@arg8] 0xFFFFFF 0.7]
				  ]
				  uialign* -1 0
                  uitarget 0.05 0.05 [
                     uihover [uicolor 0xFF0000 0.04 0.03] [uicolor 0xCC0000 0.04 0.03]
					 uiimage "media/interface/hud/exit.png" 0.026 0.026
                     uihold [uiimage "media/interface/hud/exit.png" 0.028 0.028] ; uirelease [hideui [@@@@@@arg1]]
                  ]; uialign- 1
               ]
            ]
         ])
         uicolor $c_menu 0.01 0.01 [
            uiclamp 1 1 1 1
            uispace $UIwindow_padding $UIwindow_padding [
               uiclamp 1 1 1 1
               @@@@arg2
            ]
			uioutline $c_line; uiclamp- 1 1 1 1
         ]
      ]
   ] $arg3 $arg4
]

infotxt = ""
UImenu "infobox" [
	uihgradient 0xC5BFBB 0xA59F9B 0.5 0.01 [
		uihlist 0 [
			uiimage "media/interface/hud/info.jpg" 0.035 0.035
			uicolortext $infotxt 0x444444 0.5
		]
	]
][][][] 0.79

// UI_Main_Button 1:<Text ID> 2:<On click>
UI_Main_Button = [
	uihgradient 0xEEEEEE 0xBBBBBB 0.03 0.05 [
		uihover [ 
			buttonSize = 0.315 ; fontSize = 0.66 ; triangleSize = 0.022 ; triangleColor = 0x77EE11
		] [
			buttonSize = 0.300 ; fontSize = 0.60 ; triangleSize = 0.020 ; triangleColor = 0x8F8F8F
		]
		UIbutton "hold5" [
			uihlist 0 [
				UItriangle $triangleColor $triangleSize $triangleSize 270
				uispace 0.005
				uicolortext (at $arg1) 0x444444 $fontSize
			]
		] $buttonSize 0.05 [@arg2] -1
	]
]

UI_Pretty_Button = [
	uigroup [
		uihover [ 
			buttonSize = 0.315 ; fontSize = 0.78 ; triangleSize = 0.022 ; triangleColor = 0x77EE11 ; colorUp = 0xB8B896 ; colorDown = 0x959572
		] [
			buttonSize = 0.300 ; fontSize = 0.73 ; triangleSize = 0.020 ; triangleColor = 0xFF5500 ; colorUp = 0x887F7A ; colorDown = 0x707070
		]
		uivgradient $colorUp $colorDown $buttonSize 0.05 [
			UIbutton "none" [
				uitarget $buttonSize 0.05 [
					uihlist 0.012 [
						UItriangle $triangleColor $triangleSize $triangleSize 270
						uitext (at $arg1) $fontSize
						UItriangle $triangleColor $triangleSize $triangleSize 90
					]
				]
			] 0 0 [@arg2] -1
		]
	]
]

UI_Simple_Button = [
	uigroup [
		uihover [ 
			buttonSize = 0.9 ; fontSize = 0.78 ; imgSize = 0.065
		] [
			buttonSize = 0.9 ; fontSize = 0.73 ; imgSize = 0.06
		]
		uihgradient 0xDBD7D7 0xB2B2B9 $buttonSize 0.07 [
			UIbutton "server_yes" [
				uitarget $buttonSize 0.07 [
					uihlist 0.012 [
						uiimage $arg2 $imgSize $imgSize
						uicolortext $arg1 0x444444 $fontSize
						uialign -1
					]
				]
			] 0 0 [@arg3] -1
		]
	]
]

UI_Check_Button = [
	uihgradient 0xEEEEEE 0xBBBBBB 0.03 0.04 [
		UIbutton "hold5" [
			uihlist 0 [
				UIcheckbox $arg3 0.016
				uispace 0.005
				uicolortext (at $arg1 $GAME_LANG) 0x444444 0.6
			]
		] 0.3 0.04 [@arg2] -1
	]
]

UI_outline = [
	UIbar 0 1 ; uialign- -1 0
	UIbar 0 1 ; uialign-  1 0
	UIbar 1 0 ; uialign-  0 -1
	UIbar 1 0 ; uialign-  0 1
]

// UI_Edit_Help 	1:<Text ID> 	2:<Key info> 	3:<Text color>
UI_Edit_Help = [
	helpTextsColor = [0x999999 0x00FF00 0xFFFFFF]
	uihlist 0 [
		uispace 0.005
		uitarget 0.17 0 [
			uicolortext (at $arg1) (at $helpTextsColor $arg3) 0.5
			uialign* -1
		]
		uivlist 0 [
			uigroup [UI_keyref 2 0.03 0.0222 $arg2]
			uialign 1
		]
	]
]

// UI_h_tab 1:(SIZE-W) 2:(SIZE-H) 3:VAR 4:["name" [action] ..] 5:<BUTTON BEHAVIOR: 0=HOVER, 1=PRESS, 2=RELEASE>
UI_h_tab = [
   imgSize = (*f $arg2 0.8)
   uitarget $arg1 (*f $arg2 1.2) [
      uihlist 0 [
         uiclamp 1 1
         looplist3 v n c $arg4 [
            if (=s $$arg3 $v) [
               uivgradient 0xBBBB99 0x999977 0 $arg2 [
                  UIbar 0 1 ; uialign- -1 0
                  UIbar 0 1 ; uialign-  1 0
				  UIbar 1 0 ; uialign-  0 -1
				  UIbar 1 0 ; uialign-  0 1
				  uihlist 0 [
				     uiimage (at @arg6 $v) $arg2 $arg2
                     uispace 0.01 0.015 [uitext $n 0.7]
				  ]
               ]
            ] [
               uivgradient 0x8F8F8F 0x6F6F6F 0 $arg2 [
			      UIbar 0 1 ; uialign- -1 0
                  UIbar 0 1 ; uialign-  1 0
				  UIbar 1 0 ; uialign-  0 -1
				  UIbar 1 0 ; uialign-  0 1
				  uihlist 0 [
				     uiimage (at @arg6 $v) $imgSize $imgSize
                     uispace 0.01 0 [uitext $n 0.7]
				  ]
                  case $arg5 0 [
                     uihover [$arg3 = $v]
                  ] 1 [
                     uipress [$arg3 = $v]
                  ] 2 [uirelease [$arg3 = $v]]
                  do $c
               ]
            ]
         ]
         uiclamp* 1 1
      ]
   ]
]

// UIbar 1:CLAMP-X 2:CLAMP-Y
UIbar = [
   uiline $c_line
   uiclamp- $arg1 $arg1 $arg2 $arg2
]

// UItriangle 1:0xCOLOR 2:(SIZE-W) 3:(SIZE-H) 4:(ORIENTATION)
UItriangle = [
   if $arg1 [] [arg1 = 0xFFFFFF]
   uigroup [
      uitriangle $arg1 $arg2 $arg3 $arg4
      uitriangleoutline $arg1 $arg2 $arg3 $arg4
   ]
]

// UIcircle 1:0xCOLOR 2:(SIZE)
UIcircle = [
   if $arg1 [] [arg1 = 0xFFFFFF]
   uigroup [
      uicircle $arg1 $arg2
      uicircleoutline $arg1 $arg2
   ]
]

// UItitle 1:[title children] 2:(SIZE-W) 3:(SIZE-H)
UItitle = [
   uivgradient $c_cctitleA $c_cctitleB $arg2 $arg3 [
      arg1
      uioutline $c_line
      uiclamp 1 1 1 1
   ]
]

UIsubtitle = [
   uivgradient $c_ccsubtitleA $c_ccsubtitleB $arg2 $arg3 [
      arg1
      uioutline $c_line
      uiclamp 1 1 1 1
   ]
]

// UIbuttonarea 1:(STYLE) 2:(BUTTON-WIDTH) 3:(BUTTON-HEIGHT) 4:[children]
UIbuttonarea = [
   uitarget $arg2 $arg3 [
      if (=s $arg1 "") [
         style_button_default
      ] [[style_button_@arg1]]
      uiclamp* 1 1 1 1
      doargs $arg4
   ]
]

// UIbutton 1:(STYLE) 2:[button-children] 3:(BUTTON-WIDTH) 4:(BUTTON-HEIGHT) 5:[on-release] 6:(ALIGNMENT) 7:[children] 8:(USE-PADDING)
UIbutton = [
   uitarget $arg3 $arg4  [
      doargs $arg7
      if (=s $arg1 "") [
         style_button_default
      ] [[style_button_@arg1]]
      uirelease [playUiSound $S_CLICK ; doargs $arg5]
      uiclamp* 1 1 1 1
      uispace (? (>= $numargs 8) $arg8 $UIbutton_padding) 0 [
         uiclamp 1 1
         uihlist 0.01 [
            uialign $arg6 0
            doargs $arg2
         ]
      ]
   ]
]

// UIcheckbox 1:[condition] 2:(SIZE) 3:0xCOLOR-ON 4:0xCOLOR-OFF 5:[children]
UIcheckbox = [
   if $arg4 [] [arg4 = $c_off]
   if $arg3 [] [arg3 = $c_on1]
   uicolor (? (arg1) $arg3 $arg4) $arg2 $arg2 [
      uimodvgradient 0xFFFFFF 0xB0B0B0
      doargs $arg5
      uioutline $c_line
      uiclamp* 1 1 1 1
   ]
]

// UIradio 1:[condition] 2:(SIZE) 3:0xCOLOR-ON 4:0xCOLOR-OFF 5:[children]
UIradio = [
   if $arg4 [] [arg4 = $c_off]
   if $arg3 [] [arg3 = $c_on2]
   uicircleoutline (? (arg1) $arg3 $arg4) $arg2 [
      uicircle (? (arg1) $arg3 $arg4) $arg2 [
         doargs $arg5
         uiclamp* 1 1 1 1
      ]
   ]
]

// UIarrowbutton 1:(SIZE-W) 2:(SIZE-H) 3:(ORIENTATION) 4:[on-release] 5:(ARROW-%-SIZE) 6:[children]
UIarrowbutton = [
   local arrowsize
   arrowsize = (*f (minf $arg1 $arg2) (minf 1 (maxf 0 $arg5)))
   uivgradient 0x7A7A80 0x4A4A4A $arg1 (*f $arg2 1.2) [
      doargs $arg6
      style_button_hold5
      uirelease [doargs $arg4]
      uiclamp* 1 1 1 1
      UItriangle 0 $arrowsize $arrowsize $arg3
   ]
]

// UIhtab 1:(SIZE-W) 2:(SIZE-H) 3:VAR 4:["name" [action] ..] 5:<BUTTON BEHAVIOR: 0=HOVER, 1=PRESS, 2=RELEASE>
UIhtab = [
   uifill $arg1 $arg2 [
      uihlist 0 [
         uiclamp 1 1
         looplist3 v n c $arg4 [
            if (=s $$arg3 $v) [
               uimodvgradient 0xFFFFFF $c_grdmod 0 $arg2 [
                  UIbar 0 1 ; uialign- -1 0
                  UIbar 0 1 ; uialign-  1 0
                  uispace 0.01 0 [uitext (concatword "^f8" $n) 0.7]
               ]
            ] [
               uitarget 0 $arg2 [
                  uispace 0.01 0 [uitext $n 0.7]
                  case $arg5 0 [
                     uihover [$arg3 = $v]
                  ] 1 [
                     uipress [$arg3 = $v]
                  ] 2 [uirelease [$arg3 = $v]]
                  do $c
               ]
            ]
         ]
         uiclamp* 1 1
      ]
      UIbar 1 0 ; uialign- 0 1
   ]
]

// UIvtab 1:(SIZE-W) 2:(SIZE-H) 3:VAR 4:["name" [action] ..] 5:<BUTTON BEHAVIOR: 0=HOVER, 1=PRESS, 2=RELEASE>
UIvtab = [
   uifill $arg1 $arg2 [
      uivlist 0 [
         uiclamp 0 0 1 1
         looplist3 v n c $arg4 [
            if (=s $$arg3 $v) [
               uimodhgradient 0xFFFFFF $c_grdmod $arg1 0 [
                  UIbar 1 ; uialign- 0 -1
                  UIbar 1 ; uialign- 0  1
                  uispace 0.01 0 [uitext (concatword "^f8" $n) 0.7]
               ]
            ] [
               uitarget $arg1 0 [
                  uispace 0.01 0 [uitext $n 0.7]
                  case $arg5 0 [
                     uihover [$arg3 = $v]
                  ] 1 [
                     uipress [$arg3 = $v]
                  ] 2 [uirelease [$arg3 = $v]]
                  do $c
               ]
            ]
         ]
         uiclamp* 1 1 1 1
      ]
      UIbar 0 1 ; uialign- 1
   ]
]

// UIvscroll 1:(SIZE-W) 2:(SIZE-H) 3:(SCROLLSPEED) 4:(ARROW-%-SIZE)
UIvscroll = [
   if $arg4 [] [arg4 = 0.5]
   uivlist 0 [
      uiscrollarrow (-f 0 $arg3) [UIarrowbutton $arg1 $arg1 0 [] $arg4]
      uivscrollbar [
         uifill $arg1 (-f $arg2 (*f $arg1 2)) [uicolor $c_slider ; uiclamp- 1 1 1 1]
         uiscrollbutton [
            uicolor $c_menu $arg1
            style_button_hold5
            uiclamp* 1 1 1 1
            style_generic_linesh 3 0.005 (*f $arg1 0.5)
         ]
      ]
      uiscrollarrow $arg3 [UIarrowbutton $arg1 $arg1 180 [] $arg4]
   ]
]

// UIhscroll 1:(SIZE-W) 2:(SIZE-H) 3:(SCROLLSPEED) 4:(ARROW-%-SIZE)
UIhscroll = [
   if $arg4 [] [arg4 = 0.5]
   uihlist 0 [
      uiscrollarrow (-f 0 $arg3) [UIarrowbutton $arg2 $arg2 90 [] $arg4]
      uihscrollbar [
         uifill (-f $arg1 (*f $arg2 2)) $arg2 [uicolor $c_slider ; uiclamp- 1 1 1 1]
         uiscrollbutton [
            uicolor $c_menu 0 $arg2
            style_button_hold1
            uiclamp* 1 1 1 1
            style_generic_linesv 3 0.004 (*f $arg2 0.5)
         ]
      ]
      uiscrollarrow $arg3 [UIarrowbutton $arg2 $arg2 270 [] $arg4]
   ]
]

// UIvslider 1:VAR 2:MIN 3:MAX 4:STEP 5:X 6:Y 7:[children] 8:[on-change] 9:(ARROW-%-SIZE)
UIvslider = [
   if $arg9 [] [arg9 = 0.5]
   uivlist 0 [
      uisliderarrow -1 [UIarrowbutton (*f $arg5 0.8) (*f $arg5 1.5)  0 [] $arg9]
      uihslider $arg1 (minf $arg3 $arg2) (maxf $arg2 $arg3) $arg4 $arg8 [
         uifill (-f $arg6 (*f $arg5 2)) $arg5 [uicolor $c_slider ; uiclamp- 1 1 1 1]
         uisliderbutton [
            uicolor $c_menu $arg5
            style_button_hold5
            uiclamp* 1 1 1 1
            uispace (? (>= $numargs 10) $arg11 $UIbutton_padding) 0 [arg7]
         ]
      ]
      uisliderarrow 1 [UIarrowbutton (*f $arg5 0.8) (*f $arg5 1.5)  180 [] $arg9]
   ]
]

// UIhslider 1:VAR 2:MIN 3:MAX 4:STEP 5:X 6:Y 7:[children] 8:[on-change] 9:(ARROW-%-SIZE) 10:GYR GRADIENT
UIhslider = [
   if $arg9 [] [arg9 = 0.5]
   uihlist 0 [
      uisliderarrow -1 [UIarrowbutton (*f $arg6 1.5) (*f $arg6 0.9) 90 [] $arg9]
      uihslider $arg1 $arg2 $arg3 $arg4 $arg8 [
		 uivlist 0 [
			 uioutline 0x555555
			 uiclamp- 1 1 1 1
			 if $arg10 [
				uihlist 0 [
					uimodhgradient 0x77FF77 0xFFFF77 (*f $arg5 0.437) (*f $arg6 0.6)
					uimodhgradient 0xFFFF77 0xFF7777 (*f $arg5 0.437) (*f $arg6 0.6)
				]
			 ][
			 	uifill (-f $arg5 (*f $arg6 2)) (*f $arg6 0.3) [uimodvgradient 0x999999 0xCCCCCC ; uiclamp- 1 1 1 1]
				uifill (-f $arg5 (*f $arg6 2)) (*f $arg6 0.3) [uimodvgradient 0xCCCCCC 0x999999 ; uiclamp- 1 1 1 1]
				
			 ]
			 uioutline 0x555555
			 uiclamp- 1 1 1 1
		 ]
		 
         uisliderbutton [
            uicolor $c_menu 0 $arg6
            style_button_hold5
            uiclamp* 1 1 1 1
            uispace (? (>= $numargs 11) $arg11 $UIbutton_padding) 0 [arg7]
         ]
      ]
      uisliderarrow 1 [UIarrowbutton (*f $arg6 1.5) (*f $arg6 0.9) 270 [] $arg9]
   ]
]

// UIvcolorslider 1:VAR 2:MIN 3:MAX 4:0xCOLOR1 5:0xCOLOR2 6:X 7:Y 8:[on-change]
UIvcolorslider = [
   uigroup [
      uivgradient $arg5 $arg4 (*f 0.85 $arg6) $arg7 [uioutline $c_line ; uiclamp- 1 1 1 1]
      uifont "default_outline" [uivlist -0.01 [
         loop i (strlen (abs $$arg1)) [
            uicolortext (substr $$arg1 $i 1) (? (|| uihover? uihold?) 0x80FFFFFF 0x40FFFFFF) 0.8
         ]
      ]]
      uivslider $arg1 $arg2 $arg3 1 $arg8 [
         uifill $arg6 (*f 0.98 $arg7)
         uisliderbutton [uiline 0xFFFFFF $arg6 0]
      ]
   ]
]

// UIhcolorslider 1:VAR 2:MIN 3:MAX 4:0xCOLOR1 5:0xCOLOR2 6:X 7:Y 8:[on-change]
UIhcolorslider = [
   uigroup [
      uihgradient $arg4 $arg5 $arg6 (*f 0.85 $arg7) [uioutline $c_line ; uiclamp- 1 1 1 1]
      uihslider $arg1 $arg2 $arg3 1 $arg8 [
         uifill (*f 0.98 $arg6) $arg7
         uisliderbutton [uiline 0xFFFFFF 0 $arg7]
      ]
   ]
]
