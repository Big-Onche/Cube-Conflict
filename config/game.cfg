// Game specific config stuff goes here
// List of local maps (server maps in server-init.cfg)
localMaps = [village factory moon castle volcano]

// Gamemodes selection
defvarp game_mode 0 0 5
defvarp muta_mode 1 1 5
final_mode = 0

calcmode = [
	if (= $game_mode 4) [final_mode = -3][
		if (= $game_mode 5) [final_mode = -2][
			final_mode = 1
			final_mode = (+ (+ $final_mode (* $game_mode 4)) $muta_mode)
		]
	]
]

// Pause toggle & escape
togglemainmenu = [
	if ($isconnected) [
		if (uivisible "main") [
			hideallui
			paused $FALSE
			if ($editing) [edittoggled]
		] [
			hideallui
			paused $TRUE
			showui "main"
		]
	] [
		local nextUi = "main"
		if (uivisible "translationwarning") [ if ($veryfirstlaunch) [nextUi = "firstlaunch"] [nextUi = "settings"] ]
		if (uivisible "editwarning") [nextUi =  "mapeditor"]
		if (|| (uivisible "privacy") (uivisible "rules") (uivisible "disconnected")) [nextUi = "multiplayer"]
		if (uivisible "changes") [hideui "changes"] [ if (! (uivisible "main")) [hideallui ; showui $nextUi] ]
	]
]

// Spectator
nextspecmode = [
   if $specmode [
      specmode (mod (+ $specmode 1) 3)
   ] [
      specmode (? (>= (getfollow) 0) 2 1)
   ]
]

defvarp zoomtoggle 0 0 1
dozoom = [if $zoomtoggle [zoom (? $zoom -1 1)] [zoom 1 ; onrelease [zoom -1]]]

defvarp dial -1 0 1
interact = [if (! (uivisible "dial"))[dial 1 ; sleep 0 [dial 0]]]

defvarp wieldtoggle 0 0 1
doautowield = [if $wieldtoggle [autowield (? $autowield -1 1)] [autowield 1 ; onrelease [autowield -1]]]

sayteamcommand = [inputcommand $arg1 [sayteam $commandbuf] "[team]"]

demo = [stopdemo; if (mode -1) [map $arg1]]
varcomplete demo demodir dmo

allowedittoggle = [result 1]

UI_menutabs = [5]
UI_menupausetabs = [0]

songs = ["songs/tutorial.ogg" "songs/village.ogg" "songs/factory.ogg" "songs/moon.ogg" "songs/castle.ogg" "songs/volcano.ogg"]

playmapsong = [music (at $songs $map_sel) 5]

///////////////////////////////////////////////////////////////////////////////
//  Spy disguise default config                                            //
///////////////////////////////////////////////////////////////////////////////
alias disguise_0 "mapmodel/caisses/caisse1"
alias disguise_1 "mapmodel/tonneau"
alias disguise_2 "mapmodel/panneau"
alias disguise_3 "mapmodel/distributeur"

///////////////////////////////////////////////////////////////////////////////
//  Universal Mouse Scrollwheel                                              //
///////////////////////////////////////////////////////////////////////////////
ums_modifier    = 0
ums_setmodifier = [ums_modifier = $arg1; onrelease [ums_modifier = 0]]
ums_delta       = [[ums_delta_@(? $editing "edit" "game")_@ums_modifier] $arg1]
ums_delta_game_0 = [if $fullconsole [conskip (* $arg1 3)] [nextweapon $arg1]]
ums_delta_edit_0  = [if $fullconsole [conskip (* $arg1 3)] [if $blendpaintmode [nextblendbrush $arg1] [editfacewsel $arg1 1]]]  // Console scroll -or- Change blend brush -or- Push/pull geometry
ums_delta_edit_1  = [nodebug [gridpower (+ $arg1 $gridpower)]]                       // Grid size
ums_delta_edit_2  = [editfacewsel $arg1 0]                                           // Push face/corners selected
ums_delta_edit_3  = [editfacewsel $arg1 2]                                           // Push corner pointed at by cursor
ums_delta_edit_4  = [if $blendpaintmode [rotateblendbrush $arg1] [editrotate $arg1]] // Geometry rotate -or- blend brush rotate
ums_delta_edit_5  = [edittex $arg1]                                                  // Change textures
ums_delta_edit_6  = [selecthbrush $arg1]                                             // Change heightmap brush
ums_delta_edit_7  = [vdelta [voffset (* $arg1 $delta_multiplier2) 0]]               // vSlot: offset H
ums_delta_edit_8  = [vdelta [voffset 0 (* $arg1 $delta_multiplier2)]]               // vSlot: offset V
ums_delta_edit_9  = [vdelta [vrotate $arg1]]                                         // vSlot: rotate
ums_delta_edit_10 = [vdelta [vscale (if (< $arg1 0) 0.5 2)]]                         // vSlot: scale
ums_delta_edit_11 = [entproperty 0 (* $arg1 $delta_multiplier1)]
ums_delta_edit_12 = [entproperty 1 (* $arg1 $delta_multiplier1)]
ums_delta_edit_13 = [entproperty 2 (* $arg1 $delta_multiplier1)]
ums_delta_edit_14 = [entproperty 3 (* $arg1 $delta_multiplier1)]
ums_delta_edit_15 = [entproperty 4 (* $arg1 $delta_multiplier1)]
ums_delta_edit_16 = [entautoview $arg1]
delta_multiplier1 = 1
delta_multiplier2 = 16